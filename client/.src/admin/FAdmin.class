' Gambas class file
' Copyright (C) 2008,2009,2010 Drs. Richard Terry, Ian Haywood

' This program is free software: you can redistribute it and/or modify
' it under the terms of the GNU General Public License as published by
' the Free Software Foundation, either version 3 of the License, or
' (at your option) any later version.

' This program is distributed in the hope that it will be useful,
' but WITHOUT ANY WARRANTY; without even the implied warranty of
' MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
' GNU General Public License for more details.

' You should have received a copy of the GNU General Public License
' along with this program.  If not, see <http://www.gnu.org/licenses/>.
'-----------------------------------------------------------------------------------------
' PURPOSE         The main admin module which as the name suggests is
'                 for administrative functions
'                 FGlobalPreferences = pathways etc, global to program
'                 FImportLegacyData = import legacy data - not functioning
'                 FRoomSetup = setup for room peculiar settings eg printers
'                 FAdminRequests = set up who we send requests to, maintain request names
'                 FImportPostcodes = An (Austrlian at the moment) postcode import wizard
'                 FHL7Management = set up providers sending us HL7 or scanned docs
'                 FIXME this is then a misnomer
'                 FAdminVaccinations = set up vaccination schedules, add vaccine names
'HOW THIS WORKS   User clicks on the 'Admin' button on the main button bar
'                 FMain.btnSection_Admin_Click(), which instantiates this form and
'                 calls (Init) where only FGlobalPreferences is instantiated. All other
'                 forms are loaded when a particular tab is clicked on.
'SAVING SETTINGS  As this and its child forms are never 'closed' here, when the program
'                 finally quites, FMain.Settings_Save will save any settings changes
'                 There are no settings on this form to load.
'BUGS             Please report to rterry@pacific.net.au
'------------------------------------------------------------------------------------------
'Constants to describe the tabs on the main form
'-----------------------------------------------
Private Const cGlobalPreferences As Integer = 0                '
Private Const cImport As Integer = 1
Private Const cExport As Integer = 2
Private Const cClinicRooms As Integer = 3
Private Const cRequestMaintenance As Integer = 4
Private Const cHL7Tools As Integer = 5
Private Const cVaccination As Integer = 6
Private Const cDeveloper As Integer = 7
'-------------------------------------------------
'Constants to describe the tabs on tabstrip_import
'-------------------------------------------------
Private Const cPatientDemographicData As Integer = 0    
Private Const cCodingSystemData As Integer = 1   
Private Const cDrugData As Integer = 2   
Private Const cLegacyClinicalData As Integer = 3
Private Const cPostcodeData As Integer = 4   
'----------------------------     
'Declare forms to later embed
'----------------------------  
Private form_global_preferences As FGlobalPreferences  
Private form_import_patients As FImportPatientsWizard
Private form_developer_only As FDevelop 'only for richard's use REMOVE ME AT SOME STAGE RICHARD
Private form_import_legacy As FImportLegacyData
Private form_room_setup As FRoomSetup
Private form_admin_requests As FAdminRequests
Private form_postcodes As FImportPostcodes
Private form_HL7Management As FHL7Management
Private form_admin_vaccines As FAdminVaccinations

Public Sub Init()
   '---------------------------------------------------------------
   'Forms loaded only 'on-demand', so only want the one we see first
   'this routine is called from FMain.btnSection_Admin_Click()
   '----------------------------------------------------------------

   With form_global_preferences = New FGlobalPreferences(Vbox_GlobalPreferences)
      .Init()
   End With
   
End

Public Sub Settings_Save()
   '--------------------------------------------------------------------------
   'This is only ever called on program termination from FMain.Settings_Save()
   '--------------------------------------------------------------------------   

   Try form_import_patients.Settings_Save()
   Try form_global_preferences.Settings_Save()
   Try form_import_legacy.Settings_Save()
   Try form_admin_requests.Settings_Save() 'may not be loaded
   Try form_HL7Management.Settings_Save()
   Try form_admin_vaccines.Settings_Save()
   
End

Public Sub TabStrip1_Click()
   '--------------------------------------------------------------------
   'This is the main tabstrip on this form, load other forms 'on demand'
   '--------------------------------------------------------------------   
   Inc Application.Busy
   Select Case Tabstrip1.Index
         
      Case cImport
         If IsNull(form_import_patients) Then
            form_import_patients = New FImportPatientsWizard(Vbox_ImportPatients)
         End If
      Case cExport     
         
      Case cClinicRooms
         If IsNull(form_room_setup) Then
            form_room_setup = New FRoomSetup(Vbox_RoomSetup)
         End If
         
      Case cRequestMaintenance
         If IsNull(form_admin_requests) Then
            form_admin_requests = New FAdminRequests(Vbox_Requests)
            With form_admin_requests
               .Init()
            End With
         End If
      Case cHL7Tools
         If IsNull(form_HL7Management) Then
            With form_HL7Management = New FHL7Management(HBox_HL7Tools)
               .Init()
            End With
         Endif
      Case cVaccination
         If IsNull(form_admin_vaccines) Then
            form_admin_vaccines = New FAdminVaccinations(HBox_Vaccinations)
            form_admin_vaccines.Init()
         End If
         
      Case cDeveloper
         If IsNull(form_developer_only) Then
            form_developer_only = New FDevelop(HBox_danger)
         End If
         
   End Select
   Dec Application.Busy
End

Public Sub tabstrip_Import_Click()
   '-------------------------------------------
   'User has selected  Import from the main tab
   '-------------------------------------------
   Inc Application.Busy
   Select Case Last.Index
      Case cCodingSystemData
      Case cDrugData 
      Case cLegacyClinicalData
         If IsNull(form_import_legacy) Then
            form_import_legacy = New FImportLegacyData(Vbox_ImportLegacy)
         End If
         
      Case cPostcodeData
         If IsNull(form_postcodes) Then
            form_postcodes = New FImportPostcodes(Vbox_ImportPostcodes)
         End If
         
   End Select
   Dec Application.Busy
End
