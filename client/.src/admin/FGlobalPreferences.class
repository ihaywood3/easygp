' Gambas class file

' Copyright (C) 2008-2012 Drs. Richard Terry, Ian Haywood
' This program is free software: you can redistribute it and/or modify
' it under the terms of the GNU General Public License as published by
' the Free Software Foundation, either version 3 of the License, or
' (at your option) any later version.
' This program is distributed in the hope that it will be useful,
' but WITHOUT ANY WARRANTY; without even the implied warranty of
' MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
' GNU General Public License for more details.

' You should have received a copy of the GNU General Public License
' along with this program.  If not, see <http://www.gnu.org/licenses/>.
' A module to set global preferences for easyGP eg paths to documents etc
'-------------------------------------------------------------------------
'PURPOSE       Enable admin to set basic stuff like, path to hl7, set path
'              where scanned documents are kept
'-------------------------------------------------------------------------

Private help As Collection
Private help_dir As String
Private Form_directorychooser As FDirectoryChooser
Private fk_clinic As Integer
Private bExit As Boolean
Private document_fk_lu_archive_site As Integer
Private staff_members As Collection
Private staff_member As Collection 
Private FHelp As FHtmlViewer 

Public Sub Init()
   '-----------------------------------------------------
   'Basic housekeeping, this is called from FAdmin.Init()
   '-----------------------------------------------------  
   
  staff_members = modUtil.LoadCombo(cmbStaffMembers, modInboxDBI.Inbox_Staff_Get(), "wholename")
   With FHelp = New FHtmlViewer(Vbox_Help)
      .tbWebBrowserZoomIn.Visible = True
      .tbWebBrowserZoomOut.Visible = True  
   End With
   
   Try Settings_Load()
   help_dir = Application.Path &/ "help/"
   lblmeasure.text = "  Path to libary files (network)   "
   modEditAreaHelpers.Resize_labels(VBox_EditArea, lblmeasure)
   modEditAreaHelpers.Resize_label(lblDefaultStaffMember, lblmeasure)
   cvwGeneralPreferences.columns.count = 3
   cvwGeneralPreferences.Columns[0].width = 150
   cvwGeneralPreferences.Columns[1].width = 200
   cvwGeneralPreferences.Columns[2].width = 300
   modEditAreaHelpers.Help_Show("global-preferences.html", FHelp)
   Reload()
   Preference_new()
   
End

Public Sub Save()
   '----------------------------------------------------------------
   ' Saves the global preferences
   'globalpreference!document_fk_lu_archive_site  set by radiobutton
   '----------------------------------------------------------------   

   If Not (HBox_EditArea.Padding) Then Return       'no padding = nothing changed     
   If Not EditArea_Data_Valid() Then                'ensure minimal valid data
      Return 
   Else   
      HBox_EditArea.Enabled = False  
      modDBConnect.exec_query("lock admin.global_preferences in exclusive mode") ' implictly begins a transaction 
      modAdminDBI.Config_Save(fk_clinic, "document_scanning_directory", Trim(txtFilePathScannedDocuments.text))
      modAdminDBI.Config_Save(fk_clinic, "document_archiving_directory", Trim(txtFilePathArchivedDocuments.text))
      modAdminDBI.Config_Save(fk_clinic, "document_save_to_database", CBool(chkDocumentSaveToDatabase.value))
      modAdminDBI.Config_Save(fk_clinic, "hl7_root_directory", Trim(txthl7RootDirectory.text))
      modAdminDBI.Config_Save(fk_clinic, "fk_hl7_fallback_staff_member", staff_members[cmbStaffMembers.index]!fk_staff)
      modAdminDBI.Config_Save(fk_clinic, "document_fk_lu_archive_site", document_fk_lu_archive_site)
      modAdminDBI.Config_Save(fk_clinic, "library_directory_network", Trim(txtLibraryPathNetwork.text))
      modDBConnect.CommitTrans()
      EditArea_Notify_Datachanged(False)
      modAdminDBI.Config_Refresh()
     
      Reload()
   End If
   
End

Public Sub Reload()
   
   Dim clinic As Collection
   Dim x As Integer
   
   cvwGeneralPreferences.Clear()
   For Each clinic In modAdminDBI.Clinics_Get()
      x = clinic!pk_view
      cvwGeneralPreferences.Add(x, 0)
      cvwGeneralPreferences[x][0] = clinic!branch
      cvwGeneralPreferences[x][1] = Trim(clinic!street1 & " " & clinic!street2)
      cvwGeneralPreferences[x][2] = clinic!town & " " & clinic!postcode
   Next
   
End

Public Function EditArea_Data_Valid() As Boolean
   '-------------------------------------------
   'make sure directories etc are valid paths
   '------------------------------------------- 
   
   If Not IsDir(txtFilePathScannedDocuments.text) Then 
      Message.Info("Please supply a valid path for the archived documents files.\n\nYou may browse for a file path by clicking the browse button next to this textbox")
      txtFilePathScannedDocuments.SetFocus()
      Return False
   End If
   If Not IsDir(txtFilePathArchivedDocuments.text) Then 
      Message.Info("Please supply a valid path for the archived documents files.\n\nYou may browse for a file path by clicking the browse button next to this textbo")
      txtFilePathArchivedDocuments.SetFocus()
      Return False
   End If
   If Not staff_members.Exist(cmbStaffMembers.Index) Then 
      Message.Info("Please specify a staff member to receive unmatched results")
      cmbStaffMembers.SetFocus()
      Return False
   End If
   If Not fk_clinic Then
      Message.info("Please select a clinic you wish to apply these global preferences to\nfrom the combo box at the bottom")
      cvwGeneralPreferences.SetFocus() 
      Return False
   End If   
   'If Not Then
   '  Message.Info("Please select site of storage, local or on the web")
   'Return False 
   'End If
   Return True
   
End

Public Sub EditArea_Notify_Datachanged(flag As Boolean)
   '--------------------------------------------------
   'Adds or removes padding around edit area 
   'to indicate if data is changed and unsaved
   '--------------------------------------------------
   
   If flag Then
      HBox_EditArea.Padding = 1
      HBox_EditArea.Background = Color.red
      tbSave.Foreground = color.red 
      
   Else
      HBox_EditArea.Padding = 0
      HBox_EditArea.Background = Color.white
      tbSave.Foreground = color.black
   End If
   
End 

Public Sub EditArea_TextBox_Activate()
   
   If Bexit Then Return 
   Select Case Last.tag
      Case "scanned document path"
         txtFilePathArchivedDocuments.SetFocus()
      Case "archived document path"
   End Select
   
End

Public Sub EditArea_TextBox_LostFocus()
   
   Last.BackGround = Color.white
   Last.pos = 0
   
End

Public Sub EditArea_TextBox_GotFocus()
   
   If Last.enabled Or Last.readonly Then Return
   Last.BackGround = Color.rgb(95, 255, 175)
   
End

Public Sub EditArea_TextBox_Change()
   
   EditArea_Notify_Datachanged(True)
   
End

Public Sub Settings_Save()
   
   Settings["Admin_GlobalSettings/HSplit_GlobalSettings.layout"] = HSplit_GlobalSettings.layout
   Settings["Admin_GlobalSettings/VSplit_EditArea.layout"] = VSplit_EditArea.layout
   Settings["Admin_GlobalSettings/Help_Zoom"] = FHelp.WebView1.Zoom

End

Private Sub Settings_Load()
   
   Try HSplit_GlobalSettings.layout = Settings["Admin_GlobalSettings/HSplit_GlobalSettings.layout"] 
   Try VSplit_EditArea.layout = Settings["Admin_GlobalSettings/VSplit_EditArea.layout"]
   Try FHelp.WebView1.Zoom = Settings["Admin_GlobalSettings/Help_Zoom"]

End

Public Sub Preference_Show()
   '----------------------------------------------------------------------------------------
   'User has selected one of the clinics, show the defaults for that clinic at it's address
   '----------------------------------------------------------------------------------------
   
   Dim clinic_data As Collection
   
   If Not cvwGeneralPreferences.count Then Return 
   
   cvwGeneralPreferences.MoveCurrent
   Preference_new()
   fk_clinic = cvwGeneralPreferences.Item.Key
   clinic_data = modAdminDBI.Clinics_Get()[fk_clinic]
   lblOrganisation.Text = "Global Preferences for " & clinic_data!organisation
   txtOrganisation.text = clinic_data!organisation
   txtBranch.text = clinic_data!branch
   txtBranch_Street1.Text = clinic_data!street1
   txtBranch_Street2.Text = clinic_data!street2
   txtBranch_Suburb.Text = clinic_data!town
   txtBranch_State.Text = clinic_data!state
   txtBranch_Postcode.Text = clinic_data!postcode
   chkDocumentSaveToDatabase.value = modAdminDBI.Config_Get("document_save_to_database", True, Null, fk_clinic)
   
   bExit = True
   document_fk_lu_archive_site = modAdminDBI.Config_Get("document_fk_lu_archive_site", const.globalpreference_archive_filesystem, Null, fk_clinic)
   Select Case document_fk_lu_archive_site
      Case const.globalpreference_archive_filesystem
         rbStoreInfilesystem.value = True
      Case Const.globalpreference_archive_web
         rbStoreWeb.value = True
   End Select
   
   txtFilePathScannedDocuments.text = modAdminDBI.Config_Get("document_scanning_directory", "", Null, fk_clinic)
   txtFilePathArchivedDocuments.text = modAdminDBI.Config_Get("document_archiving_directory", "", Null, fk_clinic)
   txthl7RootDirectory.text = modAdminDBI.Config_Get("hl7_root_directory", "", Null, fk_clinic)
   Try cmbStaffMembers.index = modAdminDBI.Config_Get("fk_hl7_fallback_staff_member", -1, Null, fk_clinic)
   Try txtLibraryPathNetwork.text = modAdminDBI.Config_Get("library_directory_network", "", Null, fk_clinic)
   
   bExit = False  
   EditArea_Notify_Datachanged(False)
   HBox_EditArea.Enabled = True
  
   
End

Public Sub Preference_new()
   
   bexit = False
   modEditAreaHelpers.EditArea_Clear(VBox_EditArea)
   fk_clinic = 0
   document_fk_lu_archive_site = 0
   rbStoreInfilesystem.value = True
   lblOrganisation.Text = "Branch"
   EditArea_Notify_Datachanged(False)
   bExit = False
   
End

Public Sub EditArea_Buttons_Click()
   
   Dim hCtrl As Control
   Dim aTextbox As TextBox
   
   If bexit Then Return 
   Select Case Last.tag
      Case "save"  
         Save()
      Case "new"
         Preference_new()
      Case "Path to find scanned documents", "Path to place archived documents", "Path to library files network", "Path to library files local"
         
         Form_directorychooser = New FDirectoryChooser(Me)
         With Form_directorychooser 
            .top = VSplit_EditArea.top + Last.parent.parent.top + Last.parent.height
            For Each hctrl In Last.parent.children
               If hctrl Is Textbox Then
                  aTextbox = hctrl
                  Form_directorychooser.tag = aTextbox
                  Break
               End If
            Next
            .left = VSplit_EditArea.left + aTextbox.parent.Left
            .width = aTextbox.Width
         End With
         Form_directorychooser.Visible = True  
         Form_directorychooser.ShowModal()
   End Select
   
End

Public Sub EditArea_RadioButtons_Click()
   
   If bExit Then Return 
   
   document_fk_lu_archive_site = Last.tag
   EditArea_Notify_Datachanged(True)
   
End

Public Sub cvwGeneralPreferences_Select()
   
   Preference_Show()
   
End

Public Sub cmbStaffMembers_Click()
   
   EditArea_Notify_Datachanged(True)
   
End

Public Sub EditArea_CheckBox_Click()
   
   EditArea_Notify_Datachanged(True)

End

Public Sub HSplit_GlobalSettings_Resize()
   
   modEditAreaHelpers.Resize_labels(VBox_EditArea, lblmeasure)
   
End

Public Sub mnuGeneralPreferences_Click()
   
   Select Case Last.tag
      Case "edit"
         HBox_EditArea.Enabled = True 
      Case "font"
         modUtil.Columnview_SetFont(cvwGeneralPreferences, "Admin_GlobalSettings")
   End Select
   
End

Public Sub cvwGeneralPreferences_Menu()
   
   If cvwGeneralPreferences.count Then
      mnuGeneralPreferences.Popup() 
   Endif
   
End
