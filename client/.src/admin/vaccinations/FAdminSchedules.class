' Gambas class file

' Copyright (C) 2008-2011 Dr. Richard Terry

' This program is free software: you can redistribute it and/or modify
' it under the terms of the GNU General Public License as published by
' the Free Software Foundation, either version 3 of the License, or
' (at your option) any later version.

' This program is distributed in the hope that it will be useful,
' but WITHOUT ANY WARRANTY; without even the implied warranty of
' MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
' GNU General Public License for more details.

' You should have received a copy of the GNU General Public License
' along with this program.  If not, see <http://www.gnu.org/licenses/>.
'---------------------------------------------------------------------------------
' PURPOSE         A module to assist in maintenance of vaccinations, schedules etc
' TERMINOLOGY     Which may be unfamiliar to non-australians:
'                 TSI = Torres Straight Islanders eg bScheduleIsAboriginalTSI_Only flag
'                 as government policy identifies these as special groups
'                 My terminology of 'multiple vaccines' could be confusing
'                 This means that a schedule eg 2 month childhood, has multiple
'                 separate vaccines. this is not the same as a course of vaccination
'                 say for hepatitis B given by itself which has 1 at each visit
'                 but on three occasions.
' TODO            All the key-press> set focus stuff in the edit area
'                 Craig Barnetts comments:
' > Atsi is just one of a series of "conditional schedules" ....eg aspleenic
' > individuals, diabetics etc need pneumovax , bat handlers need rabies etc
' > etc ...not sure if you wish to go so far as to have systems to trigger
' > conditional responses ....eg Dx splenectomy ===> HIB / pneumovax ?  OR
' > Already given elsewhere (Tick box +/- date)

' KNOWN BUGS      in Schedule_save() when restoring the row, dosn't move the marquee
'                 to make visible if below what's showing in the list.
'---------------------------------------------------------------------------------
Private bexit As Boolean                  'prevent entry to subroutines as needed
Private bKeyValid As Boolean              'if True this is a valid key press in edit area
Private schedules As Collection           'All the vaccination schedules
Private schedule As Collection            'A single vaccination schedule
Private fk_schedule As Integer            'key to clin_vaccinations.schedules
Private fk_vaccine As Integer
Private fk_season As Integer              'key to common.lu_seasons   
Private pk_view As Integer
Private bScheduleFemaleOnly As Boolean            'if true the vaccine is only for females  
Private bScheduleIsAboriginalTSI_Only As Boolean    'if true the schedule is only for this group
Private bScheduleStatus As Boolean        'if true the schedule is in current use
Private Seasons As Collection             'see common.lu_seasons  
Private Vaccines_in_schedule As Collection 'all the vaccines in a particular schedule
Private Vaccines As Collection            'A collection of vaccines data
Private Vaccine As Collection             'A Single vaccine 
Private FHelp As FHtmlViewer
Private Form_PDF As FPdf
Private help_dir As String
Private Const cShow_Active_Schedules As Integer = 1
Private Const cShow_InActive_Schedules As Integer = 2
Private Const cShow_All_Schedules As Integer = 3
Private iDisplay_Schedules As Integer = 1    'default to showing only active schedules
Private bMultipleVaccinesInSchedule As Boolean       'If true the vaccination schedule has more than 1 vaccine 
Private sLibraryRootDir As String 
Private CurrentVaccine As Collection
Private pk_view_vaccine As String  
Private Default_Zoom As Integer

Public Sub Init()
   
   FHelp = New FHtmlViewer(Hbox_Help)
   help_dir = Application.Path &/ "help/"
   sLibraryRootDir = User.home & "/easygp/library/Vaccination"
  
   With Form_PDF = New FPdf(HBox_Scheduies)
      .BtOpen.Visible = False                      'don't allow external files to be opened
      .btZoomIn.Visible = True
      .btZoomOut.Visible = True  
      .BtPrint.Visible = True 
   End With
   Show_Help(True)
   seasons = modUtil.LoadCombo(cmbSeasons, modCommonDBI.Seasons_Get(), "season")
   lblMeasure.text = " Multiple Vaccines at Once "
   modEditAreaHelpers.Resize_labels(Vbox_EditArea_Inner, lblMeasure)
   modEditAreaHelpers.Resize_label(lblbuttons, lblMeasure)
   modEditAreaHelpers.Resize_label(lblFindVaccine, lblMeasure)
   columnview1.Columns.count = 3 'brand/form/description see Vaccines_Get()
   Settings_Load()
   cmbStateVaccinationsSchedules.Add("Australian Schedule")
   cmbStateVaccinationsSchedules.Add("ACT Schedule")
   cmbStateVaccinationsSchedules.Add("NSW Schedule")
   cmbStateVaccinationsSchedules.Add("NT Schedule")
   cmbStateVaccinationsSchedules.Add("TAS Schedule")
   cmbStateVaccinationsSchedules.Add("VIC Schedule")
   cmbStateVaccinationsSchedules.Add("QLD Schedule")
   cvwVaccines.Columns.count = 5
   cmbStateVaccinationsSchedules.index = 0
   Schedule_Show_For_State()
   
   rbDisplaySchedulesActive.value = True 'will reload
   
End

Public Sub Schedule_Show_For_State()
   
   Dim prefix As String
   Dim charpos As Integer = InStr(cmbStateVaccinationsSchedules.text, " ")
   
   prefix = Lower(Trim(Left(cmbStateVaccinationsSchedules.text, InStr(cmbStateVaccinationsSchedules.text, " "))))
   Try Form_PDF.hPdf.Open(sLibraryRootDir & "/" & prefix & "_" & "immunisation_schedule.pdf")
   If Not Error Then
      Form_PDF.Set_Filename(sLibraryRootDir & "/" & prefix & "_" & "immunisation_schedule.pdf")
      Form_PDF.Render_From_Another_Form()
   End If
   
End

Public Sub Settings_Save()
   
   Settings["Admin_Vaccination_Schedules/VSplit_Main.layout"] = Vsplit_Main.layout
   Settings["Admin_Vaccination_Schedules/HSplit_Main.Layout"] = HSplit_Main.Layout
   Settings["Admin_Vaccination_Schedules/lvwSchedules.font"] = lvwSchedules.Font.ToString()
   Settings["Admin_Vaccination_Schedules/cvwVaccines.font"] = cvwVaccines.Font.ToString()
   Settings["Admin_Vaccination_Schedules/Form_PDF.Zoom"] = Form_PDF.CurrentZoom
   
End

Private Sub Settings_Load()
   
   Try HSplit_Main.Layout = Settings["Admin_Vaccination_Schedules/HSplit_Main.Layout"]
   Try Vsplit_Main.layout = Settings["Admin_Vaccination_Schedules/VSplit_Main.layout"] 
   Try cvwVaccines.Font = Font[Settings["Admin_Vaccination_Schedules/lvwSchedules.font"]]
   Try lvwSchedules.Font = Font[Settings["Admin_Vaccination_Schedules/cvwVaccines.font"]]
   Try Form_PDF.CurrentZoom = Settings["Admin_Vaccination_Schedules/Form_PDF.Zoom", 1.0] 
   
End

Public Sub EditArea_Clear()
   
   modEditAreaHelpers.EditArea_Clear(Vbox_EditArea_Inner)
   
End

Public Sub EditArea_Notify_DataChanged(flag As Boolean)
   
   If flag Then 
      tbSave.Foreground = Color.Red
      Vbox_EditArea_Outer.Padding = 1
      tbSave.Enabled = True   
   Else
      tbSave.Foreground = Color.Black
      Vbox_EditArea_Outer.Padding = 0 
      tbSave.Enabled = False  
   Endif
   
End

Public Sub EditArea_TextBox_KeyPress()
   
   If bExit Then Return 
   
   If Not EditArea_Textbox_ExcludeKeys(key.code, Last.tag) Then 
      Stop Event
      Return
   End If
   Select Case Last.tag
      Case "find vaccine"
         Select Case Key.Code
            Case Key.Down
               If columnview1.Visible = True Then
                  columnview1.MoveFirst()
                  columnview1.Item.Selected = True
                  columnview1.SetFocus()
               Endif
         End Select
   End Select
   
End 

Public Function EditArea_TextBox_ExcludeKeys(keycode As Integer, tag As String) As Boolean
   
   Select Case tag
      Case "age due from", "age due to"
         bKeyValid = bKeyValid = modUtil.AllowKeys(const.AllowKeys_NumbersOnly, keycode)
      Case "date inactive"
         bKeyValid = modUtil.AllowKeys(const.AllowKeys_DateFormat, keycode)
      Case Else
         bKeyValid = True
   End Select
   Return bKeyValid
   
End

Public Sub EditArea_TextBox_GotFocus()
   
   columnview1.Visible = False    'could be visible, e.g user 1/2 way through doing something, moves off that textbox
   Last.BackGround = Color.rgb(95, 255, 175)  
   Select Case Last.tag
      Case "find vaccine"
         With columnview1
            .top = Last.parent.Parent.top + Last.parent.height
            .left = Last.Parent.left
            .width = Last.width + chkVaccineFemaleOnly.Width
            .Raise
            
         End With
   End Select
   
End

Public Sub EditArea_TextBox_LostFocus()
   
   Last.BackGround = Color.White
   Select Case Last.tag
      Case "date inactive"
         If Last.text <> "" Then
            If Not modEditAreaHelpers.Valid_Date(Last) Then
               bExit = True
               Last.text = ""
               bExit = False   
            End If
         End If
         
   End Select 
   
End

Public Sub EditArea_TextBox_Activate()
   '--------------------------
   'Enter key has been pressed
   '--------------------------
   
   Select Case Last.tag
      Case "schedule"
         rbMultipleYes.SetFocus()
         
      Case "date inactive"
         txtAgeDueFrom.SetFocus()
      Case "age due from"
         txtAgeDueTo.SetFocus()
      Case "age due to"
         tbSave.SetFocus()
      Case "notes"
         
         txtFindVaccine.SetFocus()
      Case "find vaccine"
         If Trim(txtFindVaccine.text) = "" Then
            tbSave.SetFocus()
         Else
            tbAddVaccine.SetFocus()
         Endif
         
   End Select
   
End

Public Sub EditArea_TextBox_Change()
   
   If bExit Then Return 
   Select Case Last.tag
      Case "find vaccine"
         If Trim(Last.text) = "" Then
            fk_vaccine = 0
         Endif
         
   End Select
   EditArea_Notify_DataChanged(True)
   
End

Public Sub Show_Help(flag As Boolean)
   Hbox_Help.Visible = Flag
   HBox_Scheduies.Visible = Not Flag
   cmbStateVaccinationsSchedules.Visible = Not Flag
   lblTaskSchedules.Visible = Not Flag
   tbShowSchedules.Visible = True   
   modEditAreaHelpers.Help_Show("help/maintaining-vaccination-schedules.html", FHelp)
 
End

Public Sub EditArea_Buttons_Click()
   
   Select Case Last.tag
      Case "new"
         Schedule_New()
      Case "save"
         Schedule_Save()
      Case "refresh"
         txtFilterSchedule.text = ""
         Reload()
      Case "add vaccine"      'add a vaccine into the schedule
         Vaccine_Add()
      Case "help"
         Show_Help(True) 
      Case "schedules"
         Show_Help(False)   
           End Select
   
End

Public Sub EditArea_Buttons_KeyPress()
   
   If key.code = key.return Then
      EditArea_Buttons_Click()
   Endif
   
End

Public Sub Schedule_New()
   
   EditArea_Clear()
   rbScheduleFemaleOnlyNo.value = True
   rbScheduleATSIOnlyNO.value = True
   bScheduleIsAboriginalTSI_Only = False  
   rbSeasonalNo.value = True
   bScheduleFemaleOnly = False   
   bMultipleVaccinesInSchedule = False  'usually only 1 vax at a time.
   cvwVaccines.clear()
   Vaccines_in_schedule = New Collection
   Vbox_EditArea_Inner.Enabled = True
   fk_schedule = 0
   pk_view = -1
   txtSchedule.SetFocus()
   
End

Public Sub Schedule_Delete()
   
End

Public Sub Schedule_Display()
   '----------------------------------------  
   'Displays the schedule but cannot edit it
   '----------------------------------------
   EditArea_Clear()
   If lvwSchedules.count = 0 Then Return
   bExit = True
   lvwSchedules.MoveCurrent
   pk_view = lvwSchedules.Item.Key
   fk_schedule = schedules[pk_view]!pk
   txtSchedule.text = schedules[pk_view]!schedule
   Try txtAgeDueFrom.text = schedules[pk_view]!age_due_from_months
   Try txtAgeDueTo.text = schedules[pk_view]!age_due_to_months
   Try txtNotes.text = schedules[pk_view]!notes
   If schedules[pk_view]!inactive = True Then
      rbScheduleActiveNo.Value = True
   Else
      rbScheduleActiveYes.Value = True   
   Endif
   If schedules[pk_view]!multiple_vaccines = True Then
      rbMultipleYes.value = True
      lblVaccines.text = "Vaccines in Fixed Schedule"
   Else
      lblVaccines.text = "Vaccines available to use for Target Disease/Schedule"
      rbMultipleNo.value = True  
   Endif
   If schedules[pk_view]!female_only = True Then
      rbScheduleFemaleOnlyYes.value = True
   Else
      rbScheduleFemaleOnlyNo.Value = True
   Endif
   If schedules[pk_view]!atsi_only Then
      rbScheduleATSIOnlyYes.Value = True
   Else
      rbScheduleATSIOnlyNo.value = True   
   Endif
   If Not IsNull(schedules[pk_view]!fk_season) Then
      rbSeasonalYes.value = True
      cmbSeasons.Visible = True
      cmbSeasons.index = cmbSeasons.Find(seasons[schedules[pk_view]!fk_season - 1]!season)
   Else
      cmbSeasons.Visible = False 
      rbSeasonalNo.value = True  
   End If
   Vaccines_In_Schedule_Refresh()
   Vaccines_In_Schedule_Display()
   Vbox_EditArea_Inner.Enabled = False   
   bExit = False
   EditArea_Notify_DataChanged(False)
   
End

Public Sub Schedule_Edit()
   
   Schedule_Display()
   Vbox_EditArea_Inner.Enabled = True   
   
End

Public Sub Schedule_Save()
   '------------------------------------------------------------
   'Saves the schedule details and any vaccines in this schedule
   '   pk serial NOT NULL,
   '   age_due_from_months integer,
   '   age_due_to_months integer,
   '   schedule text NOT NULL, -- either a target disease name eg 'yellow fever' or a schedule name to describe course of combined vaccines eg Hepatits A + Hepatitis B.Hence this allows unlimited and user defined schedules.
   '   female_only boolean DEFAULT false,
   '   aboriginal_tsi_only boolean DEFAULT false, -- australian requirement, some schedules apply only to aboriginal or torres strait islanders at particular ages
   '   fk_season integer, -- eg. influenza prompts only wanted at particular time of year
   '   inactive boolean DEFAULT false,
   '   date_inactive date,
   '   deleted boolean DEFAULT false,
   '------------------------------------------------------------   
   
   Dim x As Integer
   
   If Vbox_EditArea_Outer.padding = 0 Then Return ' no changes, nothing to save
   If Not Schedule_Valid() Then Return
   
   Schedule = New Collection
   If fk_schedule Then
      Schedule!pk = fk_schedule
   End If
   schedule!schedule = Trim(txtSchedule.text)
   schedule!age_due_from_months = Val(txtAgeDueFrom.text)
   schedule!age_due_to_months = Val(txtAgeDueTo.text)
   schedule!female_only = bScheduleFemaleOnly
   schedule!atsi_only = bScheduleIsAboriginalTSI_Only
   If fk_season Then    
      schedule!fk_season = fk_season   'IAN FIXME: THE PROBLEM OF REVERTING TO NULL
   End If  
   schedule!inactive = bScheduleStatus
   schedule!multiple_vaccines = bMultipleVaccinesInSchedule
   If rbScheduleActiveNo.value = True Then
      schedule!date_inactive = Val(txtDateInactive.text)
   Endif
   schedule!notes = Trim(txtNotes.text)
   modDBConnect.BeginTrans()
   fk_schedule = modVaccinationDBI.Schedules_Save(Schedules, pk_view, schedule)
   modVaccinationDBI.Vaccines_In_Schedule_Save(Vaccines_in_schedule, fk_schedule)
   modDBConnect.CommitTrans()
   Reload() 
   'Move to the row just saved. FIXME: THIS SOMETIMES DOSN'T SHOW THE ROW MARQUEE
   lvwSchedules.MoveFirst
   For x = 0 To lvwSchedules.count - 1
      If lvwSchedules.Item.key = pk_view Then
         '  lvwSchedules.SetFocus()
         lvwSchedules.Item.Selected = True
         Break
      Else  
         lvwSchedules.MoveNext()
      End If   
   Next
   
End 

Public Sub Reload()
   
   EditArea_Clear()
   schedules = modVaccinationDBI.Schedules_get(Trim(txtFilterSchedule.text), iDisplay_Schedules)
   lvwSchedules.clear()
   For Each schedule In schedules
      lvwSchedules.Add(schedule!pk, Schedule!schedule)
   Next
   Select Case iDisplay_Schedules
      Case 1
         lblSchedules.text = "Active Schedules"
      Case 2
         lblSchedules.text = "Inactive Schedules"
      Case 3
         lblSchedules.text = "Active and Inactive Schedules"
   End Select
   
   EditArea_Notify_DataChanged(False)
   Vbox_EditArea_Inner.Enabled = False 
   lvwSchedules.MoveFirst()
  ' lvwSchedules.SetFocus()
  Try lvwSchedules.Item.Selected = True    
   
End

Public Function Schedule_Valid() As Boolean
   
   Dim sMsg As String
   
   If txtSchedule.text = "" Then
      sMsg = "Please enter the name of the schedule or target disease"
      txtSchedule.SetFocus()
      Goto Prompt
   Endif
   If Trim(txtDateInactive.text) <> "" Then 
      If Not IsDate(txtDateInactive.text) Then
         sMsg = "Please enter a valid date"
         txtDateInactive.SetFocus()
         Goto Prompt
      Endif
   End If
   If cvwVaccines.count = 0 Then
      txtFindVaccine.SetFocus()
      Return False
   Endif
   Return True
   
   Prompt:
   Message.title = "Vaccination Schedules or Target Disease Schedules"
   Message.Info(sMsg)
   Return False   
   
End

Public Sub rbScheduleFemaleOnly_Click()
   
   bScheduleFemaleOnly = False  
   If Last.tag = "Yes" Then bScheduleFemaleOnly.value = True
   EditArea_Notify_DataChanged(True)
   
End

Public Sub rbSeasonal_Click()
   
   If Last.tag = "yes" Then
      cmbSeasons.Visible = True
   Else
      cmbSeasons.Visible = False
      cmbSeasons.index = -1
      fk_season = 0
   End If 
   EditArea_Notify_DataChanged(True)
   
End

Public Sub cmbSeasons_Click()
   
   fk_season = Seasons[Last.index]!pk
   EditArea_Notify_DataChanged(True)
   
End

Public Sub lvwSchedules_Menu()
   
   If Last.count Then mnuSchedules.popup
   
End

Public Sub mnuSchedules_Click()
   
   Dim schedule As New Collection
   
   lvwSchedules.MoveCurrent
   
   Select Case Last.tag
      Case "edit"
         Schedule_Edit()
      Case "delete"
         Message.title = "Vaccination Schedules"
        If Message.Warning("Are you sure you wish to delete the schedule:\n\n" & lvwSchedules.Item.Text, "Yes", "No") = 1 Then   
            schedule!deleted = True
            schedule!pk = schedules[lvwSchedules.Item.key]!pk
            modVaccinationDBI.Schedules_Save(schedules, pk_view, schedule)
            modDBConnect.CommitTrans()
         End If   
      Case "font"
         modUtil.Listview_SetFont(lvwSchedules, "Admin_Vaccination_Schedules")
      Case "help"
   End Select
   
End

Public Sub mnuVaccines_Click()
   '-----------------------------------------------------
   'User has actioned the popup menu of the vaccines list
   '-----------------------------------------------------   
   
   Select Case Last.tag
      Case "font"
         modUtil.Columnview_SetFont(cvwVaccines, "Admin_Vaccination_Schedules")
      Case "remove only"
      Case "remove"
         Vaccine_Remove()
      Case "female only"
         Vaccine_FemaleOnly(True)
      Case "atsi only"
         Vaccine_ATSI(True)
      Case "available all"
         Vaccine_ATSI(False)
         Vaccine_FemaleOnly(False)
      Case "product information"
         modUtil.NotImplemented("Viewing Product Information")
      Case "help"
         modUtil.NotImplemented()
   End Select
   
End

Public Sub rbScheduleStatus_Click()
   
   bScheduleStatus = True
   If Last.tag = "No" Then bScheduleStatus = False
   
End

Public Sub lvwSchedules_Select()
   
   Schedule_Display()
   
End

Public Sub Columnview1_KeyPress()
   
   If key.code = key.return Then
      columnview1_DblClick()
   Endif
   
End

Public Sub columnview1_DblClick()
   
   Columnview1.MoveCurrent
   Select Case Last.tag.tag
      Case "find vaccine"
         Vaccines_Select()
   End Select
   columnview1.Visible = False   
   
End

Public Sub Vaccines_Get()
   '-------------------------------------------------------------------------------------------
   'Gets list of vaccines from the database
   'This routine is called by EditArea_TextBox_KeyRelease() where textbox = txtfindVaccine only
   'It gives us a list of vaccines for when we are creating a schedule
   '-------------------------------------------------------------------------------------------
   
   If Trim(txtFindVaccine.text) = "" Then
      Columnview1.Visible = False  
      Return
   Endif
   
   Vaccines = modVaccinationDBI.Vaccines_Get_By_Brand_Name(Trim(txtFindVaccine.text))
   
   If Vaccines.count = 0 Then Return
   Columnview1.clear()
   For Each vaccine In Vaccines
      Columnview1.Add(vaccine!pk, 0)
      Columnview1[vaccine!pk][0] = vaccine!brand
      Columnview1[vaccine!pk][1] = vaccine!form
      Columnview1[vaccine!pk][2] = vaccine!atsi_only
   Next
   With Columnview1
      .tag = txtFindVaccine
      .Visible = True 
   End With
   
End

Public Sub Vaccines_Select()
   
   bExit = True
   vaccine = Vaccines[columnview1.Item.key]
   txtFindVaccine.text = vaccine!brand
   txtFindVaccine.pos = 1
   fk_vaccine = vaccine!pk
   bExit = False
   tbAddVaccine.SetFocus()
   
End

Public Sub Vaccine_Remove()
   
   Message.Title = "Remove Vaccine"
   If Message.Question("Are you sure you wish to remove " & cvwVaccines[cvwVaccines.Item.key][0] & " from the schedule?", "Yes", "No") = 2 Then Return 
   modDBConnect.BeginTrans()
   CurrentVaccine!date_inactive = Now
   modVaccinationDBI.Vaccine_In_Schedule_Save(Vaccines_in_schedule, pk_view_vaccine, CurrentVaccine)
   modDBConnect.CommitTrans()
   Reload() 
   
End

Public Sub Vaccine_ATSI(bATSIOnly As Boolean)
   'Change vaccine status
   
   CurrentVaccine!atsi_only = bATSIOnly
   modDBConnect.BeginTrans()
   modVaccinationDBI.Vaccine_In_Schedule_Save(Vaccines_in_schedule, pk_view_vaccine, CurrentVaccine)
   modDBConnect.CommitTrans()
   Reload() 
   
End

Public Sub Vaccine_FemaleOnly(Flag As Boolean)
   CurrentVaccine!female_only = flag
   modDBConnect.BeginTrans()
   modVaccinationDBI.Vaccine_In_Schedule_Save(Vaccines_in_schedule, pk_view_vaccine, CurrentVaccine)
   modDBConnect.CommitTrans()
   Reload() 

   
   
End

Public Sub Vaccine_Add()
   
   Dim Schedule_Vaccine As New Collection
   
   Schedule_Vaccine!fk_vaccine = fk_vaccine
   Schedule_Vaccine!fk_schedule = fk_schedule
   Schedule_Vaccine!brand = txtFindVaccine.Text
   If chkVaccineATSIOnly.value = True Then
      Schedule_Vaccine!atsi_only = True
   Endif
    If chkVaccineFemaleOnly.value = True Then
      Schedule_Vaccine!female_only = True
   Endif
  
   Schedule_Vaccine!description = vaccine!description
   ' Schedule_Vaccine!fk_lu_vaccines_in_schedule = 
   Schedule_Vaccine!form = vaccine!form
   Schedule_Vaccine!pk_view = Str(fk_schedule & "-" & fk_vaccine) '?FIXME THIS IS NOT TRUE
   Try Vaccines_in_schedule.Add(Schedule_Vaccine, Str(fk_schedule & "-" & fk_vaccine)) 
   If Not Error Then  
      Vaccines_In_Schedule_Display()
      EditArea_Notify_DataChanged(True)
   Else
      Message.Info("Duplicate Vaccine")
   End If 
   txtFindVaccine.text = "" 'the change event will reset fk_vaccine
   txtFindVaccine.SetFocus()
   
End

Public Sub Vaccines_In_Schedule_Refresh()
   
   Vaccines_in_schedule = modVaccinationDBI.Vaccines_In_Schedules_Get(fk_schedule) 'unique key = lu_vaccines_in_shedule.pk
   
End

Public Sub Vaccines_In_Schedule_Display()
   '---------------------------------------------
   'Display all vaccines in a particular schedule
   '---------------------------------------------
   
   Dim key As String 
   
   cvwVaccines.Clear()
   
   For Each vaccine In Vaccines_in_schedule
      key = vaccine!pk_view
      cvwVaccines.Add(key, 0)
      cvwVaccines[key][0] = vaccine!brand
      cvwVaccines[key][1] = vaccine!form
      If vaccine!vaccine_atsi_only Then
         cvwVaccines[key][2] = "ATSI Only"
      Else
         cvwVaccines[key][2] = ""
      End If
      If vaccine!vaccine_female_only = True Then
        cvwVaccines[key][3] = "female only"
      Else
        cvwVaccines[key][3] = ""
      Endif
      cvwVaccines[key][4] = vaccine!vaccine_description
   Next
   
End

Public Sub txtFilterSchedule_KeyRelease()
   
   Reload()
   
End

Public Sub rbScheduleActiveNo_KeyPress()
   
   Select Case Key.Code
      Case Key.Return
         If Last.key = "yes" Then
            txtAgeDueFrom.SetFocus()
         Else
            If Last.value = True Then
               txtDateInactive.SetFocus()
            Endif
         End If 
         
   End Select
   
End

Public Sub rbScheduleActive_Click()
   
   EditArea_Notify_DataChanged(True)
   
End

Public Sub rbDisplaySchedules_Click()
   
   iDisplay_Schedules = Last.tag
   Reload()
   
End

Public Sub rbMultiple_Click()
   
   If Last.tag = "yes" Then
      bMultipleVaccinesInSchedule = True
   Else
      bMultipleVaccinesInSchedule = False
   End If
   EditArea_Notify_DataChanged(True)
   
End

Public Sub cvwVaccines_Menu()
   
   If Last.count And Vbox_EditArea_Inner.Enabled Then mnuVaccines.popup()
   
End

Public Sub tbClear2_Click()
   
   Dim sql As String
   Dim R As Result
   Dim x As Integer
   
   Return 
   x = 1
   
   sql = "Select * from clin_vaccination.lu_vaccines_in_schedule_bak"
   R = modDBConnect.exec_query(sql)
   For Each R
      sql = "Insert into clin_vaccination.lu_vaccines_in_schedule "
      sql &= "(pk, fk_schedule, fk_vaccine) Values ("
      sql &= R!pk & "," & R!fk_schedule & "," & R!fk_vaccine & ")"
      modDBConnect.exec_query(sql)
      Inc x  
   Next
   modDBConnect.CommitTrans()
   
End

Public Sub rbScheduleATSIOnly_Click()
   
   bScheduleIsAboriginalTSI_Only = False
   If Last.tag = "yes" Then
      bScheduleIsAboriginalTSI_Only = True
      chkVaccineATSIOnly.value = True
      chkVaccineFemaleOnly.Enabled = False 
   Else
      bScheduleIsAboriginalTSI_Only = False
   End If   
   EditArea_Notify_DataChanged(True)
   
End

Public Sub EditArea_TextBox_KeyRelease()
   
   If Last.tag = "find vaccine" Then
      Vaccines_Get()
   End If   
   
End

Public Sub chkVaccineFemaleOnly_Click()
   
   EditArea_Notify_DataChanged(True)
   
End

Public Sub rbMultiple_KeyPress()
   
   If key.code = key.return Then 
      If Last.tag = "yes" Then 
         rbMultipleNo.SetFocus()
      Else
         rbScheduleActiveYes.SetFocus()
      End If
   End If
   
End

Public Sub rbScheduleActive_KeyPress()
   
   If key.code = key.Return Then   
      If Last.tag = "yes" Then
         txtAgeDueFrom.SetFocus
      Else
         txtDateInactive.SetFocus()
      Endif
   End If  
   
End

Public Sub cvwVaccines_Select()
   
   cvwVaccines.MoveCurrent
   pk_view_vaccine = Vaccines_in_schedule[cvwVaccines.Item.key]!pk_view
   CurrentVaccine = New Collection
   CurrentVaccine!fk_schedule = Vaccines_in_schedule[cvwVaccines.Item.key]!fk_schedule
   CurrentVaccine!fk_vaccine = Vaccines_in_schedule[cvwVaccines.Item.key]!fk_vaccine
   CurrentVaccine!atsi_only = Vaccines_in_schedule[cvwVaccines.Item.key]!atsi_only
   CurrentVaccine!fk_lu_vaccines_in_schedule = Vaccines_in_schedule[cvwVaccines.Item.key]!fk_lu_vaccines_in_schedule
   
End

Public Sub cmbStateVaccinationsSchedules_Click()
   
   Schedule_Show_For_State()
   
End
