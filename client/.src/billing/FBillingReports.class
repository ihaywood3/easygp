' Gambas class file

Private visits As Collection
Private visit As Collection

Public Sub init()
   
   Dim x As Integer
   
   With columnview1
      .Columns.Count = 8
      For x = 0 To .Columns.Count - 1
         .Columns[x].Alignment = Align.Center
      Next
      .Columns[0].text = "Appt"
      .Columns[1].text = "Patient"
      .Columns[2].text = "   Bill To   "
      .Columns[3].text = "Item"
      .Columns[4].text = "Descriptor"
      .Columns[5].text = "Amount"
      .Columns[6].text = "Amount gst"
      .Columns[7].text = "Paid"
   End With   
   
End

Public Sub Day_list_Show(the_date As Date)
   
   Dim payer As String
   Dim item As String
   Dim x As Integer
   Dim num_visits As Integer 'may not be same as individual patients seen
   Dim fk_Last_invoice As Integer
   Dim total_billed As Float
   Dim total_paid As Float
   Dim running_total As Float
   Dim payment_totals As New Float[7] 
   Dim monies_received As Collection
   Dim payment_methods As Collection
   Dim running_total_gst As Float
   
   payment_methods = modBillingDBI.Payment_Methods_Get()
   monies_received = modBillingDBI.Receipts_Get_For_Day(the_date)
   visits = modBillingDBI.Day_List_Get(the_date, modDBConnect.currentUser!fk_branch, modDBConnect.currentUser!fk_staff)
  
   With columnview1
      .Clear 
      For x = 0 To .Columns.Count - 1
         .Columns[x].Alignment = Align.left
      Next
   End With
   For Each visit In visits
      columnview1.Add(visit!pk_items_billed, 0)
      If fk_Last_invoice <> visit!fk_invoice Then                                             'for each distinct invoice
         If Not IsNull(visit!appointment_time) Then                                           'if exists, show appointment time
            columnview1[visit!pk_items_billed][0] = Format(visit!appointment_time, "hh:nn")   ' eg. 09:00 FIXME PUT IN AM/PM
         Else
            columnview1[visit!pk_items_billed][0] = ""                                        'this may not exist if billed without appointment in book  
         Endif
         columnview1[visit!pk_items_billed][1] = visit!patient_wholename
         payer = ""
         If Not IsNull(visit!fk_lu_bulk_billing_type) Then
            If visit!fk_lu_bulk_billing_type = const.BulkBilling_Type_Medicare Then
               Payer = "Medicare"
            Else
               Payer = "Veterans"
            Endif
         Endif
         If payer = "" Then 
            If IsNull(visit!fk_payer_person) And IsNull(visit!fk_payer_branch) Then 
               payer = "Patient"
            Else
               If Not IsNull(visit!payer_organisation) Then
                  payer = visit!payer_organisation
               Else
                  payer = visit!payer_person_wholename
               End If   
            End If
         End If   
         columnview1[visit!pk_items_billed][2] = payer
         fk_Last_invoice = visit!fk_invoice
         Inc num_visits
      Else                                                                                     
         columnview1[visit!pk_items_billed][0] = ""                                          'suppress duplicates
         columnview1[visit!pk_items_billed][1] = "" 
         columnview1[visit!pk_items_billed][2] = ""
      Endif
      columnview1[visit!pk_items_billed][3] = Trim(visit!mbs_item & " " & visit!ama_item & " " & visit!user_item)
      columnview1[visit!pk_items_billed][4] = visit!descriptor_brief
      columnview1[visit!pk_items_billed][5] = visit!amount
      If Not IsNull(visit!amount_gst) Then
         columnview1[visit!pk_items_billed][6] = visit!amount_gst
      Else
         columnview1[visit!pk_items_billed][6] = "$0.0"
      Endif
      If Not IsNull(visit!total_paid) Then
         columnview1[visit!pk_items_billed][7] = visit!total_paid
      Else
         columnview1[visit!pk_items_billed][7] = "$0.0"
      End If
      ' payment_totals[visit!fk_lu_payment_method] = payment_totals[visit!fk_lu_payment_method] + CFloat(Replace(visit!amount_paid, "$", ""))
      ' running_total += modUtil.MoneyToFloat(visit!amount_paid)
      ' If visit!invoice_paid = True Then
      '    If Not IsNull(visit!item_billed_amount_gst) Then
      '       running_total_gst += modUtil.MoneyToFloat(visit!item_billed_amount_gst)
      '    Endif
      ' Endif

   Next
   '----------------------------
   'construct a bit of a summary
   '----------------------------
   tlDaySummary.text &= "<P><B>Summary</B><P>"
   tlDaySummary.text &= "<B>Patients seen:</B>" & num_visits 'put reason patient not seen and print under here
   tlDaySummary.text &= "<B>Patients billed:</B>" & num_visits
   tlDaySummary.text &= "<B>Bulk Billed Medicare:</B>"
   
End

Public Sub DateChooser1_Activate()
   
   Day_list_Show(Last.value)
   
End
