' Gambas class file

' Copyright (C) 2008-2015 Dr. Richard Terry

' This program is free software: you can redistribute it and/or modify
' it under the terms of the GNU General Public License as published by
' the Free Software Foundation, either version 3 of the License, or
' (at your option) any later version.

' This program is distributed in the hope that it will be useful,
' but WITHOUT ANY WARRANTY; without even the implied warranty of
' MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
' GNU General Public License for more details.

' You should have received a copy of the GNU General Public License
' along with this program.  If not, see <http://www.gnu.org/licenses/>.
' ------------------------------------------------------------------------------------------
' PURPOSE          A Form to allow clerical to enter details about a patients preferred pharmacy

'-------------------------------------------------------------------------------------------
Private currentconsult As CConsult  
Private pharmacies As Collection
Private pharmacy As Collection
Private fk_branch_pharmacy_patient As Variant            'if not 0 then key to contacts.data_branches = this patient's pharmacy
Private fk_branch As Variant                             'the key to contacts_data_branches for the selected pharmacy
Private bExit As Boolean
Private ParentForm_Vbox_EditArea As VBox
Private Parent_Form As String
Private listview1_key As String

Public Sub Set_Parent_Form(inForm As String)
   
   Parent_Form = inForm   
   
End

Public Sub _new()
   
   lblMeasure.text = "Memo - Patient/Webster  "
   modEditAreaHelpers.Resize_labels(Vbox_EditArea, lblmeasure)
   Settings_Load
   
End

Public Function fk_patient_pharmacy() As Variant
   
   Return fk_branch_pharmacy_patient
   
End

Public Function patient_pharmacy_memo() As String
   
   Return Trim(teMemo.text) 
   
End

Public Sub Set_Parent_From_VBox_EditArea(vb As Vbox)
   
   ParentForm_Vbox_EditArea = vb
   
End

Public Sub Init(cons As CConsult)
   'Cons is passed from FPatientBillingCards
   
   currentconsult = cons
   Reload
   
End

Private Sub Settings_Load()
   
   VSplit_Pharmacy.Layout = Settings["FPatientPharmacy/VSplit_Pharmacy.Layout", modUtilGUI.VSplit([4, 1])]
   
End

Private Sub Settings_Save()
   
   Settings["FPatientPharmacy/VSplit_Pharmacy.Layout"] = VSplit_Pharmacy.Layout
   
End

Private Sub Reload()
   'If pharmacy informatiion exists for the patient, then reload this
   
   EditArea_Clear(False) 'wipe everyfield
   If Not IsNull(currentconsult!patient!fk_branch_pharmacy) Then
      fk_branch_pharmacy_patient = currentconsult!patient!fk_branch_pharmacy
      pharmacy = modUtil.Copy_Collection_Keyed_Sequentially(modContactsDBI.Branch_get_by_keys(fk_branch_pharmacy_patient))[0]
      bExit = True  
      Pharmacy_Display
      If Not IsNull(currentconsult!patient!memo_for_pharmacy) Then
         teMemo.text = currentconsult!patient!memo_for_pharmacy
      End If   
      chkWebsterPack.value = currentconsult!patient!uses_webster_pack
       bExit = False  
   Endif
   
End

Public Sub Save()
   'any data needed (chkWebsterpack.value, fk_branch_pharmacy_patient, teMemo)
   'is saved from FPatients.Save()
   
End

Public Sub EditArea_KeyRelease()
   
   If Last.tag = "organisation" Then
      Pharmacies_Get
   Endif
   
End

Public Sub EditArea_Notify_DataChange(flag As Boolean)
   '---------------------------------------------------------------------------------------------------------------
   'if embedded in FPatients or FRecordVisit (via FPatientBillingCards) then parent edit area notifies a datachange
   '---------------------------------------------------------------------------------------------------------------
   
   If bexit Then Return 
   If flag Then
      If Parent_Form = "FPatientBillingCards" Then
         Print ParentForm_Vbox_EditArea
         ParentForm_Vbox_EditArea.padding = 1
         '  FRecordVisit.Enable_BOKBIllingSaveButton(True)
         ' Vbox_EditArea.background = Color.Background
      Endif
   Else
      '  Vbox_EditArea.Padding = 0
      If Parent_Form = "FPatientBillingCards" Then
         ParentForm_Vbox_EditArea.padding = 0
         ' FRecordVisit.Enable_BOKBIllingSaveButton(False)
         ' Vbox_EditArea.background = Color.Background
      Endif
   Endif
   
End

Public Sub Pharmacies_Get()
   'Gets branches or organisations whose category is 'pharmacy and displays in a list box'
   
   Dim organisation As Collection
   Dim sDetails As String
   
   With listview1
      .Clear
      .Visible = False  
   End With
   If Trim(txtOrganisation.text) = "" Then Return
   Pharmacies = modContactsDBI.Organisations_Get(Trim(txtOrganisation.text), "pharmacy")
   If Pharmacies.count Then
      For Each Pharmacy In Pharmacies
         sDetails = Pharmacy!organisation & " " 
         If Pharmacy!branch <> "HEAD OFFICE" Then
            sDetails &= Pharmacy!branch & " " 
         Endif
         sDetails &= Trim(Pharmacy!street1 & " " & Pharmacy!street2) & " " & Pharmacy!town
         listview1.Add(Pharmacy!pk_view, sDetails)
      Next
      listview1.Visible = True
      listview1.Raise
      listview1.tag = txtOrganisation
   Else
      listview1.Visible = False
   End If
   
End

Public Sub EditArea_GotFocus()
   
   If Last.tag = "organisation"    
      With listview1
         .top = Last.parent.parent.top + Last.parent.parent.height 
         .left = Last.parent.left + VBox_EditArea.Padding
         .width = Last.width
         .Visible = False  
      End With
   End If   
   
End

Public Sub EditArea_KeyPress()
   
   If Last.tag = "organisation" Then
      If Key.code = Key.down Then
         If listview1.Visible Then
            With listview1
               .MoveFirst
               .Item.Selected = True
               .SetFocus
            End With
         Endif
      Endif
   Endif
   
End

Public Sub Listview1_KeyPress()
   
   If Key.code = Key.return Then
      ListView1_DblClick
   End If 
   
End

Public Sub ListView1_DblClick()
   
   listview1.MoveCurrent
   listview1_key = listview1.Item.key
   pharmacy = Pharmacies[listview1_key]
   listview1.Visible = False  
   Pharmacy_Display
   
End

Public Sub Pharmacy_Display()
   
   txtOrganisation.text = pharmacy!organisation
   txtStreet1.text = pharmacy!street1
   Try txtStreet2.text = pharmacy!street2
   txtSuburb.text = pharmacy!town
   txtPostcode.text = pharmacy!postcode
   txtState.text = pharmacy!state
   fk_branch = pharmacy!fk_branch
   fk_branch_pharmacy_patient = pharmacy!fk_branch
   Pharmacy_Comms_Get()
   
End

Public Sub Pharmacy_Comms_Get()
   'Fetches the comms (phone, email fax etc) for the selected branch (pharmacy)
   
   Dim comms As Collection
   Dim comm As Collection 
   
   comms = modContactsDBI.Branch_Comms_Get(fk_branch)
   lvwBranchContacts.Clear
   For Each comm In comms
      lvwBranchContacts.Add(comm!pk, comm!type & " " & comm!value)
   Next
   
End

Public Sub EditArea_Change()
   
   If bexit Then Return
   Select Case Last.tag
      Case "organisation" 
         If Trim(Last.text) = "" Then EditArea_Clear(True) 'preserve webster and memo details
      Case "memo"
         
   End Select
   
   EditArea_Notify_DataChange(True)
   
End

Public Sub EditArea_Clear(bPreserveMemo_and_Webster As Boolean)
   'Clears the input area
   
   bExit = True
   txtOrganisation.text = "" 
   txtBranch.text = ""
   txtStreet1.text = ""
   txtStreet2.text = ""
   txtState.text = ""
   txtPostcode.text = ""
   txtSuburb.text = ""
   lvwBranchContacts.Clear
   If bPreserveMemo_and_Webster = False Then
      chkWebsterPack.value = False
      teMemo.Clear
   End If   
   fk_branch = 0
   bExit = False
   
End

Public Sub tbClear_Click()
   '--------------------------------------------------------------------------------
   'wipes either all data or just the pharmacy name/address according to user choice
   '---------------------------------------------------------------------------------   
   
   Dim smsg As String
   
   sMsg = ""
   If fk_branch Then
      Message.title = "Delete Pharmacy"
      sMsg = "Are you sure you wish to delete the current pharmacy data?\n\n"
      
      Select Case Message.Question(sMsg, "Wipe all data", "Keep Webster/Memo Data", "Cancel")
         Case 1
            If Message.Warning("Confirm: wipe all data including memo and webster details", "Yes", "No") = 1 Then 
               EditArea_Clear(False) 'preserve nothing
            Endif
         Case 2
            EditArea_Clear(True)     'clear except the memo and webster details are preserved
         Case 3
            Return
      End Select
      txtOrganisation.SetFocus
   Endif
   
End

Public Sub VSplit_Pharmacy_Resize()
   
   Settings_Save  
   
End
