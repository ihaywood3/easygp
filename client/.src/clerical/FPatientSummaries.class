' Gambas class file

' Copyright (C) 2008-2016 Dr. Richard Terry

' This program is free software: you can redistribute it and/or modify
' it under the terms of the GNU General Public License as published by
' the Free Software Foundation, either version 3 of the License, or
' (at your option) any later version.

' This program is distributed in the hope that it will be useful,
' but WITHOUT ANY WARRANTY; without even the implied warranty of
' MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
' GNU General Public License for more details.

' You should have received a copy of the GNU General Public License
' along with this program.  If not, see <http://www.gnu.org/licenses/>.
'-------------------------------------------------------------------------------------------
' PURPOSE        : Allow viewing/re-printing of some view of patient data by clerical staff
'                  e.g health summary, medication summary, immunisation summary
'                  Patient's often phone up to get copies of these
'-------------------------------------------------------------------------------------------
Private GPMP As Collection                                   'the patients GP management plan if claimed via EasyGP
Public CurrentConsult As CConsult 'do not make this private
Private PDF_Form As FPdf
Private Form_HTML_Viewer As FHtmlViewer
Private Const cRow_HealthSummary As Integer = 0
Private Const cRow_Immunisations As Integer = 1
Private Const cRow_Medications As Integer = 2

Public Sub _new()

   With PDF_Form = New FPdf(Vbox_PDf)                              'Pdf form to show any scanned doc's
      .BtOpen.Visible = False
      .BtPrint.Visible = True
      .btZoomIn.Visible = True
      .btZoomOut.Visible = True
   End With
   With Form_HTML_Viewer = New FHtmlViewer(Vbox_PDf)
      .Visible = False
      .tbWebBrowserPrint.Visible = True
      .tbWebBrowserZoomIn.Visible = True
      .tbWebBrowserZoomOut.Visible = True
   End With
   PDF_Form.BtPrint.Enabled = True
   With cvwRecalls
      .Clear()
      .Columns.count = 1
   End With
   Available_Summaries_Show
   Settings_Load()

End

Public Sub GUI_Reset()

   ' Clears the form (called from FClericalReadOnlyClinical
   Form_HTML_Viewer.WebView1.Html = "<HTML><BODY></BODY></HTML>"
   cvwRecalls.Clear

End

Public Sub Init(cons As CConsult)

   CurrentConsult = cons
   GPMP = modPastHistoryDBI.GP_Management_Plans_Get(currentconsult!patient!fk_patient, True)
   If Not IsNull(GPMP) Then
      cvwAvailableSummaries[0][0] = "GP Management Plan"
   Else
      cvwAvailableSummaries[0][0] = "Health Summary"
   End If
   With cvwAvailableSummaries
      .MoveFirst()
      .Item.Selected = True
      .SetFocus()
   End With
   Health_Summary_Show                                                                          'show GPMP or ordinary Health summary
   cvwRecalls_Refresh

End

Public Sub cvwRecalls_Refresh()
   '---------------------------------------------------------------
   'fixme separate out the toolbar display with the cvw display
   'I've stuck a routine to fill the columnview in modRecallsDBI
   'as its used in different spots in the progres
   'FIXME probably need to do so in the recalls management module
   '--------------------------------------------------------------

   Dim x As Integer                      'for readability = key
   Dim rows As String
   Dim sColText As String

   cvwRecalls.Clear()

   currentconsult.Refresh("recalls_logged")

   For Each currentconsult!recalls_logged
      x = currentconsult!recalls_logged.Key                        'readability
      '---------------------------------------------------------
      'Display in the recalls list on the tabstrip_lists control
      '---------------------------------------------------------
      cvwRecalls.Add(x, 0)
      cvwRecalls[x][0] = Format(currentconsult!recalls_logged[x]!due, "dd/mm/yyyy") & "  " & currentconsult!recalls_logged[x]!reason
   Next

End

Public Sub Available_Summaries_Show()
   '----------------------------
   'Show all available summaries
   '----------------------------

   With cvwAvailableSummaries
      .Columns.count = 2
      .Columns[0].Text = "Name"
      .Columns[1].Text = "Description"
   End With
   cvwAvailableSummaries.Add(0, 0)
   cvwAvailableSummaries[0][0] = "Health Summary" 'this may be changed in subsequent Inits(,) if patient has a GPMP
   cvwAvailableSummaries[0][1] = "All the patient's health details including social, family, past history, medications, recalls etc"
   cvwAvailableSummaries.Add(1, 0)
   cvwAvailableSummaries[1][0] = "Immunisations"
   cvwAvailableSummaries[1][1] = "A listing of the all the patient's immunisations"
   cvwAvailableSummaries.Add(2, 0)
   cvwAvailableSummaries[2][0] = "Medications"
   cvwAvailableSummaries[2][1] = "A listing of the patient's medications, brand names and food details"
   lblmeasure.font = cvwAvailableSummaries.Font
   modUtilGUI.Columnview_Columns_Set_Size(cvwAvailableSummaries, lblmeasure)

End

Public Sub Settings_Save()

   Settings["FPatientSummaries/HSplit1.Layout"] = HSplit1.Layout
   Settings["FPatientSummaries/VSplit1.Layout"] = VSplit1.Layout

End

Private Sub Settings_Load()

   Try HSplit1.Layout = Settings["FPatientSummaries/HSplit1.Layout", modUtilGUI.HSplit([1, 1])]
   Print modUtilGUI.VSplit([5, 1])
   Try VSplit1.Layout = Settings["FPatientSummaries/VSplit1.Layout", modUtilGUI.VSplit([5, 1])]
   cvwAvailableSummaries.font = Font[Settings["FPatientSummaries/cvwAvailableSummaries.font", "DejaVu Sans,9"]]

End

Public Sub cvwAvailableSummaries_Menu()

   If cvwAvailableSummaries.count Then
      mnuAvailableSummaries.Popup
   Endif

End

Public Sub mnuAvailableSummaries_Click()
   '-------------------------------------------------------
   'user has clicked on the menu over the certificates list
   '-------------------------------------------------------

   Dim x As Integer

   Select Case Last.tag
      Case "font"
         modUtilGUI.Columnview_SetFont(cvwAvailableSummaries, "FPatientSummaries")
      Case "help"
         modUtilGUI.NotImplemented("Help for clerical patient summaries section")
   End Select
Catch
   Return

End

Public Sub cvwAvailableSummaries_Select()

   cvwAvailableSummaries.MoveCurrent()
   Select Case cvwAvailableSummaries.Item.Key
      Case cRow_HealthSummary
         Health_Summary_Show
      Case cRow_Immunisations
         Immunisation_Summary_Show
      Case cRow_Medications
         Medications_Show
   End Select

Catch
   Return

End

Public Sub Medications_Show()
   '-----------------------------------------------------------------------
   'Show patients medications list + brand equivalents and food information
   '-----------------------------------------------------------------------

   PDF_Form.visible = False
   With Form_HTML_Viewer
      .Visible = True
      .WebView1.HTML = modPrescribingDBI.Brand_Equivalents_Make_HTML(modPrescribingDBI.active_medication_list(currentconsult), currentconsult)
   End With

End

Public Sub Health_Summary_Show()

   Form_HTML_Viewer.Visible = False
   With PDF_Form
      .Visible = True
      .load_PDF(modPrinting.Latex_To_PDF(modProgressNotes.Health_Summary_Construct_LaTex(currentconsult, GPMP).GetData()), False, False)
   End With

End

Public Sub Immunisation_Summary_Show()

   Form_HTML_Viewer.Visible = False
   With PDF_Form
      .Visible = True
      .load_PDF(modPrinting.Latex_To_PDF(modProgressNotes.Immunisation_Summary_Construct_Latex(currentconsult).GetData()), False, False)
   End With

End
