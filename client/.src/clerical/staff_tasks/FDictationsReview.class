' Gambas class file

' Copyright (C) 2008-2015 Dr. Richard Terry, Dr Ian Haywood

' This program is free software: you can redistribute it and/or modify
' it under the terms of the GNU General Public License as published by
' the Free Software Foundation, either version 3 of the License, or
' (at your option) any later version.

' This program is distributed in the hope that it will be useful,
' but WITHOUT ANY WARRANTY; without even the implied warranty of
' MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
' GNU General Public License for more details.

' You should have received a copy of the GNU General Public License
' along with this program.  If not, see <http://www.gnu.org/licenses/>.
'----------------------------------------------------------------------
Private Form_Referrals As FReferrals   
Private pending_dictations As Collection
Private the_dictation As CRow

Public Sub Init()
   With Form_Referrals = New FReferrals(Vbox_Referrals)
      '.Set_Embedded_Form_Name("FDictationsReview")
      .Init(Null, True)  ' set as embedded
      .Editor_Maximize_Minimize()
   End With
   Refresh()
   
End

Public Sub bRefresh_Click()

   Refresh()

End

Public Sub Refresh()
   Dim i As CRow
   
   If Not IsNull(the_dictation) Then
      Message.Error("Can't refresh while editing", "OK")
      Return
   Endif
   lvPatients.Clear()
   pending_dictations = modDictationsDBI.Get_New_Dictations()
   For Each i In pending_dictations
      lvPatients.Add(i!pk_dictation, i!wholename)
   Next
   
End

Public Sub lvPatients_Click()
   Dim key As Integer
   Dim new_cc As CConsult
   Dim the_referral As Collection
   
   If Not IsNull(the_dictation) Then Return
   lvPatients.MoveCurrent()
   Try key = CInt(lvPatients.Key)
   If Not Error And If Not IsNull(key) And If pending_dictations.Exist(key) Then
     the_dictation = pending_dictations[key] ' keyed off the dictations PK
     new_cc = New CConsult(modContactsDBI.Patient_Get_Using_PK(the_dictation!fk_patient))
     Form_Referrals.Set_Consult(new_cc)
     the_referral = modReferralsDBI.Referrals_Get_By_PK(the_dictation!fk_referral)
     the_referral!body_html = Text2HTML(the_dictation!transcript)
     Form_Referrals.Referral_Edit(the_referral)
   Endif
   
End

Private Sub Text2HTML(txt As String) As String
    
  txt = Replace$(txt, "&", "&amp;")
  txt = Replace$(txt, "<", "&lt;")
  txt = Replace$(txt, ">", "&gt;")
  txt = Replace$(txt, "\n\n", "\n</p><p>\n")
  txt = "<p>" & txt & "</p>"
  Return txt
  
End



Public Sub lvPatients_Activate()
   
  lvPatients_Click() 
   
End


Public Sub tbSaveNoSend_Click()

   If IsNull(the_dictation) Then Return
   Form_Referrals.Save(False)
   modDictationsDBI.Flag_Completed(the_dictation)
   the_dictation = Null
   
End

Public Sub tbSend_Click()

   If IsNull(the_dictation) Then 
      Message.Error("No dictation loaded, can't send")
      Return
   Endif
   Form_Referrals.Save(True)
   modDictationsDBI.Flag_Completed(the_dictation)
   the_dictation = Null
   
End

Public Sub tbDiscard_Click()

   If IsNull(the_dictation) Then Return
   Form_Referrals.Referral_New()
   modDictationsDBI.Flag_Completed(the_dictation)
   the_dictation = Null
   
End
