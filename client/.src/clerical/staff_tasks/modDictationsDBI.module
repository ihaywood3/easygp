' Gambas module file

' Copyright (C) 2008-2013 Dr. Richard Terry Dr. Ian Haywood

' This program is free software: you can redistribute it and/or modify
' it under the terms of the GNU General Public License as published by
' the Free Software Foundation, either version 3 of the License, or
' (at your option) any later version.

' This program is distributed in the hope that it will be useful,
' but WITHOUT ANY WARRANTY; without even the implied warranty of
' MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
' GNU General Public License for more details.

' You should have received a copy of the GNU General Public License
' along with this program.  If not, see <http://www.gnu.org/licenses/>.
'----------------------------------------------------------------------

Public Sub Submit_Dictation(task As Cshelltask, cc As CConsult, fk_referral As Integer, fname As String) As String
   
   Dim new_fname As String
   Dim cmd As String
   Dim dict As New Crow
   Dim ftpserver As String
   Dim logon As String
   Dim passwd As String
   
   ftpserver = modAdminDBI.Config_Get("dictation_ftp_server", "ftp.ozescribe.com.au")
   logon = modAdminDBI.Config_Get("dictation_ftp_username", Null)
   passwd = modAdminDBI.Config_Get("dictation_ftp_password", Null)
   If IsNull(logon) Or If IsNull(passwd) Then
      Message.Error("FTP not configured correctly, need to set FTP logon and password")
   Endif
   new_fname = "ref" & fk_referral & ".mp3" ' use a much simpler name on the server to avoid confusing the transcriptionists
   cmd = modUtil.FindProgram("ftp", "ftp-ssl")
   task.Send(cmd & " -ine " & ftpserver & " <<xyz123xyz")
   task.Send(Subst$("user &1 &2\npassive\nbinary\ncd voice", logon, passwd))
   task.Send(Subst$("put &1 &2\nbye\nxyz123xyz", fname, new_fname))
   ' make a nice pretty name for the recording file
   new_fname = modUtil.Filename_Protect(cc!patient!firstname)
   new_fname &= "." & modUtil.Filename_Protect(cc!patient!surname)
   new_fname &= ".ref" & fk_referral & "." & Format$(Now(), "ddmmyy") & ".mp3"
   ' and store in the document archive
   modFiles.Put("document_archiving_directory", new_fname, fname, task)
   task.Finish()
   Task.Close()
   dict!filename = new_fname
   dict!fk_referral = fk_referral
   dict!fk_user = modDBConnect.currentUser!fk_staff
   dict.Save("clin_consult.dictations", "pk_dictation")
   Return new_fname

End

Public Sub Get_New_Dictations() As Collection
   
   Dim sql As String
   
   sql = "select * from clin_consult.vwdictations where fk_user = &1 and not processed and transcript is not null"
   Return modDBConnect.exec_query_row(Subst$(sql, modDBConnect.currentUser!fk_staff))
   
End

Public Sub Flag_Completed(dict As Crow)
   
   dict!processed = True
   dict.Save("clin_consult.dictations", "pk_dictation")
   
End
