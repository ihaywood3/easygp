' Gambas class file

' Copyright (C) 2014 Dr. Richard Terry
' This program is free software: you can redistribute it and/or modify
' it under the terms of the GNU General Public License as published by
' the Free Software Foundation, either version 3 of the License, or
' (at your option) any later version.
' This program is distributed in the hope that it will be useful,
' but WITHOUT ANY WARRANTY; without even the implied warranty of
' MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
' GNU General Public License for more details.

' You should have received a copy of the GNU General Public License
' along with this program.  If not, see <http://www.gnu.org/licenses/>.
'-----------------------------------------------------------------------
' PURPOSE   to the patients measurements
'-----------------------------------------------------------------------
Private cvwMeasurements_Key As Variant 
Private cvwMeasurementValues_Key As Variant
Private currentconsult As CConsult
Private current_measurement_type As Collection
Private current_measurement As Collection  

Public Sub _new()
   
  
   
End

Public Sub Current_Consult_Exists() As Boolean
   
   If Not IsNull(currentconsult) Then Return True 
   
End

Public Sub Init(cons As CConsult)
   
   currentconsult = cons
    Measurement_Available_Refresh 
End

Public Sub Gui_Clear()
   
   currentconsult = Null
   modUtilGUI.Columnview_Clear_Properly(cvwMeasurementValues)
   modUtilGUI.Columnview_Clear_Properly(cvwMeasurements) 
   
End

Public Sub cvwMeasurementValues_Select()
   
   cvwMeasurementValues.MoveCurrent()
   cvwMeasurementValues_key = cvwMeasurementValues.Item.Key
   current_measurement = currentconsult!measurements[cvwMeasurementValues_key]
   
End

Public Sub cvwMeasurementValues_Menu()
   
   If cvwMeasurementValues.count Then
      mnuMeasurementValues.popup()
   Endif
   
End

Public Sub mnuMeasurementValues_Click()
   
   Dim units As String
   Dim measurement As String = cvwMeasurementValues[cvwMeasurementValues_Key][1]
   
   Select Case Last.tag
      Case "delete"
         cvwMeasurementValues.MoveCurrent
         Select Case current_measurement_type!name_full
            Case "blood pressure"
               units = "mmHg"
            Case "height"
               units = "cm"
            Case "weight"
               units = "kg"
            Case "waist"
               units = "cm"
         End Select   
         If modMeasurementsDBI.Measurement_Delete(currentconsult, cvwMeasurements[cvwMeasurements_Key][0], cvwMeasurementValues[cvwMeasurementValues_Key][1], cvwMeasurementValues[cvwMeasurementValues_Key][0], cvwMeasurementValues_key) Then    'true = success
            If Format(Now, "dd/mm/yyyy") = cvwMeasurementValues[cvwMeasurementValues_Key][0] Then  
               FProgressNoteEditor.Deleted_Measurement_Remove(current_measurement_type!name_full, measurement, units)
            End If  
            Measurement_Show_Values(const.Measurement_Types[cvwMeasurements_Key])
            FClinical.Update_graphs
            FClinical.Refresh_Section(const.cSection_HealthSummary)
         End If   
   End Select
Catch
   Return 
   
End

Public Sub Measurement_Show_Values(measurement_type As Collection)
   '-----------------------------------------------------
   'when user clicks on the tabbed lists for measurments
   'show list for bp/height/weight in a numerical list
   '-----------------------------------------------------
   
   Dim M As Collection
   Dim Measurement_value As String
   
   With cvwMeasurementValues
      .Clear
      .Columns.count = 2
      .Columns[0].width = 80
   End With
   currentconsult.Refresh("measurements")
   For Each M In currentconsult!measurements
      If M!fk_type = measurement_type!pk Then
         'bad bug in measurements Ive not found FIXME
         'MEASUREMENT can be null bad bug
         If Not IsNull(M!measurement) Then
            Select Case measurement_type!pk
               Case const.Measurement_BP
                  measurement_value = modMeasurementsDBI.BP_Format(M!measurement)
               Case const.Measurement_Height
                  measurement_value = Format(M!measurement, "##.##")
               Case const.Measurement_Weight
                  measurement_value = Format(M!measurement, "##.0#")
               Case const.Measurement_Hip
                  measurement_value = Format(M!measurement, "##.0#")
               Case const.Measurement_Waist
                  measurement_value = Format(M!measurement, "##.0#")
            End Select
            cvwMeasurementValues.Add(M!pk_measurement, 0)
            cvwMeasurementValues[M!pk_measurement][0] = Format(M!consult_date, "dd/mm/yyyy")
            cvwMeasurementValues[M!pk_measurement][1] = measurement_value
         End If
      Endif
   Next
Catch
   Return 'may have been called when currentconsult in this module is null (i.e not yet created)
   
End

Public Sub cvwMeasurements_Select()
   
   cvwMeasurements.MoveCurrent()
   cvwMeasurements_Key = cvwMeasurements.Item.Key 
   current_measurement_type = const.Measurement_Types[cvwMeasurements_Key]
   Measurement_Show_Values(current_measurement_type)
   
End

Public Sub Get_Current_Measurement_Type() As Collection
   'userd to refresh lists when things done in FClinical.
   
   Return current_measurement_type
   
End

Public Sub Measurement_Available_Refresh()
   '--------------------------------------------------------------------------------
   'Display a list of measurements available to anyone on the tabbed lists container
   '--------------------------------------------------------------------------------
   
   Dim Measurement As Collection
   
   cvwMeasurements.clear()
   For Each measurement In const.Measurement_Types
      cvwMeasurements.add(measurement!pk, 0)
      cvwMeasurements[measurement!pk][0] = measurement!name_full
   Next
   
End
