' Gambas class file

' Copyright (C) 2008-2016 Dr. Richard Terry

' This program is free software: you can redistribute it and/or modify
' it under the terms of the GNU General Public License as published by
' the Free Software Foundation, either version 3 of the License, or
' (at your option) any later version.

' This program is distributed in the hope that it will be useful,
' but WITHOUT ANY WARRANTY; without even the implied warranty of
' MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
' GNU General Public License for more details.

' You should have received a copy of the GNU General Public License
' along with this program.  If not, see <http://www.gnu.org/licenses/>.
'-----------------------------------------------------------------------
Private Currentconsult As CConsult
Private P As Picture

Public Sub Init(cons As CConsult)
   
   currentconsult = cons 
   If Not IsNull(currentconsult!patient!fk_image) Then
      Try P = modGraphics.Blob_Convert_To_Picture(currentconsult!patient!image)!picture
   Else
      P = Picture.Load("icons/misc/no_photo.png")
   End If
   PicPatient.Picture = P
   lblID.text = currentconsult!patient!fk_patient   
   
End

Public Sub picPatient_Menu()
   
   If Not IsNull(currentconsult) Then mnuPatientPhoto.Popup
   
End

Public Sub mnuPatientPhoto_Click()
   '----------------------------------------------------
   'this is the popup menu over the patient's photograph
   '-----------------------------------------------------  
   
   Dim fk_image As Integer
   Dim NewImage As Image
   Dim pic As Picture
   Dim tempfile As String
   Dim sMsg As String
   Dim PersonPicture As Collection
   
   Select Case Last.tag
      Case "paste image"
         If Clipboard.type = Clipboard.Image Then
            tempfile = Temp() & ".png"                                                     'create file to save image to
            NewImage = Clipboard.Paste()                                                   'retrieve the image
            Goto SavePatientPicture                                                        'Save the picture
         Else
            Message.Info("Sorry, the clipboard does not seem to contain image data")
         End If
      Case "brightness"
         With FImageGet
            .btnOK.Caption = "Save Changes"
            .tbGetPicture.Visible = False 
            .HBox_ImageSize.Enabled = False  
            .Hbox_Quality.Enabled = False 
         End With
         Try FImageGet.Set_Image(PicPatient.Picture.Image)                                 'load picture to Image Manipulator
         If Not Error Then
            FImageGet.ShowModal()
            If Clipboard.type = Clipboard.Image Then
               NewImage = Clipboard.Paste()                                                   'retrieve the image
               tempfile = Temp() & ".png"                                                     'create file to save image to
               Goto SavePatientPicture     
            End If                                                      'Save the picture
         End If   
      Case "delete"
         If Message.Question("Are you sure you want to delete this patient's picture?", "Yes", "No") = 2 Then Return
         modDBConnect.exec_query("Update contacts.data_persons set fk_image =  null WHERE pk=" & currentconsult!patient!fk_person)
         modDBConnect.CommitTrans()
         PicPatient.Picture = Picture.Load("icons/misc/no_photo.png")
      Case "load image"
         Dialog.Title = "Select Image File"
         Dialog.Filter = ["*.png", "Image Files", "*", "All files"]
         If Dialog.OpenFile() Then Return
         Try picPatient.picture = Picture.Load(Dialog.Path)
         If Error Then
            sMsg = "An error occurred the file couldn't be loaded.\n\n"
            sMsg &= "Filename:" & Dialog.Path & "\n\n"
            sMsg &= "Perhaps it was not a valid picture file?"
            Message.Info(sMsg)
            PicPatient.Picture = Picture.Load("icons/misc/no_photo.png")
            Return
         End If
         tempfile = Dialog.Path
         Goto SavePatientPicture         
   End Select
   Return
   
SavePatientPicture:
   If Not IsNull(NewImage) Then
      NewImage.Save(Tempfile)                               'create a filepath if a pasted image or brightness adjusted image
   End If   
   fk_image = modConsultDBI.Image_Save(Tempfile, Currentconsult.GetPK(), modUtil.Calc_md5sum_File(Tempfile))                           'save to backend
   modDBConnect.exec_query("Update contacts.data_persons set fk_image = " & fk_image & " WHERE pk=" & currentconsult!patient!fk_person) 'link to the person
   modDBConnect.CommitTrans()
   Try PicPatient.Picture = Picture.Load(Tempfile)                     'show the picture Bug here
Catch
   Message.Info("An error occurred whilst trying to save the patient's photo.\n\nIf this persists please contact your system administrator.")
   Return   
   
End

Public Sub Patient_Picture_Clear()
   '---------------------------------------------
   ' Clears the picture from the main picture box
   ' puts back in the default
   '---------------------------------------------

   currentconsult = Null
   PicPatient.Picture = Picture.Load("icons/misc/no_photo.png")
   lblID.text = ""
   Wait        'otherwise, visually, picture won't disappear
   
End

Public Sub PatientPhoto_Acquire()
   
   Message.info("Photo Acquisition is not implemented yet")
   
End

Public Sub PatientPhoto_Import()
   
   Message.info("Photo Import is not implemented yet")
   
End

Public Sub PatientPhoto_Edit()
   
   Message.info("Photo Editing is not implemented yet")
   
End

Public Sub PatientPhoto_Save()
   
   Message.info("Photo Saving is not implemented yet")
   
End
