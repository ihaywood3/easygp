' Gambas class file

' Copyright (C) 2008-2013 Dr. Richard Terry Dr. Ian Haywood

' This program is free software: you can redistribute it and/or modify
' it under the terms of the GNU General Public License as published by
' the Free Software Foundation, either version 3 of the License, or
' (at your option) any later version.

' This program is distributed in the hope that it will be useful,
' but WITHOUT ANY WARRANTY; without even the implied warranty of
' MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
' GNU General Public License for more details.

' You should have received a copy of the GNU General Public License
' along with this program.  If not, see <http://www.gnu.org/licenses/>.
' MODULE NAME       FPatientsSelect
' PURPOSE           Allow user to select a patient
'-------------------------------------------------------------------------------

Private allpatients As Collection
Private currentpatient As Collection
Private form_clinical As FClinical
Private bPhotoConfirmed As Boolean           'If True we have a photograph of this patient.
Private Patient_Photo As Picture
Private calling_form As String

Public Sub btnOK_Click()
   '--------------------------------------------------------------------
   'Accept current patient and load their record
   'If bPhotoConfirmed = false (ie there is no photo
   'then the user will be prompted in Fclinical to confirm identity and
   'be encouraged to add a photo
   '--------------------------------------------------------------------

   If Not IsNull(currentpatient)
      Me.Close(True)
      Select Case calling_form
         Case "FClinical"
            FClinical.patient_load_record_external(currentpatient, bPhotoConfirmed, Patient_Photo)
         Case "FMakeAppointments"
            FMakeAppointments.patient_load_record_external(currentpatient, bPhotoConfirmed, Patient_Photo)
      End Select

   Endif

End

Public Sub Init(all_patients As Collection, sql_in_english As String, cf As String) 'was form_clinical as FClinical
   '-------------------------------------------------------------------------------
   'Show multiple patients for selection
   'In this routine we:
   '       : If using the columnview, which I'll make user configureable
   '       : loop through the data_persons recordset
   '       : for each  of these, then fetch 1-n addresses
   '       : if one address, put in column 3,4
   '       : ie same row as name,surname,sex,dob
   '       : if > one address put on next row, without name,surname,sex,dob
   '       : or load similar data into the list container which shows picture/html
   '-------------------------------------------------------------------------------

   Dim patient As Collection
   Dim lcRow As CPatientSelectRow
   Dim last_fk_person As Integer

   lblSectionHeading.Background = Settings["User Preferences" & modDBConnect.currentUser!logon_name & "/Background_Color", Color.lightbackground]
   Hbox_Heading.Background = lblSectionHeading.Background
   allpatients = all_patients
   txtSqlInEnglish.Text = sql_in_english
   calling_form = cf
   For Each patient In allpatients
      If last_fk_person <> patient!fk_person Then  'excludes duplicate address for same person
         bPhotoConfirmed = True
         patient!comms = modContactsDBI.person_comms_get(patient!fk_person)
         lcRow = New CPatientSelectRow(lcPatients, patient)
         last_fk_person = patient!fk_person
      End If
   Next

End

Public Sub lcPatients_Click()
   '----------------------------------------------------------------------------------
   'User has clicked on one row of the list container - hence selecting a patient
   'put that patient into the current patient collection, and photo into patient_photo
   '----------------------------------------------------------------------------------

   Dim hctrl As Control
   Dim PB As PictureBox
   Dim HB As HBox
   Dim Tl As TextLabel

   If lcPatients.Count Then
      currentpatient = allpatients[Last.children[Last.index].tag]
      For Each hctrl In Last.children[Last.index].children
         If hctrl Is HBox Then
            HB = hctrl
            PB = HB.Children[0]
            Patient_Photo = PB.Picture
         Endif
      Next
   Endif

End

Public Sub lcPatients_DblClick()

   btnOK_Click()

End
