' Gambas class file

' Gambas class file
' Copyright (C) 2008-2016 Dr. Richard Terry

' This program is free software: you can redistribute it and/or modify
' it under the terms of the GNU General Public License as published by
' the Free Software Foundation, either version 3 of the License, or
' (at your option) any later version.

' This program is distributed in the hope that it will be useful,
' but WITHOUT ANY WARRANTY; without even the implied warranty of
' MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
' GNU General Public License for more details.

' You should have received a copy of the GNU General Public License
' along with this program.  If not, see <http://www.gnu.org/licenses/>.
' RbTired group god knows why it was called this - I havn't renamed them all

Private k10_Row As Collection 'describes one row of a K10
Private K10 As Collection 
Private k10_score As Integer 
Private bExit As Boolean
Private bK10Completed As Boolean
Private Parent_Form As String

Public Sub _new()
   
   modMentalHealthDBI.Get_K10_Questions()               'Get K10 Questions if not already loaded
 
   
End

Public Sub Init(PF As String, Optional old_K10 As Collection = Null)
   'The K10 may not have been done, but the mental health plan
   'already saved, so fk_plan  = -1 or > 0
   
   Dim existing_row As Collection 
   
   Parent_Form = PF
   '------------------------------------------------------------------
   'If a plan exists, then this plan is being edited, either because
   'it is being changed during this consult, or the plan is now being
   'reviewed
   '-----------------------------------------------------------------
   If Not IsNull(old_K10) Then
      If old_K10.count Then
         k10 = New Collection
         For Each existing_row In old_K10
            k10_row = New Collection 
            k10_row!pk = existing_row!pk
            k10_row!fk_plan = existing_row!fk_plan
            k10_row!fk_lu_k10_component = existing_row!fk_lu_k10_component
            k10_row!score = existing_row!score
            k10_Row!question = const.K10_questions[existing_row!fk_lu_k10_component]!component
            k10.add(k10_row, k10_row!pk)
         Next
         K10_Show(K10)
         Calc_K10()
      End If
   Else
      K10_New()
   End If
   
End

Public Sub K10_New()
   '---------------------------------------------------------------------
   'Creates a new K10 (collection of rows for each K10 component)
   'Each K10 has 10 possible rows see clin_mentalhealth.lu_k10_components
   'These correspond to the 10 questions on the form
   'e.g About how often did you feel tired out for no good reason? = pk 1
   '---------------------------------------------------------------------
   
   Dim x As Integer
   
   k10 = New Collection 
   k10_Row = New Collection
   For x = 1 To 10
      K10_Row = New Collection
      K10_Row!fk_lu_k10_component = x
      K10_Row!question = const.K10_questions[x]!component
      K10_Row!fk_plan = Null 
      k10.add(k10_row, x)
   Next
   
End

Public Sub K10_Clear()
   '---------------------------------------------------------------------
   'sets radiobutton 6 to true for all questions, effively as far as the
   'user is concerned 'clearing' all the radiobuttons. 6 = invisible
   '--------------------------------------------------------------------
   
   Dim hCtrl As Control
   Dim HBoxContainer As HBox
   Dim HBoxRadioButtonContainer As HBox
   Dim rb As RadioButton
   
   bExit = True
   For Each hctrl In VBox_K10.children
      If hctrl Is Hbox Then
         HBoxContainer = hCtrl
         '------------------------------------------------------------
         'only the Hboxes with labels containing a question have a tag
         '------------------------------------------------------------
         For Each hctrl In HBoxContainer.Children
            If hctrl Is Hbox Then
               HBoxRadioButtonContainer = hCtrl
               For Each hctrl In HBoxRadioButtonContainer.children
                  If hctrl Is RadioButton Then
                     rb = hctrl
                     If rb.tag = "" Then  '6 = switch off rb has no tag
                        rb.value = True
                        Break
                     End If
                  End If
               Next
            End If   
         Next
      End If
   Next
   bexit = False
   lblK10Result.text = "Score  __/50"
   
End

Public Function K10_Completed() As Boolean
   
   Return bK10Completed
   
End

Public Function Get_CurrentK10_Score() As Integer
   
   Return k10_score
   
End

Public Function Get_CurrentK10() As Collection 
   '-------------------------------------------
   'Returns the K10 as a collection of K10 rows
   '-------------------------------------------
   
   Return K10
   
End

Public Sub K10_Show(K10 As Collection)
   '-----------------------------------------------
   'If a k10 exists then fill in the option buttons
   '-----------------------------------------------
   
   Dim hCtrl As Control
   Dim HBoxContainer As HBox
   Dim HBoxRadioButtonContainer As HBox
   Dim rb As RadioButton
   Dim K10_Row As Collection 
    
   If IsNull(K10) Then Return 
   bExit = True
   '-----------------------------------------------------------------
   'For each K10 row, loop through the questions rows, match the tag
   'on the container to that of the row, then set the option button
   '-----------------------------------------------------------------
   For Each K10_Row In K10
      '-----------------------------------------
      'For each of the rows containing questions
      '-----------------------------------------
      For Each hctrl In VBox_K10.children
         HBoxContainer = hCtrl
         '-----------------------------------------------------
         'If its tag = the components foreign key, have a match
         '------------------------------------------------------
         If HBoxContainer.tag = K10_Row!fk_lu_k10_component Then
            For Each hctrl In HBoxContainer.Children
               '----------------------------------------
               'find the container with the radiobuttons
               '----------------------------------------
               If hctrl.tag = HBoxContainer.tag Then 
                  HBoxRadioButtonContainer = hCtrl
                  For Each hctrl In HBoxRadioButtonContainer.children
                     If hctrl Is RadioButton Then
                        rb = hctrl 'only rb have tag
                        If rb.tag = K10_Row!score Then
                           rb.value = True
                           Break
                        End If  
                     End If                     
                  Next
                  Break
               End If
            Next
            Break  
         End If
         
      Next
   Next
   bExit = False 
   ' Calc_K10()  
   
End

Public Sub Calc_K10()
   
   Dim hCtrl As Control
   Dim HBoxContainer As HBox
   Dim HBoxRadioButtonContainer As HBox
   Dim rb As RadioButton
   Dim K10_Row As Collection 
    Dim Questions_completed As Integer
   
   k10_score = 0
   If IsNull(K10) Then Return 
   bExit = True
   '-----------------------------------------------------------------
   'For each K10 row, loop through the questions rows, match the tag
   'on the container to that of the row, then set the option button
   '-----------------------------------------------------------------
   For Each K10_Row In K10
      '-----------------------------------------
      'For each of the rows containing questions
      '-----------------------------------------
      For Each hctrl In VBox_K10.children
         HBoxContainer = hCtrl
         '-----------------------------------------------------
         'If its tag = the components foreign key, have a match
         '------------------------------------------------------
         If HBoxContainer.tag = K10_Row!fk_lu_k10_component Then
            For Each hctrl In HBoxContainer.Children
               '----------------------------------------
               'find the container with the radiobuttons
               '----------------------------------------
               If hctrl.tag = HBoxContainer.tag Then 
                  HBoxRadioButtonContainer = hCtrl
                  For Each hctrl In HBoxRadioButtonContainer.children
                     If hctrl Is RadioButton Then
                        rb = hctrl 'only rb have tag
                        If rb.value = True And rb.tag <> "" Then  '6 = switch off rb has no tag
                           k10_score += Val(rb.tag)
                           K10_Row!score = Val(rb.tag)
                           Inc Questions_completed
                           Break
                        End If  
                     End If                     
                  Next
                  Break
               End If
            Next
            Break  
         End If
         
      Next
   Next
   bExit = False 
   lblK10Result.text = "Score  " & k10_score & "/50"
   If Questions_completed = 10 Then bK10Completed = True   
   
   If Parent_Form = "FMentalHealth" Then FMentalHealth.k10_Show_Results(k10_score)
   
End

Public Sub rbTired_Click()
   '---------------------------------------------------------
   'God knows how this radiobutton got named, but damned if
   'I'm going to rename them all to something sensible. I was
   'probably half asleep!
   '---------------------------------------------------------
   If Bexit Then Return 
   Calc_K10()
   
End
