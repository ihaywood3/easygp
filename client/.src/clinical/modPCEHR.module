' Gambas module file

' Copyright (C) 2008-2015 Dr. Richard Terry, Dr. Ian Haywood

' This program is free software: you can redistribute it and/or modify
' it under the terms of the GNU General Public License as published by
' the Free Software Foundation, either version 3 of the License, or
' (at your option) any later version.

' This program is distributed in the hope that it will be useful,
' but WITHOUT ANY WARRANTY; without even the implied warranty of
' MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
' GNU General Public License for more details.

' You should have received a copy of the GNU General Public License
' along with this program.  If not, see <http://www.gnu.org/licenses/>.


Private magic As Integer[] = [0, 2, 4, 6, 8, 1, 3, 5, 7, 9]

Public Function checkIdentifier(ihi As String) As Boolean
   ' An implementation of the Luhn check algorithm
   ' This works for all PCEHR-related numbers: IHIs, HPI-Is and HPI-Os
   ' Although referenced in the Super Secret Medicare documentation, the algorithm itself
   ' is in the public domain
   ' http://en.wikipedia.org/wiki/Luhn_algorithm
  Dim i, a, total As Integer
  
  If Len(ihi) <> 16 Then Return False
  If Left$(ihi, 5) <> "80036" Then Return False
  For i = 1 To 15
     a = CInt(Mid$(ihi, i, 1))
     If Odd(i) Then
        total += magic[a]
     Else
        total += a
     Endif
  Next
  total = 10 - (total Mod 10)
  If CInt(Right$(ihi, 1)) = total Then Return True
  Return False
End

' returns null if IHI is unique
' excludes existing patient if provided
' returns the other patient if found

Public Sub Check_Unique_IHI(ihi As String, fk_patient As Integer) As Collection
   
   Dim q As String
   Dim r As Collection
   
   q = "Select wholename, fk_person, fk_patient from contacts.vwpatients where ihi = &1 "
   If fk_patient <> -1 Then
      q &= "and fk_patient <> &2 limit 1"
      r = modDBConnect.exec_query_first(q, [ihi, fk_patient])
   Else
      q &= "limit 1"
      r = modDBConnect.exec_query_first(q, [ihi])
   Endif
   Return r
   
End

'sets a patients IHI status to error

Public Sub Set_PCEHR_Error(fk_patient As Integer)
   
   modDBConnect.exec_query("update clerical.data_patients set pcehr_consent='e' where pk=&1", [fk_patient])
   
End


Public Sub Unittest(form_main As FMain)
   
  Debug checkIdentifier("8003608666672787") ' IHI
  Debug checkIdentifier("8003608333339232")
  Debug checkIdentifier("8003608333339432") ' deliberate mistake
  Debug checkIdentifier("8003616566681691") ' HPI-I
  Debug checkIdentifier("8003623233349876") ' HPI-O
End
