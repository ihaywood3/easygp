' Gambas class file

' Copyright (C) 2008-2013 Dr. Richard Terry

' This program is free software: you can redistribute it and/or modify
' it under the terms of the GNU General Public License as published by
' the Free Software Foundation, either version 3 of the License, or
' (at your option) any later version.

' This program is distributed in the hope that it will be useful,
' but WITHOUT ANY WARRANTY; without even the implied warranty of
' MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
' GNU General Public License for more details.

' You should have received a copy of the GNU General Public License
' along with this program.  If not, see <http://www.gnu.org/licenses/>.
'----------------------------------------------------------------------------------
'PURPOSE          Allow transfer of a progress note to another persons file
'                 with appropriate audit trail.
'                 This allows for the situation where one writes in the wrong file
'TODO             Put in the audit trail
'                 in the calling module put up menu item "transfer whole consult"
'---------------------------------------------------------------------------------
Private From_Patient As Collection
Private To_Patient As Collection
Private To_Patient_Row As CPatientSelectRow
Private fk_consult As Integer
Private patients As Collection
Private timer_count As Integer
Private From_patient_Progress_Notes As Collection
Private bTransferComplete As Boolean

Public Sub Run() As Boolean
   
   Return Not Me.ShowModal()
   
End

Public Sub Form_Close()
   
   FProgressNotesViewer.Edit_Mode_Remove(bTransferComplete)
   
End

Public Sub Form_Open()
   
   With Me
      .Center
      .Title = "TRANSFER CLINICAL NOTES"
   End With
   
End

Public Sub Init(fp As Collection, Notes As String, fppn As Collection)
   
   '-------------------------------------------------------------------------
   'fp is patient transfering the notes from
   'notes = the richtext of the notes with any images re-inserted
   'fk_consult = the fk_consult in clin_consult.consult for this progress note NOTE: WORK IN PROGRESS ?NO LONGER USE.
   'this row in clin_consult.consult current as fp!fk_patient = source patient
   '--------------------------------------------------------------------------
   
   Dim FPR As CPatientSelectRow
   Dim sMsg As String
   
   Message.title = "Limitations of record transfer"
   sMsg = "Please note that there are situations where EasyGP cannot accurately transfer the entire clinical record you have created.\n\n"
   "Progress notes which are not related to any of the following situations will transfer fully - i.e your ordinary clinical notes and "
   "any images, graphs etc you embedded in the notes.\n\n"
   "Any module you have used which generated either a printed output such as a form or a hl7 message which has been sent, will not transfer accurately, for example:\n\n"
   "- A request form\n"
   "- A letter\n"
   "- A mental health plan\n"
   "- A diabetes annual cycle of care\n\n I.E anywhere you may have printed something out.\n\n"
   "This is because EasyGP keeps the actual letter, pathology form etc in the database in its orginal form as proof in the future "
   "of what it was you actually generated (although of course the underlying database fields contain all the data) and hence that document will contain the original patient's name "
   "in the formattted text.\n\n"
   "If you are transferring a record that  you created on the same day it was created then all is not lost. You may either delete and re-produce "
   "the document, or if possible (e.g referral/request/mental health plan) you should edit the document, change something eg a comment to trigger "
   "EasyGP to recognise the data has changed, then resave.\n\n Note that prescriptions cannot be edited in the current version of EasyGP and you should hence delete these and "
   "re-write them.\n\n"
   "If  you are transfering a record on a day you did not create the documents, After transfering the record, you should go into the patient's notes and delete the printed documents "
   "from the documents list - the record of this will remain in the database as but will be 'stuck through'. You can then re-generate the documents if you desire.\n\n"
   "It is more than possible in future releases of EasyGP that a more comprehensive tranfer will be possible. Note also that an underlying audit trail is created when you transfer records."
   
   From_patient_Progress_Notes = fppn
   WebView1.HTML = Notes
   From_Patient = fp
   FPR = New CPatientSelectRow(ListContainer1, From_Patient)
   With cvwPatients
      .Columns.count = 3
   End With
   fk_consult = From_patient_Progress_Notes!fk_consult
   cvwPatients.Columns.count = 6
   Message.Info(sMsg)
   
End

Public Sub btnOK_Click()
   '---------------------------------------------------
   'User has decided to go ahead and transfer the notes
   '---------------------------------------------------
   
   Transfer()
   Me.Close(True)
   
End

Public Sub btnCancel_Click()
   
   Me.Close
   
End

Public Sub EditArea_Clear()
   
   With txtFindPatient
      .BackGround = Color.rgb(95, 255, 175)
      .Clear()
   End With
   With cvwPatients
      .Visible = True
      .Clear()
   End With
   ListContainer2.Visible = False
   btnOK.Enabled = False
   
End

Public Sub EditAreaTextBox_GotFocus()
   '------------------------------------------------------
   'User has clicked on the search for patient textbox
   'clear it, and ensure patient list columnview is visible
   '--------------------------------------------------------
   
   EditArea_Clear()
   
End

Public Sub Patients_Find()
   '----------------------------------------------------------
   'User is searching for a patient to transfer the records to
   '----------------------------------------------------------
   
   Dim patient As Collection
   Dim sql_in_english As String
   
   Inc Application.Busy
   Timer1.Enabled = False
   Patients = modContactsDBI.patients_get_firstname_surname(Trim(txtFindPatient.text))
   sql_in_english = Patients!sql_in_english
   Patients.Remove("sql_in_english")
   If Patients.count = 0 Then
      txtFindPatient.SetFocus()
      Dec application.Busy
      Return
   Else
      '----------------------------------------------------
      'One or more patients.
      'If one, get their photo if it exists and load record
      '----------------------------------------------------
      If Patients.count = 1 Then
         For Each To_Patient In Patients 'no interaction = no collection
            Patient_Show(To_Patient)
         Next
      Else
         cvwPatients.Clear()
         For Each patient In Patients
            cvwPatients.Add(patient!pk_view, 0)
            cvwPatients[patient!pk_view][0] = patient!surname
            cvwPatients[patient!pk_view][1] = patient!firstname
            cvwPatients[patient!pk_view][2] = patient!age_display
            cvwPatients[patient!pk_view][3] = Format(patient!birthdate, "dd/mm/yyyy")
            cvwPatients[patient!pk_view][4] = patient!sex
            cvwPatients[patient!pk_view][5] = Trim(patient!street1 & " " & patient!street2) & " " & patient!town
         Next
         ' With cvwPatients
         '    .MoveFirst
         '    .SetFocus
         '    .Item.Selected = True
         ' End With
      Endif
   End If
   Dec application.Busy
   
End

Public Sub Patient_Show(Patient As Collection)
   '-----------------------------------------------------------------------------
   'shows patient details + photo in the list container to make it really obvious
   '-----------------------------------------------------------------------------
   
   cvwPatients.Visible = False
   With ListContainer2
      .Visible = True
      .Clear()
   End With
   To_Patient_Row = New CPatientSelectRow(ListContainer2, Patient) As "Patient_Row"
   
End

Public Sub cvwPatients_KeyPress()
   '-------------------------------------------------------------
   'User has hit the enter key on the patients list, show details
   '-------------------------------------------------------------
   
   If Key.code = Key.Return Then cvwPatients_DblClick()
   
End

Public Sub cvwPatients_DblClick()
   '-----------------------------------------------------------
   'User has selected a patient = show expanded details + photo
   '-----------------------------------------------------------
   
   cvwPatients.MoveCurrent()
   To_Patient = Patients[cvwPatients.Item.Key]
   Patient_Show(To_Patient)
   btnOK.Enabled = True
   
End

Public Sub EditAreaTextBox_Activate()
   '------------------------------------------------
   'If user hits <enter> key, search for the patient
   '------------------------------------------------
   
   '   Patients_Find()
   
End

Public Sub EditAreaTextBox_KeyRelease()
   
   Timer1.Enabled = True
   timer_count = 0
   
End

Public Sub timer1_Timer()
   '------------------------------------------------------------------------------
   'If the timer is running, user is typing in either the brand or generic textbox
   '------------------------------------------------------------------------------
   
   Inc timer_count
   If timer_count > 3 Then
      Patients_Find()
   Endif
   
End

Public Sub EditAreaTextBox_KeyPress()
   
   If Key.code = Key.down Then
      If Patients.count Then
         With cvwPatients
            .MoveFirst
            .SetFocus
            .Item.Selected = True 'bug here fixme
         End With
      Endif
   Endif
   Catch
      Return 
End

Public Sub EditAreaTextBox_Change()
   
   If Trim(Last.text) = "" Then
      EditArea_Clear()
   Endif
   
End

Public Sub Patient_Row_Click()
   '--------------------------------------
   'user has clicked on list container row
   '--------------------------------------
   
   btnOK.Enabled = True
   
End

Public Sub Transfer()
   
   '-----------------------------------------------------------------------------------------------------------------------------
   'User has decided to transfer the notes from one patient to another, this is complicated
   'Examples
   '1) I've written in the wrong patient's notes a complete consultation
   '   - in this scenario I want to transfer the whole fk_consult
   '   - if the destination patient did have a consultation that day that I wrote in, use that fk_consult
   '   - otherwise, will have to create a new consult.pk to use
   '2) I've actually written part of a consultation into the wrong patients notes
   '   - ? how could this happen you say, well visualise this scenario
   '   - a patient comes in for an INR, I just print out the form, and don't print anything in the general notes
   '     In this situation the progress notes get an entry for this fk_referral.
   '   - I then forget to change patients. The next patient comes in, sits down, and I write in the general notes and save this.
   '     without looking back at the previous notes tab.
   '   - some time later I discover the mixup - ie parts of two consulations in the one notes. I'm able to identify which is which
   '   - and want to transfer.
   '------------------------------------------------------------------------------------------------------------------------------
   ' As we progress along -- give them a change to back out
   ' i) first check if there was > 1 progress note (pk_progressnote) for the patient **from whose notes we are transfering ***
   ' on that day in question  if yes, then make sure they only want part of the consultation transfered
   ' Suggestion: fixme - (may need dto put up other notes on the day the user hasn't selected, under the one they did)
   '    if no, then
   '       -> search... did the destination patient have a consult that day
   '          yes? then its ok, can just use that fk_consult
   '         if no, then have to create  a new fk_consult, with the data of the transferred consultattion and appropridate audit)\
   ' FIXME PUT IN AN AUDIT TRAIL
   '-------------------------------------------------------------------------------------------------------
   Dim sql As String
   Dim other_notes As Collection
   Dim R As Result
   Dim sMsg As String
   Dim fk_consult_destination As Integer                                'if exists, the destination patients fk_consult on same date
   Dim New_consult As Collection
   Dim bTransferSingleProgressNote As Boolean
   '----------------------------------------------------------------------------------------------------------------------
   'Firstly, did the patient we are transferring from, have more than one progress note for that day
   'e.g if you wrote notes + printed a letter + printed a request there would be three keys in cin_consult.progress_notes
   'if so, we can't just swap    ' FIXME PUT IN AN AUDIT TRAIL
   '----------------------------------------------------------------------------------------------------------------------
   other_notes = modDBConnect.exec_query_collection("Select * from clin_consult.progressnotes where fk_consult = " & fk_consult)
   If other_notes.count <> 1 Then
      sMsg = "EasyGP notes that you are only transferring part of a patients consultation notes.\n\n"
      sMsg &= "Do you wish to proceed"
      If Message.Question(sMsg, "Yes", "No") = 2 Then Return
      bTransferSingleProgressNote = True
      '---------------------------------------------------------------------------------
      'did the patient we are trying to transfer to, have a consultation on the same day
      'This would seem possible if the user has typed in the wrong notes, but they
      'still could have attended and we just had no entry.
      'If they did, we can just swap this progress note to their existing fk_consult
      '---------------------------------------------------------------------------------
      
      sql = "Select * from clin_consult.consult where fk_patient =" & To_Patient!fk_patient
      sql &= " And consult_date::date = $$" & Format(From_patient_Progress_Notes!consult_date, "yyyy-mm-dd") & "$$"
      R = modDBConnect.exec_query(sql)
      If R.count Then
         '-----------------------------------
         'Yes, there is a consult key already
         '-----------------------------------
         fk_consult_destination = r!pk
         sMsg = "EasyGP has determined that the destination patient did have a consultation on the same day as the patient "
         sMsg &= "from whose progress notes you are transferring these notes.\n\n"
         sMsg &= "Proceed with the transfer?"
         If Message.Question(smsg, "Yes - transfer the notes", "No") = 2 Then Return
         Goto Transfer_Progress_Note
      Else
         sMsg = "EasyGP has determined that the destination patient has no consultation recorded for the same day.\n\n"
         sMsg &= "You will need to confirm that you just have written in the wrong notes, an audit trail will be created "
         sMsg &= "and the progress note you have requested to move will be transferred."
         If Message.Question(smsg, "Confirm - create new consultation", "Cancel") = 2 Then Return
         Goto Create_Consult
      Endif
      
   Else
      '--------------------------------------------------------------------------------------------------------------------
      'There is only 1 consultation note on the day in the from patient's notes, so just swap the patient key in fk_consult
      'i.e effectively transfer's the whole consultation
      '--------------------------------------------------------------------------------------------------------------------
      If Message.Warning("Please confirm transfer of the entire consultation - this cannot be undone.", "Yes - transfer consultation notes", "Cancel") = 2 Then Return
      Goto Transfer_Consult
   End If
   
Create_Consult:
   Print From_patient_Progress_Notes
   New_consult = New Collection
   New_consult!fk_patient = To_Patient!fk_patient
   New_consult!consult_date = From_patient_Progress_Notes!consult_date
   New_consult!fk_staff = From_patient_Progress_Notes!fk_staff
   New_consult!fk_type = From_patient_Progress_Notes!fk_type
   New_consult!summary = From_patient_Progress_Notes!summary
   fk_consult_destination = modDBConnect.insert("clin_consult.consult", New_consult)
   If bTransferSingleProgressNote Then
      Goto Transfer_Progress_Note
   Else
      Goto Transfer_Consult
   Endif
   Return
   
Transfer_Consult:
   modDBConnect.update("clin_consult.consult", Null, ["pk": fk_consult, "fk_patient": To_Patient!fk_patient])
   modDBConnect.CommitTrans()
   bTransferComplete = True
   Return
Transfer_Progress_Note:
   modDBConnect.update("clin_consult.progressnotes", Null, ["pk": From_patient_Progress_Notes!pk_progressnote, "fk_consult": fk_consult_destination])
   modDBConnect.CommitTrans()
   bTransferComplete = True
   
End
