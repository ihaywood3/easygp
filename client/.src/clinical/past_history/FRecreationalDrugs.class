' Gambas class file

' Copyright (C) 2008-2016 Dr. Richard Terry
' This program is free software: you can redistribute it and/or modify
' it under the terms of the GNU General Public License as published by
' the Free Software Foundation, either version 3 of the License, or
' (at your option) any later version.

' This program is distributed in the hope that it will be useful,
' but WITHOUT ANY WARRANTY; without even the implied warranty of
' MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
' GNU General Public License for more details.
' You should have received a copy of the GNU General Public License
' along with this program.  If not, see <http://www.gnu.org/licenses/>.
' ---------------------------------------------------------------------------------------------------
' PURPOSE     - A module to allow entry of persons recreational drug use
'----------------------------------------------------------------------------------------------------

Private currentconsult As CConsult
Private bExit As Boolean
Private bIniting As Boolean
Private bEditing As Boolean
Private substance As Collection
Private substances As Collection
Private RecreationalDrugs As Collection
Private fk_progressnote As Variant
Private fk_recreationaldrug As Variant
Private cvwRecreationalDrugs_key As Integer 'this one is an interger is keyed sequentially 0>n
Private common_lu_route_administration_keys As Variant[] 'only use a few match these to the cmbRouteTaken



Public Sub Init(cons As CConsult)   
   
   currentconsult = cons
   lblmeasure.text = " Authority Approval No.  "  'to keep all edit area's left width the same the script one is the widest
   modEditAreaHelpers.Resize_labels(Vbox_EditArea, lblMeasure)
   substances = modUtilGUI.LoadCombo(cmbSubstances, modUtil.Copy_Collection_Keyed_Sequentially(modRecreationalDrugs.Recreational_Drugs_Get()), "drug")
   cmbRouteTaken.Add("inhaled", 0)
   cmbRouteTaken.Add("intramuscular", 1)
   cmbRouteTaken.Add("intranasal", 2)
   cmbRouteTaken.Add("intravenous", 3)
   cmbRouteTaken.Add("oral", 4)
   cmbRouteTaken.Add("subcutaneous", 5)
   cmbRouteTaken.Add("topical", 6)
   common_lu_route_administration_keys = New Variant[7]
   common_lu_route_administration_keys[0] = const.route_administration_inhaled
   common_lu_route_administration_keys[1] = const.route_administration_intramuscular
   common_lu_route_administration_keys[2] = const.route_administration_intranasal
   common_lu_route_administration_keys[3] = const.route_administration_intravenous
   common_lu_route_administration_keys[4] = const.route_administration_oral
   common_lu_route_administration_keys[5] = const.route_administration_subcutaneous
   common_lu_route_administration_keys[6] = const.route_administration_topical
   With cvwRecreationalDrugs
      .Columns.count = 6
      .Columns[0].text = "Substance"
      .Columns[1].text = "Route"
      .Columns[2].text = "Age Details"
      .Columns[3].text = "Amount"
      .Columns[4].text = ""
      .Columns[5].text = "Notes"
   End With
   bIniting = True
   cmbSubstances_Click()
   Try Settings_Load
   Reload()
   tlSubstanceInfo.text = ""
   bIniting = False
   EditArea_Notify_DataChange(False)
   
End

Public Sub Save()
   '---------------------------------------------------------------------------------------------------------------------
   'Save the recreational drug
   '
   ' CREATE TABLE clin_history.recreational_drugs
   ' (
   '   pk serial NOT NULL,
   '   fk_consult integer NOT NULL,
   '   fk_lu_recreational_drug integer NOT NULL, -- foreign key to common.lu_recreational_drugs
   '   age_started integer, -- the age the patient first used this drug
   '   age_last_used integer, -- the age the patient last used or stopped using the drug
   '   substance_amount integer, -- the quantity of the substance e.g 10 if say 10gm of alcohol per day see lu_substance_frequency
   '   fk_substance_unit_amount integer, -- the units for the substance amount eg gm key to common.lu_units (to be restricted in the front end
   '   fk_lu_substance_frequency integer, -- foreign key to common.lu_units but front end will allow on day/week/month/year
   '   fk_lu_route_administration integer, -- keyColor.rgb(95, 255, 175) to common.lu_route_administration
   '   notes text, -- any qualifying information about the drug eg, 'used intermittently 20-30, stopped in 30's, started again in 40's
   '   deleted boolean,
   '   CONSTRAINT recreational_drugs_pkey PRIMARY KEY (pk),
   '   CONSTRAINT data_recreational_drugs_fk_consult FOREIGN KEY (fk_consult)
   '       REFERENCES clin_consult.consult (pk) MATCH SIMPLE
   '       ON UPDATE NO ACTION ON DELETE NO ACTION,NotificationFlag
   '   CONSTRAINT data_recreational_drugs_fk_lu_recreational_drug FOREIGN KEY (fk_lu_recreational_drug)
   '       REFERENCES common.lu_recreational_drugs (pk) MATCH SIMPLE
   '       ON UPDATE NO ACTION ON DELETE NO ACTION
   ' )
   ' WITH (bstance!drug & ".\n\n"
   '   OIDS=FALSE
   ' )
   '-----------------------------------------------------------------------------------------------------------------------
   
   Dim recreationalDrug As CRow
   Dim NotificationFlag As Integer = const.cSmokerUnknown
   Dim NotificationMessage As String 'summary to be passed to the FClinical Toolbar eg EX-SMOKER ceased 42yrs age
   
   If HBox_EditArea.Padding = 0 Then Return
   If Not RecreationalDrug_Valid() Then Return
   HBox_EditArea.Enabled = False
   
   recreationalDrug = New CRow
   If Not IsNull(fk_recreationaldrug) Then
      recreationalDrug.put_unchanged(fk_recreationaldrug, "fk_recreationaldrug")
   Endif
   recreationalDrug!fk_consult = currentconsult.GetPK()
   recreationalDrug!fk_lu_recreational_drug = substances[cmbSubstances.index]!pk
   recreationalDrug!age_started = Val(Trim(txtAgeStarted.text))
   recreationalDrug!age_last_used = Val(Trim(txtAgeLastUsed.text))
   recreationalDrug!substance_amount = Val(Trim(txtQuantity.text))
   If rbMonth.Value = True Then
      recreationalDrug!fk_lu_substance_frequency = const.Units_month
   Endif
   If rbDay.Value = True Then
      recreationalDrug!fk_lu_substance_frequency = const.Units_day
   Endif
   If rbWeek.Value = True Then
      recreationalDrug!fk_lu_substance_frequency = const.Units_week
   Endif
   recreationalDrug!fk_lu_route_administration = common_lu_route_administration_keys[cmbRouteTaken.index]
   recreationalDrug!notes = Trim(txtNotes.text)
   recreationalDrug!cumulative_amount = Trim(txtPackYears.Text)
   recreationalDrug!never_used_drug = CBool(chkNeverUsedDrug.value)
   If rbConfidentialNo.value = True Then
      recreationalDrug!confidential = False  
   End If
   If rbConfidentialYes.Value = True Then
      recreationalDrug!confidential = True   
   Endif
   recreationalDrug!fk_progressnote = modConsultDBI.ProgressNote_Save(currentconsult, ProgressNotes_Create(), const.cSection_RecreationalDrugs, fk_progressnote, "Recreational Drugs")
   recreationalDrug.Save("clin_history.recreational_drugs", "fk_recreationaldrug")
   modDBConnect.CommitTrans()
   EditArea_Notify_DataChange(False)
   HBox_EditArea.Enabled = False
   If recreationalDrug!fk_lu_recreational_drug = const.cRecreational_Drug_Nicotine Then  'work out the text to show in FClinical about smoking
      If recreationalDrug!never_used_drug Then
         NotificationFlag = const.cSmokerNever
      Else
         If Not IsNull(recreationalDrug!age_last_used) Then
            NotificationFlag = const.cSmokerPast
            NotificationMessage = " CEASED AGE " & Str(recreationalDrug!age_last_used)
         Else
            NotificationFlag = const.cSmokerCurrent
         End If
      End If
      If Not IsNull(recreationalDrug!cumulative_amount) Then
         NotificationMessage &= " " & Str(recreationalDrug!cumulative_amount) & " PY"
      End If  
   End If   
   FClinical.Refresh_Section(const.cSection_HealthSummary)
   FClinicalToolbar.Smoking_Nofication_Update(NotificationFlag, NotificationMessage)
   Try FGPMPTCA.GPMP_update
   Reload()
   
End

Public Sub ProgressNotes_Create() As String
   '-----------------------------------------------------------------
   'Create a progress note entry for the substance along the lines of
   'current smoker 10/day or ex-smoker 15/day from age 23 to 50
   'fixme remove the fk_progressnote 
   '-----------------------------------------------------------------
   
   Dim sPN As String
   
   sPN = "<B> " & cmbSubstances.text
   If Not IsNull(fk_recreationaldrug) Then
      sPN &= " details updated:</B> "
   Else
      sPN &= " details added:</B> "
   End If
   If chkNeverUsedDrug.value = True Then
      Select Case cmbSubstances.text
         Case "nicotine"
            sPn &= "never smoked."
         Case "alcohol"
            sPn &= "never drunk alcohol."
      End Select
      Return sPN
   End If
   '----------------------------------------
   'ok, they have used the drug in some form
   '----------------------------------------
   Select Case cmbSubstances.text
      Case "nicotine"
         If Trim(txtAgeLastUsed.text) = "" Then
            sPn &= "currently smokes "
         Else
            sPn &= "ex smoker "
         End If
         If txtQuantity.text <> "" Then
            sPn &= txtQuantity.text & " per "
         Else
            sPn &= "? per "
         Endif
         If rbDay.Value = True Then
            sPn &= "day "
         Endif
         If rbWeek.Value = True Then
            sPn &= "week "
         Endif
         If Not rbWeek.value And Not rbDay.value Then
            sPn &= " ?day ?week "
         Endif
      Case "alcohol"
         If Trim(txtAgeLastUsed.text) = "" Then
            sPn &= "currently drinks "
         Else
            sPn &= "ex-drinker"
         End If
         If txtQuantity.text <> "" Then
            sPn &= txtQuantity.text & "gms per "
         Else
            sPn &= "?gms per "
         End If
         If rbDay.Value = True Then
            sPn &= "day "
         Endif
         If rbWeek.Value = True Then
            sPn &= "week "
         Endif
   End Select
   
   sPN &= "from age "
   If Trim(txtAgeStarted.text) <> "" Then
      sPN &= txtAgeStarted.text & " yrs "
   Else
      sPN &= " ? "
   Endif
   If Trim(txtAgeLastUsed.text) <> "" Then
      sPN &= " to age " & txtAgeLastUsed.Text
   Endif
   Return sPN
   
End

Public Sub Reload()
   '----------------------------------
   'Reload the users recreational drugs
   'fixme remove NotificationFlag
   '-----------------------------------
   
   Dim recreationalDrug As Collection
   Dim sString As String
   Dim NotificationFlag As Integer = const.cSmokerUnknown 'the default.
   Dim x As Integer
   
   EditArea_Clear()
   cvwRecreationalDrugs.clear()
   currentconsult.Refresh("recreationaldrugs")                    'force reload from back end
   RecreationalDrugs = modUtil.Copy_Collection_Keyed_Sequentially(currentconsult!recreationaldrugs)
   For Each recreationalDrug In RecreationalDrugs
      cvwRecreationalDrugs.Add(x, 0)
      cvwRecreationalDrugs[x][0] = recreationalDrug!drug
      cvwRecreationalDrugs[x][1] = recreationalDrug!route
      If recreationalDrug!never_used_drug = False Then                         'the user has taken a history and never used the drug
         sString = "Age started:"
         If Not IsNull(recreationalDrug!age_started) Then
            sString &= recreationalDrug!age_started
         Else
            sSTring &= "?"
         Endif
         If IsNull(recreationalDrug!age_last_used) Then
            sSTring &= " " & recreationalDrug!age_last_used
            cvwRecreationalDrugs[x][4] = " Current "
            NotificationFlag = const.cSmokerCurrent
         Else
            cvwRecreationalDrugs[x][4] = " Has Quit "
            sString &= " to " & recreationalDrug!age_last_used
            NotificationFlag = const.cSmokerPast
         Endif
         cvwRecreationalDrugs[x][2] = sString
         If Not IsNull(recreationalDrug!substance_amount) Then
            sString = "Quantity " & recreationalDrug!substance_amount & " " & recreationalDrug!quantification
            Select Case recreationalDrug!fk_lu_substance_frequency
               Case const.Units_day
                  sString &= " per day "
               Case const.Units_week
                  sString &= " per week "
               Case const.Units_month
                  sString &= " per month "
            End Select
            cvwRecreationalDrugs[x][3] = sString
         Endif
         cvwRecreationalDrugs[x][5] = recreationalDrug!notes
      Else
         NotificationFlag = const.cSmokerNever
         cvwRecreationalDrugs[x][5] = "NEVER USED THIS DRUG"
      End If
      Inc x
   Next
   modUtilGUI.Columnview_Columns_Set_Size(cvwRecreationalDrugs, lblMeasure)
   lblSubstance.SetFocus()
   
End

Public Sub mnuRecreationalDrugs_Click()
   '-----------------------------------------------------------
   'User has clicked on one of the menu items on the popup menu
   'over cvwRecreationalDrugs
   '-----------------------------------------------------------
   
   Select Case Last.tag
      Case "edit"
         RecreationalDrug_Edit
      Case "delete"
         RecreationalDrug_Delete
      Case "font"
         modUtilGUI.Columnview_SetFont(cvwRecreationalDrugs, "FRecreationalDrugs")
      Case "help"
         modUtilGUI.NotImplemented("Help")
   End Select
Catch
   Return 
   
End

Public Sub cvwRecreationalDrugs_Select()
   
   cvwRecreationalDrugs.MoveCurrent
   cvwRecreationalDrugs_key = cvwRecreationalDrugs.Item.Key
   RecreationalDrug_Display
   
End

Public Sub cvwRecreationalDrugs_Menu()
   
   If cvwRecreationalDrugs.count Then
      mnuRecreationalDrugs.popup()
   Endif
   
End

Public Sub New_Entry()
   '---------------------------------------------------------------
   'Called from FClinical, called this because can't use New() and
   'all across easyp this is the wording I've chosen
   'see FClinicalToolbar.MainToolbar_Click for details
   '---------------------------------------------------------------
   
   RecreationalDrug_New()
   
End

Public Sub RecreationalDrug_Edit()
   'Edit the drug if on different date to when last edited, null the fk_progressnote
   
   Hbox_EditArea.Enabled = True
   If Not modUtil.IsSameDay(currentconsult.GetConsultDate(), RecreationalDrugs[cvwRecreationalDrugs_Key]!consult_date) Then 
      fk_progressnote = Null  
   Endif
   bEditing = True   
   Print fk_progressnote

End

Public Sub RecreationalDrug_New()
   '--------------------------------------------------------
   'Clear the edit area for input of a new recreational drug
   '--------------------------------------------------------
   
   EditArea_Clear()
   HBox_EditArea.Enabled = True
   cmbSubstances.SetFocus()
   
End

Public Sub RecreationalDrug_Delete()
   '----------------------------------------------------------------
   'Delete the reacreational drug laying down the audit trail  
   'if the fk_progressnote for that drug is null then drug was imported
   'i.e the field progressnotes = null
   '-----------------------------------------------------------------
   
   Dim sMsg As String
   Dim auditnotes As String
   Dim RD As Collection 
   
   cvwRecreationalDrugs.MoveCurrent
   RD = RecreationalDrugs[cvwRecreationalDrugs_key]
   SMsg = "Are you sure you want to delete this drug?\n\n"
   sMsg = RD!drug
   If Message.Question(sMsg, "Yes - Delete Drug", "No") = 2 Then Return
   If RD!fk_consult = currentconsult.GetPK()                                                                          'the drug was created today, just reverse this
      modDBConnect.update("clin_history.recreational_drugs", Null, ["pk": fk_recreationaldrug, "deleted": True])      'mark the record as deleted
      modDBConnect.exec_query("Update clin_consult.progressnotes set deleted = True where pk=" & RD!fk_progressnote)
      modDBConnect.CommitTrans()
   Else                                                                                                               'this is an old entry
      modDBConnect.update("clin_history.recreational_drugs", Null, ["pk": fk_recreationaldrug, "deleted": True])      'mark the record as deleted
      If Not IsNull(RD!progress_notes) Then                                                                            'if progress notes for this drug exist
         auditnotes = "<STRIKE><P><FONT COLOR='#b3b3b3'>" & RD!progress_notes & "</STRIKE><p>"                       'strike them through in the persons notes with an annotation
         auditnotes &= "<small><B><I>Audit note:</B> these recreational drug details were deleted by " & modDBConnect.currentUser!wholename & " on " & Format(currentconsult.GetConsultDate(), "dd/mm/yyyy") & ". Check the audit trail on that date for details.</I></small>"
         modDBConnect.exec_query("update clin_consult.progressnotes set notes =$$" & auditnotes & "$$ where pk = " & RD!fk_progressnote)
         Goto Refresh
      End If 
      auditnotes = "Details for the drug " & RD!drug & "<BR>"                                                         'create audit notes
      auditnotes = modUtil.Ucase_Word_First_Letter(Replace(Trim(ProgressNotes_Create()), "updated", "deleted")) 
      auditnotes = Replace(auditnotes, "<b>", "")
      auditnotes = Replace(auditnotes, "</b>", "")
      If modAudit.MakeAudit(currentconsult, "mark deleted", "clin_history.recreational_drugs", fk_recreationaldrug, const.cSection_RecreationalDrugs, auditnotes) Then
         modDBConnect.CommitTrans()
      Else
         modDBConnect.RollBack
      End If   
   End If   
   
Refresh:
   Try FClinical.Refresh_Section(const.cSection_HealthSummary) 
   Reload()
   
End

Public Sub RecreationalDrug_Display()
   '-------------------------------------------------------------------
   'Display the recreational drug details in the editing area
   'this remains uneditable until Edit selected form tool bar or menu
   '-------------------------------------------------------------------
   
   Dim RecreationalDrug As Collection
   
   EditArea_Clear()
   cvwRecreationalDrugs.MoveCurrent()
   RecreationalDrug = RecreationalDrugs[cvwRecreationalDrugs_Key]
   bIniting = True
   cmbSubstances.index = cmbSubstances.Find(RecreationalDrug!drug)
   bIniting = False
   bExit = True
   txtAgeStarted.text = RecreationalDrug!age_started
   txtAgeLastUsed.text = RecreationalDrug!age_last_used
   txtPackYears.text = RecreationalDrug!cumulative_amount 'only used for nicotine
   txtQuantity.text = RecreationalDrug!substance_amount
   Select Case RecreationalDrug!fk_lu_substance_frequency
      Case const.Units_day
         rbDay.value = True
      Case const.Units_week
         rbWeek.Value = True
      Case const.Units_month
         rbMonth.Value = True
   End Select
   If RecreationalDrug!confidential Then
      rbConfidentialYes.value = True
   Else
      rbConfidentialNo.value = True 
   Endif
   fk_progressnote = RecreationalDrug!fk_progressnote
   chkNeverUsedDrug.value = RecreationalDrug!never_used_drug
   txtNotes.text = RecreationalDrug!notes
   fk_recreationaldrug = RecreationalDrug!pk
   cmbRouteTaken.index = cmbRouteTaken.Find(RecreationalDrug!route)
   bExit = False
   EditArea_Notify_DataChange(False)
   
End

Public Sub RecreationalDrug_Valid() As Boolean
   '-----------------------------------------------------------------
   'Returns true if enough information to save this recreational drug
   '-----------------------------------------------------------------
   
   Dim sMsg As String
   
   For Each substance In currentconsult!recreationaldrugs
      If substances[cmbSubstances.index]!pk = substance!fk_lu_recreational_drug And bEditing = False Then
         Message.title = "Existing Recreational Drug"
         sMsg = "You have already entered data for " & substance!drug & ".\n\n"
         sMsg &= "Please edit existing data for this drug if you wish to modify it."
         Message.info(sMsg)
         RecreationalDrug_New
         Return
      Endif
   Next
   If cmbSubstances.index = -1 Then
      cmbSubstances.SetFocus()
      Return
   Endif
   If cmbRouteTaken.index = -1 Then
      cmbRouteTaken.SetFocus()
      Return
   Endif
   If Trim(txtQuantity.text) = "" And chkNeverUsedDrug.value = False Then
      txtQuantity.Background = Color.rgb(95, 255, 175)
      txtQuantity.SetFocus()
      Return
   Endif
   If rbConfidentialNo.value = False And rbConfidentialYes.Value = False Then
      Message.info("Please enter confidentiality information for this drug")
      Return 
   Endif
   Return True
   
End

Private Sub Settings_Load()
   
   Vsplit_Main.Layout = Settings["FRecreationalDrugs/VSplit_Main.Layout", modUtilGUI.VSplit([299, 558])]
   cvwRecreationalDrugs.font = Font[Settings["RecreationalDrugs/cvwRecreationalDrugs.font", "DejaVu Sans,9"]]
   
End

Public Sub Settings_Save()
   
   Settings["FRecreationalDrugs/VSplit_Main.Layout"] = Vsplit_Main.Layout
   
End

Public Sub Form_Close()
   '------------------
   'Save splits layout
   '------------------
   
   Settings_Save()
   
End

Public Sub EditArea_Clear()
   
   bExit = True
   modEditAreaHelpers.EditArea_Clear(Vbox_EditArea)
   fk_recreationaldrug = Null
   fk_progressnote = Null
   rbReset.value = True 'unsets the day/month/week radiobuttons
   bexit = False
   bEditing = False
   cmbSubstances.index = -1
   cmbRouteTaken.index = -1
   rbReset.value = True 
   Hbox_EditArea.Enabled = False 
   bEditing = False  
   EditArea_Notify_DataChange(False)
   
End

Public Sub EditAreaTextBox_GotFocus()
   
   Last.BackGround = Color.rgb(95, 255, 175)
   
End

Public Sub EditAreaTextBox_LostFocus()
   
   Last.Background = Color.White
   
End

Public Sub EditArea_Notify_DataChange(flag As Boolean)
   '--------------------------------------------------
   'Adds or removes padding around edit area and lists
   'to indicate if data is changed and unsaved
   '--------------------------------------------------
   
   If bexit Then Return
   If flag Then
      HBox_EditArea.Padding = 1
   Else
      HBox_EditArea.Padding = 0
   End If
   
End

Public Sub EditAreaTextBox_KeyPress()
   '-------------------------------------------------------------
   'first make sure only valid keys for each text box are entered
   'here effectively this means only the date
   '--------------------------------------------------------------
   
   Dim bKeyValid As Boolean
   
   bkeyvalid = EditAreaTextBox_ExcludeKeys(key.code, Last.tag)
   If bkeyvalid = False Then
      Stop Event
      Return
   End If
   Select Case Key.Code
      Case Key.Return, Key.tab
         Select Case Last.tag
            Case "age started"
               txtAgeLastUsed.SetFocus()
            Case "age last used"
               txtQuantity.SetFocus()
            Case "quantity"
               rbDay.SetFocus()
            Case "pack years"
               cmbRouteTaken.SetFocus()
         End Select
   End Select
   
End

Public Sub EditAreaTextBox_Change()
   
   EditArea_Notify_DataChange(True)
   If Last.tag = "age last used" And Len(Trim(txtAgeLastUsed.text)) > 0 Then
      If Trim(txtAgeStarted.text) <> "" Then    
         If Val(Trim(txtAgeLastUsed.text)) > currentconsult!patient!age_numeric Or currentconsult!patient!age_numeric < Val(Trim(txtAgeStarted.text)) Then 'wayne kildney bugged out tried to add age stopped.,
            bexit = False
            Last.text = ""
            bExit = True
         Endif
      End If   
   Endif
   Select Case Last.tag
      Case "age started", "age last_used", "pack years"
         If Len(Last.text) Then chkNeverUsedDrug.value = False
   End Select
   
End

Public Function EditAreaTextBox_ExcludeKeys(keycode As Integer, tag As String) As Boolean
   
   Dim bKeyValid As Boolean
   
   Select Case tag
      Case "pack years", "age started", "age last used", "quantity"
         bKeyValid = modUtilGUI.AllowKeys(const.AllowKeys_NumbersOnly, keycode)
      Case Else
         bKeyValid = True
   End Select
   Return bKeyValid
   
End

Public Sub cmbSubstances_Click()
   
   Dim sMsg As String
   
   If bexit Then Return
   '-----------------------------------------------------------------------
   'First check the user is not stupid enough to be starting another record
   'for a substance already recorded
   '-----------------------------------------------------------------------
   If Not bIniting Then
      For Each substance In currentconsult!recreationaldrugs
         If substances[cmbSubstances.index]!pk = substance!fk_lu_recreational_drug Then
            Message.title = "Existing Recreational Drug"
            sMsg = "You have already entered data for " & substance!drug & ".\n\n"
            sMsg &= "Please edit existing data for this drug if you wish to modify it."
            Message.info(sMsg)
            Return
         Endif
      Next
   End If
   
   If cmbSubstances.text = "alcohol" Then
      lblQuantity.text = "gms per"
   Endif
   lblSubstanceAKA.text = substances[cmbSubstances.index]!alternative_names
   tlSubstanceInfo.text = substances[cmbSubstances.index]!alternative_names
   If cmbSubstances.text = "nicotine" Then
      tlSubstanceInfo.text &= "<BR><BR>A pack year is defined as twenty cigarettes smoked everyday for one year."
   Endif
   
   cmbRouteTaken.index = cmbRouteTaken.Find(substances[cmbSubstances.index]!route)
   lblQuantity.text = substances[cmbSubstances.index]!quantification & " per  "
   If cmbSubstances.text = "nicotine" Then
      HBox_PackYears.Visible = True
   Else
      HBox_PackYears.Visible = False
   End If
   EditArea_Notify_DataChange(True)
   
End

Public Sub EditArea_RadioButtons_Click()
   
   EditArea_Notify_DataChange(True)
   
End

Public Sub EditArea_RadioButtons_KeyPress()
   
   If bexit Then Return
   Select Case Key.Code
      Case Key.Tab, Key.Return
         Select Case Last.tag
            Case "day"
               rbWeek.SetFocus()
            Case "week"
               rbMonth.SetFocus()
            Case "month"
               If cmbSubstances.text = "nicotine" Then
                  txtPackYears.SetFocus()
               Else
                  cmbRouteTaken.SetFocus
               Endif
               
         End Select
   End Select
   
End

Public Sub cmbRouteTaken_KeyPress()
   
   If bexit Then Return
   txtNotes.SetFocus()
   
End  

Public Sub chkNeverUsedDrug_Click()
   
   If bexit Then Return
   EditArea_Notify_DataChange(True)
   If Last.value = True Then  
      txtAgeLastUsed.text = ""
      txtAgeStarted.text = ""
      rbReset.value = True
      txtNotes.text = ""
      txtPackYears.text = ""
      txtQuantity.text = ""
   End If
   
End
