' Gambas class file

' Copyright (C) 2008-2015 Dr. Richard Terry

' This program is free software: you can redistribute it and/or modify
' it under the terms of the GNU General Public License as published by
' the Free Software Foundation, either version 3 of the License, or
' (at your option) any later version.

' This program is distributed in the hope that it will be useful,
' but WITHOUT ANY WARRANTY; without even the implied warranty of
' MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
' GNU General Public License for more details.

' You should have received a copy of the GNU General Public License
' along with this program.  If not, see <http://www.gnu.org/licenses/>.
'----------------------------------------------------------------------
'A module to record ante-natal visits
'----------------------------------------------------------------------
Private icounter As Integer
Private bExit As Boolean
Private bKeyValid As Boolean
Private cTableColumn_Date As Integer = 0
Private cTableColumn_duration As Integer = 1
Private cTableColumn_FundalHeight As Integer = 2
Private cTableColumn_Presentation As Integer = 3
Private cTableColumn_FHS As Integer = 4
Private cTableColumn_Urinalysis As Integer = 5
Private cTableColumn_BP As Integer = 6
Private cTableColumn_Weight As Integer = 7
Private cTableColumn_FMF As Integer = 8
Private cTableColumn_Notes As Integer = 9
Private currentconsult As CConsult
Private Presentations As Collection
Private antenatal_visits As Collection
Private fk_visit As Integer
Private fk_pregnancy As Integer
Private fk_progressnote As Integer
Private progressnotes_for_visit As String 'this is used for auditing if changes occur
Private currentPregnancy As Collection
Private bDeclinedToMeasureBP As Boolean
Private bDeclinedToMeasureFHH As Boolean
Private bDeclinedToMeasureDuration As Boolean
Private bDeclinedToMeasureWeight As Boolean 
Private bDeclinedToMeasureFoetalMovements As Boolean

Public Sub Init(cons As CConsult, currpreg As Collection)
   
   currentconsult = cons
   currentPregnancy = currpreg
   Presentations = modPregnancyDBI.Presentations_Get()
   Presentations = modUtilGUI.LoadCombo(cmbPresentation, modUtil.Copy_Collection_Keyed_Sequentially(modPregnancyDBI.Presentations_Get()), "presentation")
   lblMeasure.text = "   Foetal Heart Sounds    "
   modEditAreaHelpers.Resize_labels(VBox_EditArea, lblmeasure)
   rbFMF.width = rbFHH.width
   Settings_Load
   Tableview_Init()
   
End

Public Sub Pregnancy_Set_PK(key As Integer)
   'set the pregnancy key from FPregnancy
   
   fk_pregnancy = key
   
End

Public Sub Save()
   '-------------------------
   'Save the ante-natal sheet
   '  pk serial NOT NULL,
   '   fk_consult integer NOT NULL,
   '   fk_pregnancy integer NOT NULL,
   '   visit_date date NOT NULL,
   '   duration_weeks integer NOT NULL,
   '   fundal_height integer NOT NULL,
   '   fk_lu_presentation integer NOT NULL,
   '   foetal_heart_heard boolean DEFAULT false,
   '   urinalysis text,
   '   bloodpressure text,
   '   weight numeric,
   '   foetal_movements_felt boolean DEFAULT false,
   '   notes text,
   '-------------------------
   
   Dim visit As CRow
   Dim progressnote As CRow   
   
   If HBox_EditArea.Padding = 0 Then Return
   If Not Visit_Valid() Then Return
   Visit = New CRow
   If fk_visit <> 0 Then
      visit.put_unchanged(fk_visit, "fk_visit")
   Endif
   visit!fk_consult = currentconsult.GetPK()
   visit!fk_pregnancy = fk_pregnancy
   visit!visit_date = Val(txtVisitDate.text)
   visit!duration_weeks = txtDuration.text     'not keep as number but text
   visit!fundal_height = Val(txtFundalHeight.text)
   visit!fk_lu_presentation = Presentations[cmbPresentation.index]!pk
   visit!foetal_heart_rate = Val(txtFoetalHeartRate.text)
   If rbFHH.value = True Then
      visit!foetal_heart_heard = True
   Else
      visit!foetal_heart_heard = False
   End If
   visit!urinalysis = Trim(txtUrinalysis.text)
   visit!bloodpressure = Trim(txtBloodPressure.Text)
   visit!weight = Trim(txtWeight.text)
   If rbFMF.value = True Then
      visit!foetal_movements_felt = True
   Else
      visit!foetal_movements_felt = False
   Endif
   visit!notes = Trim(txtNotes.text)
   
   '------------------------------------------
   ' CREATE TABLE clin_consult.progressnotes
   ' (
   '   pk serial NOT NULL,
   '   fk_consult integer,
   '   notes text,
   '   fk_section integer,
   '   fk_code bigint,
   '   problem text,
   '   fk_problem integer,
   '   fk_audit_action integer DEFAULT 1,
   '   linked_table regclass,
   '   fk_row integer,
   '   fk_audit_reason integer,
   '   deleted boolean DEFAULT false,
   '-----------------------------------
   progressnote = New CRow 
   If fk_progressnote Then
      progressnote.put_unchanged(fk_progressnote, "fk_progressnote")
      If Format(currentconsult.GetConsultDate(), "dd/mm/yyyy") <> currentconsult.GetConsultDate() Then
         ' put in an audit on the changed notes FIXME.
      Endif
   Endif
   progressnote!fk_consult = currentconsult.GetPK()
   progressnote!notes = ProgressNotes_Visit_Create()
   progressnote!fk_section = const.cSection_Pregnancy
   ProgressNote!fk_audit_action = const.cAuditAction_Insert 
   ProgressNote.Save("clin_consult.progressnotes", "fk_progressnote")
   visit!fk_progressnote = progressnote!fk_progressnote
   visit.Save("clin_pregnancy.ante_natal_visits", "fk_visit")
   modDBConnect.CommitTrans()
   FClinical.Pregnancy_Notify()
   EditArea_Notify_DataChange(False)
   Reload()
   
End

Public Function ProgressNotes_Visit_Create() As String
   
   Dim Pn As String 
   Dim table_row As String 
   
   Table_row = ""
   "<TR VALIGN=TOP>"
   "<TD WIDTH=20%>%col1%</TD>"
   "<TD WIDTH=80%>%col2%</TD>"
   "</TR>"
   pn = "<TABLE WIDTH=100% BORDER=0 CELLPADDING=0 CELLSPACING=0>"
   "<COL WIDTH=20%>"
   "<COL WIDTH=80%>"
   pn &= Table_row
   pn = Replace(pn, "%col1%", "Duration")
   pn = Replace(pn, "%col2%", txtDuration.text)
   pn &= Table_row
   pn = Replace(pn, "%col1%", "Fundal Height")
   If Trim(txtFundalHeight.text) <> "" Then pn = Replace(pn, "%col2%", Trim(txtFundalHeight.text))
   pn &= Table_row
   pn = Replace(pn, "%col1%", "Presentation")
   pn = Replace(pn, "%col2%", cmbPresentation.Text)
   pn &= Table_row
   pn = Replace(pn, "%col1%", "Foetal Heart Sounds")
   If rbFHH.value = True Then
      If Trim(txtFoetalHeartRate.text) <> "" Then
         pn = Replace(pn, "%col2%", "Heard - rate = " & Trim(txtFoetalHeartRate.text) & " bpm")
      Else
         pn = Replace(pn, "%col2%", "Heard")
      Endif
   Endif
   pn &= Table_row
   pn = Replace(pn, "%col1%", "Urinalysis")
   If Trim(txtUrinalysis.text) <> "" Then pn = Replace(pn, "%col2%", Trim(txtUrinalysis.Text))
   pn &= Table_row
   pn = Replace(pn, "%col1%", "BP")
   If Trim(txtBloodPressure.text) <> "" Then pn = Replace(pn, "%col2%", Trim(txtBloodPressure.Text))
   pn &= Table_row
   pn = Replace(pn, "%col1%", "Weight")
   If Trim(txtWeight.text) <> "" Then pn = Replace(pn, "%col2%", Trim(txtWeight.Text))
   pn &= Table_row
   pn = Replace(pn, "%col1%", "Foetal Movements")
   If rbFMF.value = True Then
      pn = Replace(pn, "%col2%", "felt")
   Else
      pn = Replace(pn, "%col2%", "not felt")
   Endif
   If Trim(txtNotes.text) <> "" Then
      pn &= Table_row
      pn = Replace(pn, "%col1%", "Notes")
      pn = Replace(pn, "%col2%", Trim(txtNotes.text))
   Endif
   pn = Replace(pn, "%col2%", "")
   pn &= "</TABLE>"
   Print pn
   Return PN  

End

Public Function Visit_Valid() As Boolean
   '-------------------------------------------------------------
   'Try and get some sort of minimum data-set
   'if the various    bDeclinedToMeasure<Parameter> = True then
   'don't bother the user again
   '------------------------------------------------------------
   
   Dim duration As Integer 
   
   Try duration = Val(Split(txtDuration.text, " ")[0])
   If Trim(txtVisitDate.text) <> "" Then
      If Not IsDate(Trim(txtVisitDate.text)) Then
         With txtVisitDate
            .SetFocus()
            .Background = Color.rgb(95, 255, 175)
         End With
         Return
      Endif
   Endif
   If Trim(txtDuration.text) = "" Then
      
      ' With txtDuration
      '    .SetFocus()
      '    .Background = Color.rgb(95, 255, 175)
      ' End With
      ' Return
   Endif
   If bDeclinedToMeasureDuration = False Then   
      If Trim(txtFundalHeight.text) = "" Then
         If duration > 19 Then
            If Message.Question("Do you wish to enter the fundal height?", "Yes", "No") = 1 Then
               With txtFundalHeight
                  .SetFocus()
                  .Background = Color.rgb(95, 255, 175)
               End With
               Return
            Else
               bDeclinedToMeasureDuration = True   
            End If   
         End If   
      Endif
   End If 
   If bDeclinedToMeasureFHH = False Then   
      If rbFHH.value = False And rbFHNotHeard.value = False Then
         If duration > 19 Then    
            If Message.question("Do  you wish to record the foetal heart sounds?", "Yes", "No") = 1 Then
               rbFHH.SetFocus
               Return
            Else
               bDeclinedToMeasureFHH = True
            End If
         End If
      End If   
   Endif
   
   If bDeclinedToMeasureBP = False Then  
      If Trim(txtBloodPressure.text) = "" Then
         If Message.question("Do  you wish to record the blood pressure?", "Yes", "No") = 1 Then
            With txtBloodPressure
               .SetFocus()
               .Background = Color.rgb(95, 255, 175)
            End With
            Return
         Else
            bDeclinedToMeasureBP = True   
         Endif
      End If  
   End If   
   If bDeclinedToMeasureWeight = False Then  
      If duration > 11 Then   
         If Trim(txtWeight.text) = "" Then
            If Message.question("Do  you wish to record the weight?", "Yes", "No") = 1 Then
               With txtWeight
                  .SetFocus()
                  .Background = Color.rgb(95, 255, 175)
               End With
               Return
            Else
               bDeclinedToMeasureWeight = True   
            End If 
         Endif
      End If   
   End If   
   If bDeclinedToMeasureFoetalMovements = False Then  
      If rbFMF.value = False And rbFMNotFelt.value = False Then
         If duration > 19 Then   
            If Message.question("Do you wish to ask if the patient has felt the foetal movemenst?", "Yes", "No") = 1 Then  
               rbFMF.SetFocus
               Return
            Else
               bDeclinedToMeasureFoetalMovements = True  
            End If 
         End If 
      Endif
   End If   
   Return True
   
End

Public Sub Print_Content()
   
   'Print the ante-natal sheet
   
End

Private Sub Tableview_Init()
   '--------------------------------------------------
   'Init the tableview headers, default to single row
   '--------------------------------------------------
   
   With gvwVisits
      '  .Rows.count = antenatal_visits.Count + 1
      .Columns.count = 11
      .Columns[0].text = "     Date     "
      lblMeasure.text = "10/10/2013  "
      .Columns[1].text = "   Duration     "
      .Columns[2].text = "Fundus"
      .Columns[3].text = "Presentation"
      .Columns[4].text = " FH  "
      .Columns[5].text = " Rate  "
      .Columns[6].text = "Urine"
      .Columns[7].text = "BP"
      .Columns[8].text = "Weight"
      .Columns[9].text = "FM"
      .Columns[10].text = "Notes"
   End With
   Reload()
   
End

Private Sub Reload()
   '---------------------------------
   'Display existing ante-natal rows
   '--------------------------------
   
   Dim antenatal_visit As Collection
   Dim irow As Integer
   
   antenatal_visits = modUtil.Copy_Collection_Keyed_Sequentially(modPregnancyDBI.Antenatal_Visits_Get(currentconsult!patient!fk_patient))
   If Not antenatal_visits.count Then Return
   HBox_EditArea.Enabled = False
   With gvwVisits
      .Clear()
      .Rows.count = 0
   End With
   antenatal_visits = modUtil.Copy_Collection_Keyed_Sequentially(modPregnancyDBI.Antenatal_Visits_Get(currentconsult!patient!fk_patient))
   For Each antenatal_visit In antenatal_visits
      Inc gvwVisits.Rows.count
      gvwVisits[irow, 0].Text = Format(antenatal_visit!visit_date, "dd/mm/yyyy")
      gvwVisits[irow, 1].Text = antenatal_visit!duration_weeks
      gvwVisits[irow, 1].Text = Replace(gvwVisits[irow, 1].Text, "Weeks", "W")
      gvwVisits[irow, 1].Text = Replace(gvwVisits[irow, 1].Text, "Days", "D")
      gvwVisits[irow, 2].Text = antenatal_visit!fundal_height & "cm"
      gvwVisits[irow, 3].Text = antenatal_visit!presentation
      If antenatal_visit!foetal_heart_heard = True Then
         gvwVisits[irow, 4].Text = "FHH"
      Else
         gvwVisits[irow, 4].Text = " - "
      End If
      gvwVisits[irow, 5].Text = antenatal_visit!foetal_heart_rate
      gvwVisits[irow, 6].Text = antenatal_visit!urinalysis
      gvwVisits[irow, 7].Text = antenatal_visit!bloodpressure
      gvwVisits[irow, 8].Text = antenatal_visit!weight
      If antenatal_visit!foetal_movements_felt = True Then
         gvwVisits[irow, 9].Text = "Felt"
      Else
         gvwVisits[irow, 9].Text = " - "
      End If
      gvwVisits[irow, 10].Text = antenatal_visit!notes
      Inc irow
      
   Next
   VBox_EditArea.Enabled = False
   
End

Public Sub gvwVisits_Click()
   '-----------------------------------------------------------------------
   'User has clicked on a cell in the grid
   'set the edidtor to either a textbox (default) or provide combo-box data
   '-----------------------------------------------------------------------
   '  EditArea_Clear()
   ' Select Case gridview1.column
   '    Case cTableColumn_Presentation
   '       gridview1.Edit(Presentations, True)
   '    Case cTableColumn_FHS
   '       gridview1.Edit(["Heard", "Not Heard"], True)
   '    Case cTableColumn_FMF
   '       gridview1.Edit(["Felt", "Not Felt"], True)
   '    Case Else
   '     '  gridview1.Edit()
   ' End Select
   
End

Public Sub gvwVisits_Save(Row As Integer, Column As Integer, Value As String)
   '------------------------------------------------------------------------
   'Move the data from the editing control back into the tableview grid cell
   '------------------------------------------------------------------------
   
   ' gridview1[gridview1.row, gridview1.column].text = Value
   
End

Public Sub gvwVisits_Data(Row As Integer, Column As Integer)
   
End

Public Sub gvwVisits_Insert()
   '--------------------------------------------------------------
   'User has pressed <ENTER> at the end of a row, insert a new one
   '--------------------------------------------------------------
   '
   ' Inc gridview1.Rows.count
   ' gridview1.column = cTableColumn_Date
   ' gridview1.row = gridview1.Rows.Max
   ' gridview1_Click()
   
End

Public Sub EditAreaTextBox_Change()
   
   If bexit Then Return
   Select Case Last.tag
      Case "notes"
         If InStr(Last.text, "\n\n") Then
            Last.text = Replace(Last.text, "\n\n", "")
            lblForFocus.SetFocus()
         End If
   End Select
   EditArea_Notify_DataChange(True)
   
End

Public Sub EditArea_Clear()
   
   bExit = True
   modEditAreaHelpers.EditArea_Clear(VBox_EditArea)  'clear textboxes/combo's
   cmbPresentation.index = -1
   fk_visit = 0
   fk_progressnote = 0
   progressnotes_for_visit = ""
   bDeclinedToMeasureBP = False  
   bDeclinedToMeasureDuration = False  
   bDeclinedToMeasureFHH = False 
   bDeclinedToMeasureWeight = False  
   bDeclinedToMeasureFoetalMovements = False
   EditArea_Notify_DataChange(False)
   bexit = False
   
End

Public Function EditArea_TxtBox_ExcludeKeys(keycode As Integer, tag As String) As Boolean
   
   Select Case tag
      Case "visit date"
         bKeyValid = modUtilGUI.AllowKeys(const.AllowKeys_DateFormat, keycode)
      Case "duration", "fundal height", "foetal heart rate"
         bKeyValid = modUtilGUI.AllowKeys(const.AllowKeys_NumbersOnly, keycode)
      Case "weight"
         bKeyValid = modUtilGUI.AllowKeys(const.AllowKeys_NumbersDecimal, keycode)
      Case "blood pressure"
         bKeyValid = modUtilGUI.AllowKeys(const.AllowKeys_BP, keycode)
      Case Else
         bKeyValid = True
   End Select
   Return bKeyValid
   
End

Public Sub EditAreaTextBox_LostFocus()
   
   Last.BackGround = Color.White
   
End

Public Sub EditAreaTextBox_Keypress()
   
   If bexit Then Return
   bkeyvalid = EditArea_TxtBox_ExcludeKeys(key.code, Last.tag)
   If bkeyvalid = False Then
      Stop Event
      Return
   End If
   If Key.code = Key.Return Or Key.Code = Key.tab Then
      Select Case Last.tag
         Case "visit date"
            If Not IsDate(Last.text) Then
               bexit = True
               Last.text = ""
               bexit = False
            Else
               If DateDiff(currentconsult.GetConsultDate(), Val(Last.text), gb.day) > 0 Then
                  bexit = True
                  Last.text = ""
                  bexit = False
               Endif
            End If
            txtDuration.SetFocus()
         Case "duration"
            txtFundalHeight.SetFocus()
         Case "fundal height"
            cmbPresentation.SetFocus()
         Case "foetal heart rate"
            txtUrinalysis.SetFocus()
         Case "urinalysis"
            txtBloodPressure.SetFocus()
         Case "blood pressure"
            txtWeight.SetFocus()
         Case "weight"
            rbFMF.SetFocus
      End Select
   Endif
   
End

Public Sub EditAreaTextBox_GotFocus()
   
   Last.BackGround = Color.rgb(95, 255, 175)
   ListView1.Visible = False
   columnview1.Visible = False
   
End

Public Sub EditArea_Notify_DataChange(bflag As Boolean)
   
   If bFlag Then
      HBox_EditARea.padding = 1
   Else
      HBox_EditARea.padding = 0
   End If
   
End

Public Sub cmbPresentation_KeyPress()
   
   If bExit Then Return
   If Key.code = Key.return Or Key.code = Key.Tab Then
      rbFHH.SetFocus()
   End If
   EditArea_Notify_DataChange(True)
   
End

Public Sub EditArea_RadioButtons_KeyPress()
   
   If Key.code = Key.return Or Key.code = Key.tab Then
      Select Case Last.tag
         Case "heard"
            rbFHNotHeard.SetFocus()
         Case "not heard"
            txtUrinalysis.SetFocus()
         Case "felt"
            rbFMNotFelt.SetFocus()
         Case "not felt"
            txtNotes.SetFocus()
      End Select
   End If
   
End

Private Sub Settings_Load()
   
   Vsplit_Main.Layout = Settings["FPregnancyVisits/VSplit_Main.Layout", modUtilGUI.VSplit([272, 554])]
   gvwVisits.Font = Font[Settings["FPregnancyVisits/gvwVisits.font", "DejaVu Sans,9"]]
   
End

Public Sub Settings_Save()
   
   Settings["FPregnancyVisits/VSplit_Main.Layout"] = Vsplit_Main.Layout
   
End

Public Sub New_Entry()
   '------------------------------------------------------------------
   'see FClinical for explanation anyway, allow entry of new pregnancy
   '------------------------------------------------------------------
   
   AnteNatal_Visit_New()
   
End

Public Sub AnteNatal_Visit_New()
   
   Dim M As Collection
   
   EditArea_Clear()
   currentconsult.Refresh("measurements")
   Print currentconsult!measurements
   For Each M In currentconsult!measurements
      If Format(M!consult_date, "dd/mm/yyyy") = Format(currentconsult.GetConsultDate(), "dd/mm/yyyy") Then
         If M!fk_type = const.Measurement_BP Then
            txtBloodPressure.text = modMeasurementsDBI.Measurement_Get_Latest(currentconsult!measurements, const.Measurement_BP)!data
         Endif
      Endif
   Next
   With txtVisitDate
      .text = Format(currentconsult.GetConsultDate(), "dd/mm/yyyy")
   End With
   txtDuration.text = Calc_Gestation()
   If txtDuration.text <> "" Then
      txtFundalHeight.SetFocus
   Else
      txtDuration.SetFocus
   Endif
   fk_pregnancy = currentPregnancy!fk_pregnancy
   HBox_EditArea.Enabled = True
   VBox_EditArea.Enabled = True
   cmbPresentation.index = cmbPresentation.Find("n/a")
   
End

Public Sub Calc_Gestation() As Variant
   
   Dim days As Integer
   Dim weeks As Integer
   
   If IsNull(currentPregnancy!edc_revised) And Not IsNull(currentPregnancy!lmp) Then
      If IsDate(currentPregnancy!lmp) Then
         days = DateDiff(Val(currentPregnancy!lmp), currentconsult.GetConsultDate(), gb.day) 'won't bug if LMP is proper date
         weeks = days / 7
         Return Str(weeks) & " Weeks " & Str(days Mod 7) & " Days"
      End If
   Endif
   
End

Public Sub EditArea_RadioButtons_Click()
   
   If bexit Then Return
   EditArea_Notify_DataChange(True)
   
End

Public Sub gvwVisits_Select()
   
   Visit_Display()
   
End

Public Sub mnuVisits_Click()
   
   Select Case Last.tag
      Case "edit"
         VBox_EditArea.Enabled = True
      Case "delete"
         
      Case "help"
         modUtilGUI.NotImplemented("Help for this module")
      Case "font"
         modUtilGUI.Columnview_SetFont(gvwVisits, "FPregnancyVisits")
   End Select
Catch
   Return 

End

Public Sub Visit_Edit()
   
   VBox_EditArea.Enabled = True
   
End

Public Sub Visit_Display()
   '------------------------------------------------
   'Displays the currently selected ante-natal visit
   '------------------------------------------------
   
   Dim antenatal_visit As New Collection
   
   EditArea_Clear()
   antenatal_visit = antenatal_visits[gvwVisits.row]
   bexit = True
   cmbPresentation.index = cmbPresentation.Find(antenatal_visit!presentation)
   txtVisitDate.text = Format(antenatal_visit!visit_date, "dd/mm/yyyy")
   txtDuration.text = antenatal_visit!duration_weeks
   txtFundalHeight.text = antenatal_visit!fundal_height
   txtFoetalHeartRate.text = antenatal_visit!foetal_heart_rate
   fk_pregnancy = antenatal_visit!fk_pregnancy
   fk_visit = antenatal_visit!pk_antenatal_visit
   fk_progressnote = antenatal_visit!fk_progressnote
   progressnotes_for_visit = antenatal_visit!progress_notes
   If antenatal_visit!foetal_heart_heard Then
      rbFHH.value = True
   Else
      rbFHNotHeard.value = True
   End If
   If antenatal_visit!foetal_movements_felt Then
      rbFMF.value = True
   Else
      rbFMNotFelt.value = True
   Endif
   txtUrinalysis.text = antenatal_visit!urinalysis
   txtBloodPressure.text = antenatal_visit!bloodpressure
   txtWeight.text = antenatal_visit!weight
   Try txtNotes.text = antenatal_visit!notes
   bExit = False
   
End

Public Sub gvwVisits_Menu()
   
   If gvwVisits.Rows.count Then
      mnuVisits.Popup()
   Endif
   
End

Public Sub cmbPresentation_Click()
   
   If bexit Then Return
   EditArea_Notify_DataChange(True)
   
End
