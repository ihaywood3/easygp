' Gambas class file

' Copyright (C) 2008-2013 Dr. Richard Terry

' This program is free software: you can redistribute it and/or modify
' it under the terms of the GNU General Public License as published by
' the Free Software Foundation, either version 3 of the License, or
' (at your option) any later version.

' This program is distributed in the hope that it will be useful,
' but WITHOUT ANY WARRANTY; without even the implied warranty of
' MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
' GNU General Public License for more details.

' You should have received a copy of the GNU General Public License
' along with this program.  If not, see <http://www.gnu.org/licenses/>.

Private icounter As Integer 
Private bExit As Boolean
Private bKeyValid As Boolean
Private cTableColumn_Date As Integer = 0
Private cTableColumn_duration As Integer = 1
Private cTableColumn_FundalHeight As Integer = 2
Private cTableColumn_Presentation As Integer = 3
Private cTableColumn_FHS As Integer = 4
Private cTableColumn_Urinalysis As Integer = 5
Private cTableColumn_BP As Integer = 6
Private cTableColumn_Weight As Integer = 7
Private cTableColumn_FMF As Integer = 8
Private cTableColumn_Notes As Integer = 9
Private currentconsult As CConsult
Private Presentations As Collection
Private antenatal_visits As Collection
Private fk_visit As Integer 
Private fk_pregnancy As Integer
Private currentPregnancy As Collection    

Public Sub Init(c As CConsult, cp As Collection)
   
   currentconsult = c  
   currentPregnancy = cp
   Presentations = modPregnancyDBI.Presentations_Get()
   Presentations = modUtil.LoadCombo(cmbPresentation, modUtil.Copy_Collection_Keyed_Sequentially(modPregnancyDBI.Presentations_Get()), "presentation")
   antenatal_visits = currentconsult!antenatal_visits
   lblMeasure.text = "   Foetal Heart Sounds    "
   modEditAreaHelpers.Resize_labels(VBox_EditArea, lblmeasure)
   rbFMF.width = rbFHH.width
   Settings_Load  
   Tableview_Init()
   
End

Public Sub Pregnancy_Set_PK(key As Integer)
   'set the pregnancy key from FPregnancy
   
   fk_pregnancy = key
   
End

Public Sub Save()
   '-------------------------
   'Save the ante-natal sheet
   '  pk serial NOT NULL,
   '   fk_consult integer NOT NULL,
   '   fk_pregnancy integer NOT NULL,
   '   visit_date date NOT NULL,
   '   duration_weeks integer NOT NULL,
   '   fundal_height integer NOT NULL,
   '   fk_lu_presentation integer NOT NULL,
   '   foetal_heart_heard boolean DEFAULT false,
   '   urinalysis text,
   '   bloodpressure text,
   '   weight numeric,
   '   foetal_movements_felt boolean DEFAULT false,
   '   notes text,
   '-------------------------
   
   Dim visit As CRow
   
   If HBox_EditArea.Padding = 0 Then Return
   If Not Visit_Valid() Then Return 
   Visit = New CRow  
   If fk_visit <> 0 Then
      visit.put_unchanged(fk_visit, "fk_visit")
   Endif
   visit!fk_consult = currentconsult.GetPK()
   visit!fk_pregnancy = fk_pregnancy
   visit!visit_date = Val(txtVisitDate.text)
   visit!duration_weeks = txtDuration.text     'not keep as number but text
   visit!fundal_height = Val(txtFundalHeight.text)
   visit!fk_lu_presentation = Presentations[cmbPresentation.index]!pk
   If rbFHH.value = True Then 
      visit!foetal_heart_heard = True
   Else
      visit!foetal_heart_heard = False
   End If   
   visit!urinalysis = Trim(txtUrinalysis.text)
   visit!bloodpressure = Trim(txtBloodPressure.Text)
   visit!weight = Trim(txtWeight.text)
   If rbFMF.value = True Then
      visit!foetal_movements_felt = True
   Else
      visit!foetal_movements_felt = False
   Endif
   visit!notes = Trim(txtNotes.text)
   visit.Save("clin_pregnancy.ante_natal_visits", "fk_visit")
   modDBConnect.CommitTrans()
   EditArea_Notify_DataChange(False)   
   Reload()
   
End 

Public Function Visit_Valid() As Boolean
   
   If Trim(txtVisitDate.text) <> "" Then
      If Not IsDate(Trim(txtVisitDate.text)) Then
         With txtVisitDate
            .SetFocus()
            .Background = Color.rgb(95, 255, 175)
         End With  
         Return
      Endif
   Endif
   If Trim(txtDuration.text) = "" Then
      With txtDuration
         .SetFocus()
         .Background = Color.rgb(95, 255, 175)
      End With  
      Return
   Endif
   If Trim(txtFundalHeight.text) = "" Then
      With txtFundalHeight
         .SetFocus()
         .Background = Color.rgb(95, 255, 175)
      End With  
      Return
   Endif
   If rbFHH.value = False And rbFHNotHeard.value = False Then
      Message.info("Please decide about the foetal heart sounds.")
      rbFHH.SetFocus
      Return
   Endif
   If Trim(txtUrinalysis.text) = "" Then
      With txtUrinalysis
         .SetFocus()
         .Background = Color.rgb(95, 255, 175)
      End With  
      Return
   Endif
   If Trim(txtBloodPressure.text) = "" Then
      With txtBloodPressure
         .SetFocus()
         .Background = Color.rgb(95, 255, 175)
      End With  
      Return
   Endif
   If Trim(txtWeight.text) = "" Then
      With txtWeight
         .SetFocus()
         .Background = Color.rgb(95, 255, 175)
      End With  
      Return
   Endif
   If rbFMF.value = False And rbFMNotFelt.value = False Then
      Message.Info("Please ask if the patient has felt the foetal movements.")
      rbFMF.SetFocus
      Return
   Endif
   Return True
   
End

Public Sub Print_Content()
   
   'Print the ante-natal sheet 
   
End

Private Sub Tableview_Init()
   '--------------------------------------------------
   'Init the tableview headers, default to single row
   '--------------------------------------------------   
   
   With gridview1
      .Rows.count = antenatal_visits.Count + 1
      .Columns.count = 10
      .Columns[0].text = "     Date     "
      lblMeasure.text = "10/10/2013  "
      .Columns[1].text = "   Duration     "
      .Columns[2].text = "Fundus"
      .Columns[3].text = "Presentation"
      .Columns[4].text = " FH  "
      .Columns[5].text = "Urine"
      .Columns[6].text = "BP"
      .Columns[7].text = "Weight"
      .Columns[8].text = "FM"
      .Columns[9].text = "Notes"
   End With
   If antenatal_visits.count Then Reload()
   ' gridview1[gridview1.Rows.max, cTableColumn_Date].text = Format(Now, "dd/mm/yyyy")
   ' gridview1.column = cTableColumn_duration
   ' gridview1_click()
   
End

Private Sub Reload()
   '---------------------------------
   'Display existing ante-natal rows
   '--------------------------------  

   Dim antenatal_visit As Collection
   Dim irow As Integer

   HBox_EditArea.Enabled = False 
   With gridview1
      .Clear()
      .Rows.count = 1
   End With
   antenatal_visits = modPregnancyDBI.Antenatal_Visits_Get(currentconsult!patient!fk_patient)
   For Each antenatal_visit In antenatal_visits
      gridview1[irow, 0].Text = Format(antenatal_visit!visit_date, "dd/mm/yyyy")
      gridview1[irow, 1].Text = antenatal_visit!duration_weeks
      gridview1[irow, 1].Text = Replace(gridview1[irow, 1].Text, "Weeks", "W")
      gridview1[irow, 1].Text = Replace(gridview1[irow, 1].Text, "Days", "D")
      gridview1[irow, 2].Text = antenatal_visit!fundal_height & "cm"
      gridview1[irow, 3].Text = Presentations[antenatal_visit!fk_lu_presentation]!presentation
      If antenatal_visit!foetal_heart_heard = True Then
         gridview1[irow, 4].Text = "FHH"
      Else
         gridview1[irow, 4].Text = " - "
      End If  
      gridview1[irow, 5].Text = antenatal_visit!urinalysis
      gridview1[irow, 6].Text = antenatal_visit!bloodpressure
      gridview1[irow, 7].Text = antenatal_visit!weight 
      If antenatal_visit!foetal_movements_felt = True Then
         gridview1[irow, 8].Text = "Felt"
      Else
         gridview1[irow, 8].Text = " - "
      End If  
      gridview1[irow, 9].Text = antenatal_visit!notes  
     Inc irow 
     Inc gridview1.Rows.count
   Next

End

Public Sub gridview1_Click()
   '-----------------------------------------------------------------------
   'User has clicked on a cell in the grid
   'set the edidtor to either a textbox (default) or provide combo-box data
   '-----------------------------------------------------------------------
    EditArea_Clear()
   ' Select Case gridview1.column
   '    Case cTableColumn_Presentation
   '       gridview1.Edit(Presentations, True)
   '    Case cTableColumn_FHS
   '       gridview1.Edit(["Heard", "Not Heard"], True)
   '    Case cTableColumn_FMF
   '       gridview1.Edit(["Felt", "Not Felt"], True)
   '    Case Else
   '     '  gridview1.Edit()
   ' End Select
   
End

Public Sub gridview1_Save(Row As Integer, Column As Integer, Value As String)
   '------------------------------------------------------------------------
   'Move the data from the editing control back into the tableview grid cell
   '------------------------------------------------------------------------
   
  ' gridview1[gridview1.row, gridview1.column].text = Value
   
End

Public Sub gridview1_Data(Row As Integer, Column As Integer)
   
End

Public Sub gridview1_Insert()
   '--------------------------------------------------------------
   'User has pressed <ENTER> at the end of a row, insert a new one
   '--------------------------------------------------------------   
   ' 
   ' Inc gridview1.Rows.count
   ' gridview1.column = cTableColumn_Date
   ' gridview1.row = gridview1.Rows.Max
   ' gridview1_Click()
   
End

Public Sub EditAreaTextBox_Change()
   
   If bexit Then Return    
   Select Case Last.tag
      Case "notes"
         If InStr(Last.text, "\n\n") Then
            Last.text = Replace(Last.text, "\n\n", "")
            lblForFocus.SetFocus()
         End If  
   End Select
   EditArea_Notify_DataChange(True)
   
End

Public Sub EditArea_Clear()
   
   bExit = True
   modEditAreaHelpers.EditArea_Clear(VBox_EditArea)  'clear textboxes/combo's
   cmbPresentation.index = 0 'n/a
   fk_visit = 0
   EditArea_Notify_DataChange(False)
   bexit = False
   
End    

Public Function EditArea_TxtBox_ExcludeKeys(keycode As Integer, tag As String) As Boolean  
   
   Select Case tag
      Case "visit date"
         bKeyValid = modUtil.AllowKeys(const.AllowKeys_DateFormat, keycode)
      Case "duration", "fundal height"
         bKeyValid = modUtil.AllowKeys(const.AllowKeys_NumbersOnly, keycode)
      Case "weight"
         bKeyValid = modUtil.AllowKeys(const.AllowKeys_NumbersDecimal, keycode)
      Case "blood pressure"
         bKeyValid = modUtil.AllowKeys(const.AllowKeys_BP, keycode)
      Case Else
         bKeyValid = True
   End Select
   Return bKeyValid
   
End 

Public Sub EditAreaTextBox_LostFocus()
   
   Last.BackGround = Color.White
   
End

Public Sub EditAreaTextBox_Keypress()
   
   If bexit Then Return
   bkeyvalid = EditArea_TxtBox_ExcludeKeys(key.code, Last.tag)
   If bkeyvalid = False Then
      Stop Event
      Return
   End If
   If Key.code = Key.Return Or Key.Code = Key.tab Then
      Select Case Last.tag
         Case "visit date"
            If Not IsDate(Last.text) Then
               bexit = True
               Last.text = ""
               bexit = False  
            Else
               If DateDiff(Now, Val(Last.text), gb.day) > 0 Then
                  bexit = True
                  Last.text = ""
                  bexit = False  
               Endif
            End If   
            txtDuration.SetFocus()
         Case "duration"
            txtFundalHeight.SetFocus()
         Case "fundal height"
            cmbPresentation.SetFocus()
         Case "urinalysis"
            txtBloodPressure.SetFocus()
         Case "blood pressure"
            txtWeight.SetFocus()
         Case "weight"
            rbFMF.SetFocus
      End Select
   Endif
   
End 

Public Sub EditAreaTextBox_GotFocus()
   
   Last.BackGround = Color.rgb(95, 255, 175)
   ListView1.Visible = False
   
   columnview1.Visible = False   
   
End

Public Sub EditArea_Notify_DataChange(bflag As Boolean)
   
   If bFlag Then
      HBox_EditARea.padding = 1
   Else
      HBox_EditARea.padding = 0
   End If 
   
End

Public Sub cmbPresentation_KeyPress()
   
   If bExit Then Return
   If Key.code = Key.return Or Key.code = Key.Tab Then
      rbFHH.SetFocus()
   End If   
   EditArea_Notify_DataChange(True)
   
End

Public Sub EditArea_RadioButtons_KeyPress()
   
   If Key.code = Key.return Or Key.code = Key.tab Then   
      Select Case Last.tag
         Case "heard"
            rbFHNotHeard.SetFocus()
         Case "not heard"
            txtUrinalysis.SetFocus()
         Case "felt"
            rbFMNotFelt.SetFocus()
         Case "not felt"
            txtNotes.SetFocus()   
      End Select
   End If   
   
End

Private Sub Settings_Load()
   
   Vsplit_Main.Layout = Settings["FPregnancyVisits/VSplit_Main.Layout", [1, 1]] 
   
End

Public Sub Settings_Save()
   
   Settings["FPregnancyVisits/VSplit_Main.Layout"] = Vsplit_Main.Layout
   
End

Public Sub New_Entry()
   '------------------------------------------------------------------
   'see FClinical for explanation anyway, allow entry of new pregnancy
   '------------------------------------------------------------------
   
   AnteNatal_Visit_New() 
   
End

Public Sub AnteNatal_Visit_New()
   
   EditArea_Clear()
   With txtVisitDate
      .text = Format(Now, "dd/mm/yyyy")
   End With
   txtDuration.text = Calc_Gestation()
   If txtDuration.text <> "" Then
      txtFundalHeight.SetFocus
   Else
      txtDuration.SetFocus
   Endif
   fk_pregnancy = currentPregnancy!fk_pregnancy
   HBox_EditArea.Enabled = True
End

Public Sub Calc_Gestation() As Variant
   
   Dim lmp As Date
   Dim days As Integer
   Dim weeks As Integer
   
   If currentPregnancy!edc_revised = False And Not IsNull(currentPregnancy!lmp) Then
      If IsDate(currentPregnancy!lmp) Then 
         days = DateDiff(Val(currentPregnancy!lmp), Now, gb.day) 'won't bug if LMP is proper date
         weeks = days / 7
         Return Str(weeks) & " Weeks " & Str(days Mod 7) & " Days"
      End If  
   Endif
   
End

Public Sub EditArea_RadioButtons_Click()
   
   If bexit Then Return
   EditArea_Notify_DataChange(True)
   
End
