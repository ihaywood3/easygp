' Gambas class file

Private brands As Collection
Private brand As Collection 
Private Form_PDF As FPdf
Private generics As Collection
Private drug_forms As Collection
Private bKeyValid As Boolean
Private bExit As Boolean  
Private fk_form As Integer
Private units_keys As Integer[]
Private fk_clinic As Integer

Public Sub Form_Open()
   
   Init() 
   
End

Public Sub Form_Close()
   
   Settings_Save() 
   
End

Public Sub Settings_Save()
   
   '  Settings["Drugs/HSplit_EditArea.Layout"] = HSplit_EditArea.Layout
   Settings["Drugs/VSplit_DataEntry.Layout"] = VSplit_DataEntry.Layout
   Settings["Drugs/HSplit1.Layout"] = HSplit1.Layout
   Settings["Drugs/Form_PDF.Zoom"] = Form_PDF.CurrentZoom
   
End

Private Sub Settings_Load()
   
   '  Try HSplit_EditArea.Layout = Settings["Drugs/HSplit_EditArea.Layout"]
   Try VSplit_DataEntry.Layout = Settings["Drugs/VSplit_DataEntry.Layout"]
   Try HSplit1.Layout = Settings["Drugs/HSplit1.layout"]
   Try Form_PDF.CurrentZoom = Settings["Drugs/Form_PDF.Zoom"]
   
End

Public Sub Init()
   
   lblMeasure.text = " Volume (Liquids only) "
   modEditAreaHelpers.Resize_labels(Vbox_EditArea_Outer, lblMeasure)
   Try Settings_Load()
   With cvwDrugs
      .Columns.count = 5
   End With
   With Form_PDF = New (FPDF, VBox_PDF)
      .btZoomIn.Visible = True
      .btZoomOut.Visible = True
      .BtPrint.Visible = True  
   '   Try .Load_PDF(User.home &/ "Documents/alphamox.pdf")
   End With
   cmbUnits.Add("mcg")
   cmbUnits.Add("mg")
   cmbUnits.Add("gm")
   cmbUnits.Add("mg/g")
   cmbUnits.Add("mcg/g")
   cmbUnits.Add("mg/ml")
   cmbUnits.Add("mcg/ml")
   cmbUnits.Add("units")
   fk_clinic = modDBConnect.currentUser!fk_clinic
   Reload()
   
End Sub

Public Sub Reload()
   
   brands = modPrescribingDBI.Brands_Get(Trim(txtSearch.text))
   Brands_Show()
   
End

Public Sub Brands_Show()
   
   Dim x As Integer
   
   cvwDrugs.Clear()
   
   For Each brand In brands
      cvwDrugs.Add(brand!pk_view, brand!brand)
      cvwDrugs[brand!pk_view][0] = brand!brand
      cvwDrugs[brand!pk_view][1] = brand!generic
      cvwDrugs[brand!pk_view][2] = brand!strength & " " & brand!form
      If Not IsNull(brand!product_information_filename) Then
         cvwDrugs[brand!pk_view][3] = "PI available"
      Endif
   Next
   If cvwDrugs.count = 1 Then
      cvwDrugs_Select()
   Endif
   
End

Public Sub txtSearch_KeyRelease()
   
   Reload()
   
End

Public Sub cvwDrugs_Select()
   
   cvwDrugs.MoveCurrent
   txtBrand.text = brands[cvwDrugs.Item.key]!brand
   txtGeneric.text = brands[cvwDrugs.Item.key]!generic
   txtStrength.text = brands[cvwDrugs.Item.key]!strength
   txtForm.text = brands[cvwDrugs.Item.key]!form
   If Not IsNull(brands[cvwDrugs.Item.key]!product_information_filename) Then
      Print modAdminDBI.Config_Get("product_information_directory", "", Null, fk_clinic)
      Try Form_PDF.Load_PDF(modAdminDBI.Config_Get("product_information_directory", "", Null, fk_clinic) &/ brands[cvwDrugs.Item.key]!product_information_filename)
   Endif
   
End

Public Sub EditArea_Notify_Data_Change(flag As Boolean)
   
   If flag Then
      Vbox_EditArea_Outer.Padding = 1
      Vbox_EditArea_Outer.Background = Color.red
    Else
      Vbox_EditArea_Outer.Padding = 0
      Vbox_EditArea_Outer.Background = Color.white
   End If
   
End

Public Sub Drug_New()
   
   modEditAreaHelpers.EditArea_Clear(VBox_EditArea_Inner)   
   EditArea_Notify_Data_Change(False)
   EditArea_TextBoxes_SetRead_Status(False)
   HBox_Volume.Enabled = False   
   cvwDrugs.Clear()
   ListView1.Clear()
   fk_form = 0
   brand = New CRow
   txtBrand.SetFocus()
   
End

Public Sub EditArea_TextBoxes_SetRead_Status(bflag As Boolean)
   
   txtBrand.ReadOnly = bflag
   txtGeneric.ReadOnly = bflag   
   txtForm.ReadOnly = bflag
   txtStrength.ReadOnly = bflag  
   txtPackSize.ReadOnly = bflag  
   txtComment.ReadOnly = bflag
   Hbox_Generics.Visible = bflag     
   HBox_Strength.Visible = bflag  
   Hbox_AddGeneric.Visible = Not bFlag   
   HBox_GenericsList.Visible = Not bFlag
   
End

Public Sub Drug_Forms_Get()
   
   Dim drug_form As Collection
   
   listview2.Clear()
   drug_forms = modPrescribingDBI.Formulations_Get(Trim(txtForm.text))  
   For Each drug_form In drug_forms
      listview2.Add(drug_form!pk, drug_form!form)
   Next
   If listview2.count Then
      listview2.Visible = True
   Endif 
   
End

Public Sub Save()
   '-----------------------------
   'Save a new user defined drug
   ' CREATE TABLE drugs.brand
   ' (
   '   fk_product uuid NOT NULL,
   '   fk_company character varying(3) NOT NULL,
   '   brand character varying(100) NOT NULL,
   '   price money, -- dispensed price for PBS drugs.
   '   from_pbs boolean NOT NULL DEFAULT false, -- true if the brand comes from the PBS database, allows the list to be easily reloaded...
   '   original_tga_text text, -- drugs imported from TGA database, the original label therein
   '   original_tga_code character varying(12), -- drugs imported from TGA database, their TGA code
   '   pk uuid NOT NULL DEFAULT uuid_generate_v4(),
   '   product_information_filename text,
   '   CONSTRAINT brand_pkey PRIMARY KEY (pk ),
   '   CONSTRAINT brand_fk_company_fkey FOREIGN KEY (fk_company)
   '       REFERENCES drugs.company (code) MATCH SIMPLE
   '       ON UPDATE NO ACTION ON DELETE NO ACTION,
   '   CONSTRAINT brand_fk_product_fkey FOREIGN KEY (fk_product)
   '       REFERENCES drugs.product (pk) MATCH SIMPLE
   '       ON UPDATE NO
   ' CREATE TABLE drugs.pack
   ' (
   '   fk_product uuid,
   '   amount double precision, -- the amount of drugs that have a fluid form
   '   amount_unit integer,
   '   pack_size integer DEFAULT 1, -- the number of identical units (bottles, vials, tablets, etc) within a pack
   '   CONSTRAINT pack_fk_product_fkey FOREIGN KEY (fk_product)
   '       REFERENCES drugs.product (pk) MATCH SIMPLE
   '       ON UPDATE NO ACTION ON DELETE NO ACTION
   ' )
   ' WITH (
   '   OIDS=FALSE
   ' );
   
   '-----------------------------   
   brand!strength = Trim(txtStrength.Text)
   brand!fk_form = fk_form
   brand!free_comment = Trim(txtComment.Text)
   brand.Save("drugs.product", "fk_product")
   'brand!brand = Trim(txtBrand.Text)
   'brand!fk_company = fk_company
  ' brand!fk_product = brand!fk_product ' looks stupid but forces save of this field
   'brand!product_information_filename = 
  ' brand.Save("drugs.brand", "fk_brand")
   modDBConnect.CommitTrans()
   'If Vbox_EditArea_Outer.padding = 0 Then Return 
   'If Not Drug_Valid() Then Return 
   
   EditArea_TextBoxes_SetRead_Status(False)
   Reload()
   txtSearch.SetFocus
   
End

Public Function Drug_Valid() As Boolean
   '------------------------------------------
   'Returns true if adequate drug information
   '------------------------------------------
   
   Return True
   
End

Public Sub EditArea_Buttons_Click()
   
   Select Case Last.tag
      Case "new"
         Drug_New()
      Case "save"
         Save()
   End Select
   
End

Public Function EditAreaTextBox_ExcludeKeys(keycode As Integer, tag As String) As Boolean
   
   Select Case tag
      Case "pack size", "strength new"
         bKeyValid = modUtil.AllowKeys(Const.AllowKeys_NumbersOnly, keycode)
      Case Else  
         bKeyValid = True  
   End Select  
   Return bKeyValid
   
End

Public Sub EditArea_TextBox_KeyPress()
   
   If Not EditAreaTextbox_ExcludeKeys(key.code, Last.tag) Then    'validate key input first
      Stop Event                                                  'no good? don't show key output
      Return
   End If
   Select Case Key.Code
      Case key.Down
         If listview2.Visible Then
            listview2.MoveFirst
            listview2.Item.Selected = True
            listview2.SetFocus()            
         End If 
      Case key.Return, key.Tab
         
         Select Case Last.tag
            Case "brand"
               If Hbox_AddGeneric.Visible Then
                  txtGenericNew.SetFocus
               Else
                  txtGeneric.SetFocus
               Endif
            Case "generic"
               txtStrength.SetFocus()
            Case "strength"
               txtForm.SetFocus()
            Case "form"
               txtPackSize.SetFocus()
            Case "pack size"
               txtComment.SetFocus()
            Case "comment"
               txtPIFile.SetFocus()   
            Case "generic add"
               If Trim(Last.text) = "" Then
                  txtForm.SetFocus()
               Else
                  txtStrengthNew.SetFocus()
               Endif
               
            Case "strength add"
               cmbUnits.SetFocus()
         End Select
         
   End Select
   
End Sub

Public Sub listview2_KeyPress()
   
   If key.code = key.return Then
      listview2_DblClick
   Endif
   
End Sub

Public Sub listview2_DblClick()
   
   listview2.MoveCurrent
   txtForm.text = drug_forms[listview2.Item.key]!form
   fk_form = listview2.Item.key 
   listview2.Visible = False  
   If drug_forms[listview2.Item.key]!volume_amount_required Then
       HBox_Volume.Enabled = True   
      txtVolume.SetFocus()
   Else
      HBox_Volume.Enabled = False  
      txtPackSize.SetFocus()
   End If   
   
End

Public Sub EditArea_TextBox_GotFocus()
   
   Last.BackGround = Color.rgb(95, 255, 175)   
   ListView2.Visible = False  
   Select Case Last.tag                         
      Case "form", "atc", "company"
         With listview2                        
            .top = Last.Parent.Parent.top + Last.Parent.height 
            .Left = Last.Parent.Left           
            .width = Last.width   
            .Visible = False                   
            .Raise                             
         End With                              
   End Select                                   
   
End  

Public Sub EditArea_TextBox_KeyRelease()
   
   Select Case Last.tag
      Case "form"
         Drug_Forms_Get()
   End Select
   
End

Public Sub EditArea_TextBox_LostFocus()
   
   Last.Background = Color.White
   If Last.tag <> "form" Then ListView2.Visible = False  
   
End

Public Sub txtSearch_GotFocus()
   
   EditArea_TextBoxes_SetRead_Status(True)
   
End

Public Sub ListView1_Menu()    
   
   If Last.count Then mnuGenerics.Popup()
   
End

Public Sub mnuGenerics_Click()
   
   Select Case Last.tag
      Case "edit"
         modUtil.NotImplemented("Editing a generic component")
      Case "delete"
         modUtil.NotImplemented("Deleting a generic component")
         
   End Select
   
End

Public Sub tbAddGeneric_KeyPress()
   
   If key.code = key.return Then
      tbAddGeneric_Click()
   End If  
   
End

Public Sub tbAddGeneric_Click()
   
   ListView1.Add(ListView1.count, txtGenericNew.text & " " & txtStrengthNew.text & " " & cmbUnits.text)
   txtGenericNew.Clear()
   txtStrengthNew.Clear()
   txtGenericNew.SetFocus()
   
End

Public Sub txtSearch_KeyPress()
   
   If key.code = key.down Then
      If cvwDrugs.count Then
         cvwDrugs.MoveFirst
         cvwdrugs.SetFocus
         
      Endif
   Endif
   
End

Public Sub EditArea_Combo_KeyPress()
   
   If key.code = key.return Then
      tbAddGeneric.SetFocus()
   End If   

End
