' Gambas class file
' Copyright (C)  Beno√Æt Minisini

' This program is free software: you can redistribute it and/or modify
' it under the terms of the GNU General Public License as published by
' the Free Software Foundation, either version 3 of the License, or
' (at your option) any later version.

' This program is distributed in the hope that it will be useful,
' but WITHOUT ANY WARRANTY; without even the implied warranty of
' MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
' GNU General Public License for more details.

' You should have received a copy of the GNU General Public License
' along with this program.  If not, see <http://www.gnu.org/licenses/>.

Property Enabled As Boolean
Property ToolTip As String
Property Foreground As Integer
Property Font As Font
Property Visible As Boolean
Property Expand As Boolean
Property Text As String 
Private $hPanel As Panel
Public $hImage As PictureBox
Private $hLabel As Label
Private $iColor As Integer
Public $hImageNotify As PictureBox 'just experimenting - if outstanding tasks, show red_button image (set from FMain for example)

Event Click()

Public Sub _new(hContainer As Container, X As Integer, Y As Integer, W As Integer, H As Integer, sText As String, vIcon As Variant, Optional sFont As String, Optional sIcon As String)
   
   Dim hPict As Picture
   
   $hPanel = New Panel(hContainer) As "Panel"
   $hPanel.Move(X, Y, W, H)
   $hPanel.Mouse = Mouse.Pointing
   $hPanel.Arrangement = Arrange.Horizontal
   $hPanel.Padding = 2
   $hPanel.Spacing = 8
   
   If TypeOf(vIcon) = gb.String Then
      hPict = Picture[vIcon]
   Else
      hPict = vIcon
   Endif
   
   $hImage = New PictureBox($hPanel) As "Panel"
   If hPict Then
      $hImage.Resize(hPict.Width + 4, H - 8)
      $hImage.Alignment = Align.Right
      $hImage.Picture = hPict
   Else
      $hImage.Resize(32, H - 8)
   Endif
   
   $hLabel = New Label($hPanel) As "Panel"
   $hLabel.AutoResize = True
   $hLabel.Expand = False
   
   If sFont Then $hLabel.Font = Font[sFont]
   $hLabel.Text = sText
   
   If sIcon <> ""
      hPict = Picture[sIcon]
      $hImageNotify = New PictureBox($hPanel) As "Notify"
      If hPict Then
         $hImageNotify.Resize(hPict.Width + 4, H - 8)
         $hImageNotify.Alignment = Align.Right
         $hImageNotify.Picture = hPict
         $hImageNotify.Visible = False 
      Else
         $hImageNotify.Resize(32, H - 8)
      Endif
      Print $hImageNotify
   End If

End

Public Sub Panel_Enter()
   
   If Object.Type(Last) <> "Panel" Then Return
   $iColor = Last.Foreground
   Last.Background = Color.SelectedBackground '&88D5FF&
   
   Last.Foreground = Color.SelectedForeground '&88D5FF&
   
End

Public Sub Panel_Leave()
   
   If Object.Type(Last) <> "Panel" Then Return
   Last.Background = Last.Parent.Background
   Last.Foreground = $iColor
   
End

Private Function GetPanel() As Panel
   
   Dim hPanel As Panel
   
   Try hPanel = Last
   If Error Then
      hPanel = Last.Parent
   Endif
   
   Return hPanel
   
End

Public Sub Panel_MouseDown()
   
   With GetPanel()
      .Border = Border.Sunken
   End With
   
   '$hLabel.Move($hLabel.X + 1, $hLabel.Y + 1)
   '$hImage.Move($hImage.X + 1, $hImage.Y + 1)
   
End

Public Sub Panel_MouseUp()
   
   Dim hPanel As Panel
   Dim X As Integer
   Dim Y As Integer
   
   hPanel = GetPanel()
   hPanel.Border = Border.None
   
   X = Mouse.X
   Y = Mouse.Y
   
   If Last <> hPanel Then
      X = X + Last.X
      Y = Y + Last.Y
   Endif
   
   '$hLabel.Move($hLabel.X - 1, $hLabel.Y - 1)
   '$hImage.Move($hImage.X - 1, $hImage.Y - 1)
   
   If (X >= 0 And Y >= 0 And X < hPanel.W And Y < hPanel.H) Then
      
      Raise Click()
      
   Else
      
      'PRINT "Pas Click !"; X; " "; Y; " "; hPanel.W; " "; hPanel.H
      
   Endif
   
End

Private Function Expand_Read() As Boolean
   
   Return $hPanel.Expand
   
End

Private Sub Expand_Write(bExpanded As Boolean)
   
   $hPanel.Expand = bExpanded
   
End

Private Function Text_Write(txt As String)
   
   $hLabel.text = txt
   
End

Private Function Text_Read() As String 
   
   Return $hLabel.text
   
End

Private Function Enabled_Read() As Boolean
   
   Return $hPanel.Enabled
   
End

Private Sub Enabled_Write(bEnabled As Boolean)
   
   $hPanel.Enabled = bEnabled
   
End

Public Sub Move(X As Integer, Y As Integer)
   
   $hPanel.Move(X, Y)
   
End

Public Sub Resize(W As Integer, H As Integer)
   
   $hPanel.Resize(W, H)
   '$hLabel.Resize(W - ($hImage.X + $hImage.W + 8) - 8, H - 8)
   
End

Private Function ToolTip_Read() As String
   
   Return $hPanel.ToolTip
   
End

Private Sub ToolTip_Write(sToolTip As String)
   
   $hPanel.ToolTip = sToolTip

End

Private Function Visible_Read() As Boolean
   
   Return $hPanel.visible
   
End

Private Sub Visible_Write(Value As Boolean)
   
   $hPanel.visible = Value
   
End

Private Function Foreground_Read() As Integer
   
   Return $hPanel.Foreground
   
End

Private Sub Foreground_Write(Value As Integer)
   
   $hPanel.Foreground = Value
   
End

Private Sub Font_Write(Value As Font)
   
   $hLabel.Font = Value
   
End

Private Function Font_Read() As Font

   Return $hLabel.Font
   
End
