' Gambas class file

' Copyright (C) 2008-2014 Dr. Richard Terry
' This program is free software: you can redistribute it and/or modify
' it under the terms of the GNU General Public License as published by
' the Free Software Foundation, either version 3 of the License, or
' (at your option) any later version.
' This program is distributed in the hope that it will be useful,
' but WITHOUT ANY WARRANTY; without even the implied warranty of
' MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
' GNU General Public License for more details.
' You should have received a copy of the GNU General Public License
' along with this program.  If not, see <http://www.gnu.org/licenses/>.
'----------------------------------------------------------------------
'PURPOSE    Holds all the forms needed to action a document
'           The document metadata
'           Staff Tasks
'-----------------------------------------------------------------------

Public FMetaData As FDocumentMetaData
Public FTasks As FStaffTasks
Private Embedded_Forms As Collection 
'-------------------------------------------------------------------
'Constants to describe the tabs which action a document on tabstrip1
'--------------------------------------------------------------------
Private Const Metadata As Integer = 0
Private Const tasks As Integer = 1
Private Embedded_In_Form As String
Private SelectedDocument As Collection 
Public context_date As String 
Private currentconsult As CConsult

Public Sub Init(embedding_form As String)
   '--------------------------------------------------------------------------
   'only metadata is loaded at first as this often the only bit the user needs
   'unless they action the document in a more complex way
   '--------------------------------------------------------------------------

   Embedded_In_Form = embedding_form
   With FMetaData = New FDocumentMetaData(Vbox_Metadata)
      .Set_Embedded_Form_Name("FInbox")
      .Init("Allocated Documents")
   End With
   Embedded_Forms = New Collection 

End

Public Sub Init_Cons(Cons As CConsult)    'in the inbox this consult is an audit cConsult

   currentconsult = Cons                   'this now = the cConsult for patient in the inbox
   FMetaData.Set_Consult(currentconsult)   'passed now to the document metatdata for (Save)

End

Public Sub Set_Document(D As Collection)
   
   SelectedDocument = D
   
End

Public Sub TabStrip1_Click()
   
   Dim patient_details As String       
   
   If FMetaData.get_fk_patient() = 0 Then
      Tabstrip1.index = 0
      Return 
   End If   
   patient_details = currentconsult!patient!wholename & " [" & Format(currentconsult!patient!birthdate, "dd/mm/yyyy") & "] " & currentconsult!patient!age_numeric & "yrs"
   Select Case Tabstrip1.Index
      Case tasks
         '---------------------------------
         'Create a new staff tasks instance
         '---------------------------------
         If IsNull(FTasks) Then
            FTasks = New FStaffTasks(Hbox_Tasks)
         End If  
         With FTasks  
            .Set_Parent_Form(Embedded_In_Form)                 'Embedded_In_Form  = "Inbox" or "FDayBook"= staff tasks
            .Set_Audit_Schema_Table_SourceRowKey(SelectedDocument!pk_document, "documents.documents")  'will be removed once ian does the audit stuff
            .Set_Consult(currentconsult)                       'currentconsult has been passed from the parent form
            SelectedDocument!tag_user = FMetaData.txtUserObservationName.Text
            .Set_Document(SelectedDocument)        'Key to documents.documents 
            .BExit = True 
            .Init()                                            'this clears edit area etc, shows existing tasks
            .txtPatient.text = SelectedDocument!patient_title & " " & SelectedDocument!patient_firstname & " " & SelectedDocument!patient_surname
            .lblBirthdate.text = Format(SelectedDocument!patient_birthdate, "dd/mm/yyyy")
            .txtStreet.text = Trim(SelectedDocument!patient_street1 & " " & SelectedDocument!patient_street2)
            .txtSuburb.text = SelectedDocument!patient_town & " " & SelectedDocument!patient_postcode 
            .txtPatient.ReadOnly = True  
            .txtStreet.ReadOnly = True
            .txtSuburb.ReadOnly = True
            .lblFinaliseTask.Visible = False  
            .HBox_FinalisedBy.Visible = False 
            .Hbox_DateFinalised.Visible = False
            .txtRelatedTo.TEXT = FMetaData.txtUserObservationName.text
            .lblNotesAllMembers.visible = False  
            .VBox_Notes.Visible = False   
            .tbNewPatientTask.text = "New Task"
            .chkVerifyCompleted.Visible = True
            .bexit = False  
         End With
   End Select
   
End

Public Sub EmbeddedForms_Reset()
   
   Dim O As Object
   
   For Each O In Embedded_Forms
      O.Consult_Set_Null()
   Next
   
End 

Public Sub Tabstrip1_MouseDown()

   Dim document_type As String
   Dim comment_type As String 
   'fixme remove most of this see notes in bugs file
   If FMetaData.txtUserObservationName.text = "" Then
      Stop Event
      Select Case FMetaData.iDisplay_As
         Case const.Document_Display_As_Letter
            document_type = "Letter"
            comment_Type = "letter summary"
         Case const.Document_Display_As_Result
            document_type = "Result"
            comment_type = "user name"
         Case const.Document_Display_As_Certificate
            document_type = "Certificate"
            comment_type = "details"
         Case const.Document_Display_As_Image
            document_type = "Image"
            comment_type = "details"
      End Select
      Tabstrip1.index = 0
      Message.Info("Please complete the " & comment_type & " for this " & Lower(document_type) & " before attempting to action this document")
  End If
End

