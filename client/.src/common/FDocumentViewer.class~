' Gambas class file

Private form_pdf As FPdf
Private form_pictures_viewer As FPictureViewer

Public Sub Form_Open()
   
   Settings_Load 
   
End

Public Sub Form_Close()
   
   Settings_Save 
   
End

Public Sub Load_HTML(sHtml As String)
   
   Try form_pdf.Visible = False 
   Try form_pictures_viewer.Visible = False   
   With Webview1
      .HTML = sHtml
      .Visible = True
   End With
   
End

Public Sub Load_Picture(sfilename As String)
   
   Webview1.Visible = False
   Try form_pdf.Visible = False
   If IsNull(form_pictures_viewer) Then
      form_pictures_viewer = New FPictureViewer(vbox1)
   Endif
   With form_pictures_viewer
      .Init(sfilename)
      .Visible = True
   End With
   
End

Public Sub Load_PDF(filename As String)
   
   Webview1.Visible = False  
   Try form_pictures_viewer.Visible = False    
   If IsNull(form_pdf) Then
      With form_pdf = New FPdf(Vbox1)
         With .btRotate
            .Visible = True   
            .enabled = True   
         End With
      End With
   End If 
   With form_pdf
      .CurrentZoom = Settings["FDocumentViewer/FPDF.zoom", 1.0]
      .Load_PDF(filename)
      .Visible = True   
   End With
   
End

Public Sub zoom_in()
   'Don't put this back in tbWebBrowser_click as is called from outside this form
   
   If Webview1.Visible Then 
      WebView1.Zoom = Round(webview1.Zoom * 1.1, -2)
   Else
      form_pdf.zoom_in
   End If  
   Settings_Save
   
End

Public Sub zoom_out()
   'Don't put this back in tbWebBrowser_click as is called from outside this form
   
   If Webview1.Visible Then    
      WebView1.Zoom = Round(webview1.Zoom / 1.1, -2)
   Else
      form_pdf.zoom_out
   End If 
   Settings_Save
   
End

Public Sub Settings_Save()
   
   Settings["FDocumentViewer/webview1.zoom"] = WebView1.Zoom
   Try Settings["FDocumentViewer/FPDF.zoom"] = form_pdf.CurrentZoom  'may not be instantiated
   
End

Public Sub Settings_Load()
   
   webview1.zoom = Settings["FDocumentViewer/webview1.zoom", 1.0]
   Try form_pdf.CurrentZoom = Settings["FDocumentViewer/FPDF.zoom", 1.0] 'may not be instantiated
   
End

Public Sub Print_Content()
   
   If Webview1.Visible Then 
      modPrinting.PrintHtml(WebView1.HTML, const.Paper_Plain)
   End If
   If Not IsNull(form_pdf) Then  
      If form_pdf.Visible Then 
         
         form_pdf.Print_Content
      End If   
   End If 
   If Not IsNull(form_pictures_viewer) Then  
      If form_pictures_viewer.Visible Then
         form_pictures_viewer.Print_Content
      Endif
   End If   

End
