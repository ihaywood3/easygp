' Gambas class file

' Copyright (C) 2008-2016 Dr. Richard Terry

' This program is free software: you can redistribute it and/or modify
' it under the terms of the GNU General Public License as published by
' the Free Software Foundation, either version 3 of the License, or
' (at your option) any later version.

' This program is distributed in the hope that it will be useful,
' but WITHOUT ANY WARRANTY; without even the implied warranty of
' MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
' GNU General Public License for more details.

' You should have received a copy of the GNU General Public License
' along with this program.  If not, see <http://www.gnu.org/licenses/>.

Private BrowserHtmlFileName As String
Private selected_link As String
Private Document_Details As String
Private current_help As String
Private sParent_Form As String
Private sSection As String
Private bPrintLandscape As Boolean 'default false

Public Sub Set_Landscape_Printing(flag As Boolean)
   
   bPrintLandscape = flag    
   
End

Public Sub Section_Set(s As String)
   
   sSection = s 'e.g when called from Mental Health = "Decision Support" 
   
End

Public Sub Parent_Form_Set(pf As String)
   
   sParent_Form = pf
   
End

Public Sub Set_Document_Details(dd As String)
   
   Document_Details = dd
   
End

Public Function Get_Document_Details() As String
   
   Return Document_Details
   
End

Public Sub SetHtmlFileName(sstring As String)
   
   BrowserHtmlFileName = sstring
   
End

Public Sub tbWebBrowser_Click()
   
   Select Case Last.tag
      Case "print"
         Print_Content
      Case "zoom in"
         zoom_in()
      Case "zoom out"
         zoom_out()
      Case "reload document"
         
   End Select
   
End

Public Sub zoom_in()
   'Don't put this back in tbWebBrowser_click as is called from outside this form
   
   WebView1.Zoom = Round(webview1.Zoom * 1.1, -2)
   Settings_Save
   
End

Public Sub zoom_out()
   'Don't put this back in tbWebBrowser_click as is called from outside this form
   
   WebView1.Zoom = Round(webview1.Zoom / 1.1, -2)
   Settings_Save
   
End

Public Sub Settings_Save()
   
   Settings[sParent_Form & "/" & sSection & ".webview1.zoom"] = WebView1.Zoom
   
End

Public Sub Settings_Load()
   
   If IsNull(Settings[sParent_Form & "/" & sSection & ".webview1.zoom"]) Then
      webview1.Zoom = 1.0
   Else
      webview1.Zoom = Settings[sParent_Form & "/" & sSection & ".webview1.zoom"]
   Endif
   If webview1.zoom < 0.5 Then webview1.Zoom = 1.0
   
End

Public Sub Print_Content()
   
   modPrinting.PrintHtml(WebView1.HTML, const.Paper_Plain)
   
End

Public Sub WebView1_MouseDown()
   
   Dim hView As WebView = Last
   Dim hTest As WebHitTest = hView.HitTest(Mouse.X, Mouse.Y)
   Dim sMsg As String
   
   If hTest.Document Then sMsg &= "DOCUMENT "
   If hTest.Link Then
      sMsg &= "LINK "
      selected_link = hTest.Url
   End If
   If hTest.Image Then sMsg &= "IMAGE "
   If hTest.Selected Then
      sMsg &= "SELECTED "
      Clipboard.Copy(Webview1.SelectedText)
   End If
   If hTest.Editable Then sMsg &= "EDITABLE "
   Debug sMsg; hTest.Url
   
End

Public Sub Button1_Click()
   
   webview1.PopupMenu()
   
End

Public Sub Help_Show(topic As String)
   
   If current_help <> topic Then
      Try Me.WebView1.HTML = File.Load(modUtil.Find_File("help" &/ topic))
      Me.WebView1.HTML = Replace(Me.WebView1.HTML, "src=\"", "src=\"" & modUtil.Find_File("help"))
      current_help = topic
   Endif
   
End

Public Sub Form_Open()
   
   Try Settings_Load()
   
End

Public Sub tbExternalViewer_Click()
   'User wants to display the html contents in an external viewer as a pdf
   'note the orientation can be set externally by public sub Set_Landscape_Printing
    
   Dim pdf_path As String
   Dim orientation As Integer

   If bPrintLandscape Then 
      orientation = const.orientation_landscape
   Else
      orientation = const.orientation_portrait
   End If   
   pdf_path = modPrinting.Make_PDF_From_Html(webview1.HTML, orientation)
   modArchiveDocumentsDBI.Launch_External_PDF_Viewer(pdf_path)
   
End
