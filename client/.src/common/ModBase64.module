' Gambas module file

' Gambas module file
' (c) Christer Nygren, petterfilip@gmail.com, gambas@fronet.fro.se + 11 more addresses....puh !
' Ireland and whiskey, bah !, In Gothenburg you could buy Mackmyra in the Systembolaget http://www.mackmyra.se/

Private Const B64Const As String = "=ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz0123456789+/"

Public Sub ToB64(TextStr As String) As String
  Dim Position As Long
  Dim c As String
  Dim dt As Byte
  Dim B64Coded, u As String
  Dim Tkn As Integer
  '
  Position = 1
  Do Until Position > Len(TextStr)
    Wait                          ' Let other things happen
    c = Mid(TextStr, Position, 1) ' Ta ut ett tecken
    Tkn = Tkn + 1
    Select Case Tkn
      Case 1
        dt = Asc(c)
        u = Mid(B64Const, (((dt And &HFC&) / 4) + 2), 1)  ' (1) 1111.1100
      Case 2
        dt = (dt And &H3&) * 16              ' 0000.0011 resterande 2 bitar fr√•n tecknet innan
        u = u & Mid(B64Const, (((Asc(c) And &HF0&) / 16 + dt) + 2), 1) ' (2) 0000.0011 1111.0000
        dt = Asc(c) And &HF&
      Case 3
        Tkn = 0
        dt = dt * 4
        u = u & Mid(B64Const, (dt + ((Asc(c) And &HC0&) / 64) + 2), 1)
        u = u & Mid(B64Const, ((Asc(c) And &H3F&) + 2), 1)
        B64Coded = B64Coded & u
    End Select 
    Inc Position
  Loop   
  Select Case Tkn
    Case 1
      dt = (dt And &H3&) * 16
      u = u & Mid(B64Const, (dt + 2), 1)
    Case 2
      u = u & Mid(B64Const, ((dt * 4) + 2), 1)
   End Select
   If Len(u) < 4 Then
     u = u & String(4 - Len(u), "=")
     B64Coded = B64Coded & u
   Endif
   Return B64Coded
End


Public Sub FromB64(B64Str As String) As String
  Dim Position As Long
  Dim dt As Byte
  Dim B64Decoded, u, c As String
  Dim Tkn, p As Integer
  '
  Position = 1
  Tkn = 0
  Do Until Position > Len(B64Str)
    Wait
    c = Mid(B64Str, Position, 1)
    p = InStr(B64Const, c)
    If p > 1 Then
      p = p - 2
      Inc Tkn
      Select Case Tkn
        Case 1
          dt = (p * &h4)
        Case 2
          B64Decoded = B64Decoded & Chr(dt + ((p And &H30&) / &H10&))
          dt = (p And &HF&) * &H10&
        Case 3
          B64Decoded = B64Decoded & Chr(dt + ((p And &H3C&) / 4))
          dt = (p And &H3&) * &H40&
        Case 4
          B64Decoded = B64Decoded & Chr(dt + p)
      End Select
      If Tkn = 4 Then Tkn = 0   
   Else    ' Ej skrivbart tecken
     If p = 1 Then
       Position = Len(B64Str) + 1
     End If
   Endif
   Inc Position   
  Loop
   Select Case Tkn
     Case 1
       B64Decoded = B64Decoded & Chr(dt)
   End Select
  Return B64Decoded 
End