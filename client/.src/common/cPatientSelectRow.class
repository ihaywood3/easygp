' Gambas class file

' cPatientSelectRow
' Copyright (C) 2008-2013 Dr. Richard Terry
' class to construct a control containing the patient's photo
' and basic details to assist selecting patients
'
' This program is free software: you can redistribute it and/or modify
' it under the terms of the GNU General Public License as published by
' the Free Software Foundation, either version 3 of the License, or
' (at your option) any later version.
'
' This program is distributed in the hope that it will be useful,
' but WITHOUT ANY WARRANTY; without even the implied warranty of
' MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
' GNU General Public License for more details.

' You should have received a copy of the GNU General Public License
' along with this program.  If not, see <http://www.gnu.org/licenses/>.
'
' PURPOSE   Contruct a row for a list container - with patients photo & details
'------------------------------------------------------------------------------
Private Patient_Photo As Picture
Private $hbox As HBox
Private $HBox_Picture As HBox
Private $PictureBox As PictureBox
Private $PnlSpacer As Panel
Private $TextLabel As TextLabel

Event Click()

Public Sub _new(hContainer As ListContainer, patient As Collection)
   Dim comm As Collection  'to iterate through patients comms (phone numbers, email etc)
   
   Patient = patient         
   '--------------------------------------------------------------------------
   'This is the main 'container', holding the patient picture and text details
   '--------------------------------------------------------------------------     
   With $hbox = New HBox(hContainer)
      .Mouse = Mouse.Pointing
      .expand = False
      .padding = 3
      .AutoResize = True  
      .height = 80
      .tag = patient!pk_view 
   End With
   '-------------------------------------------------------------------
   'Does the patient have a picture?, if not load an anonymous one
   'patient!image - this is blob data, not an image, must be converted
   '-------------------------------------------------------------------
   If Not IsNull(patient!image.data) Then 
      Patient_Photo = modGraphics.Blob_Convert_To_Picture(patient!image)!picture  
   Else
      Patient_Photo = Picture.Load("icons/misc/no_photo.png") 
   Endif
   
   With $HBox_PIcture = New HBox($hbox)
      .width = 72
      .name = "HBox_Patient_Photo"
      .Height = 82
      .Background = Color.White
      .padding = 1
   End With
   '--------------------------------------------------------------------------------------
   'This is a picture box, first child of the HBox in the continer, holds patients picture
   '--------------------------------------------------------------------------------------
   With $PictureBox = New PictureBox($HBox_Picture)
      .Border = True  
      .Background = Color.white 
      .Picture = Patient_Photo
      .Stretch = True   
      .width = 70
      .Height = 80
      .Expand = False
   End With
   '-----------------------------------------------------
   'put a spacer between the picture and the next control
   '-----------------------------------------------------
   With $PnlSpacer = New Panel($hbox)
      .width = 50 
   End With
   '---------------------------------------------------------------------
   'Now the text showing patient's name/address/contact phone numbers etc
   '---------------------------------------------------------------------
   With $TextLabel = New TextLabel($Hbox) As "Text_Label"
      .Expand = True 
      .AutoResize = False  
      .font = Application.Font
      .text = "<P><FONT COLOR=\"#280099\"><SPAN STYLE='background:#ffffff'><B>" & patient!surname & " " & patient!firstname & " " & "Age: "
      If patient!age_numeric < 5 Then 
         .text &= patient!age_display
      Else
         .text &= patient!age_numeric
      End If   
      .text &= " (" & patient!sex & ")</B></P>"
      .text &= "<TABLE>"
      .text &= "<Tr><TD WIDTH=15%><small>Birthdate</TD><TD><small>" & Format(patient!birthdate, "dd/mm/yyyy") & "</small></TD></TR>"
      .text &= "<TR><TD WIDTH=15%><small>Address</TD><TD><small>" & Trim(patient!street1 & " " & patient!street2) & " " & patient!town & " " & patient!postcode & "</small></TD></TR>"
      .text &= "<TR><TD WIDTH=15%><small>Contacts</TD><TD><small>"
      If Not IsNull(patient!comms)
         For Each comm In patient!comms 
            .text &= comm!type & ": " & comm!value & "  "
         Next
      End If   
      .text &= "</small></TD></TR>"
      .text &= "</TABLE>"
      .text &= ""     
   End With
End

Private Function Get_txt_label() As TextLabel
   
   Dim htxtLabel As TextLabel
   
   Try htxtLabel = Last
  
   
   Return htxtLabel
   ' 
End



Public Sub Text_Label_MouseUp()
   
   Dim htxtLabel As TextLabel
   Dim X As Integer
   Dim Y As Integer
   
   htxtLabel = Get_txt_label()
   
   
   X = Mouse.X
   Y = Mouse.Y
   
   If Last <> htxtLabel Then
      X = X + Last.X
      Y = Y + Last.Y
   Endif
   If (X >= 0 And Y >= 0 And X < htxtLabel.W And Y < htxtLabel.H) Then
      Raise Click()
      
   Else
      
   Endif
   
End
