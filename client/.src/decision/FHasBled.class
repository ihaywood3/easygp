' Gambas class file

' Hypertension History?(uncontrolled, > 160 mmHg systolic)Yes + 1
' Renal Disease?(Dialysis, transplant, Cr > 2.6 mg / dL Or > 200 µmol/L)    Yes+1
' Liver Disease?(Cirrhosis, Bilirubin > 2 x Normal, AST / ALT / AP > 3 x Normal)Yes + 1
' Stroke History? Yes + 1
' Prior Major Bleeding Or Predisposition To Bleeding? Yes + 1
' Labile INR?(Unstable / high INRs, Yes + 1
' Age ≥65?    Yes+1
' Medication Usage Predisposing To Bleeding?(Antiplatelet agents, NSAIDs)Yes + 1
'Alcohol Usage History? Yes + 1
'    Pisters R, Lane DA, Nieuwlaat R, de Vos CB, Crijns HJ, Lip GY.
'    A novel user - friendly score(HAS - BLED) To assess 1 - Year risk Of major bleeding In patients With atrial fibrillation:
'    the Euro Heart Survey.Chest.2010 Nov; 138(5): 1093 - 100. Epub 2010 Mar 18. PubMed PMID: 20299623.
'   Lip GY, Frison L, Halperin JL, Lane DA.
'   Comparative validation Of a novel risk score For predicting bleeding risk In anticoagulated patients With atrial fibrillation:
'    the HAS - BLED
'    (Hypertension, Abnormal Renal / Liver Function, Stroke, Bleeding History Or Predisposition, Labile INR, Elderly, Drugs / Alcohol Concomitantly)score.J Am Coll Cardiol.2011 Jan 11; 57(2): 173 - 80. Epub 2010 Nov 24. PubMed PMID: 21111555.
'
Private CurrentConsult As CConsult
Private iHasBledScore As Integer

Public Sub Init(cons As CConsult)

   CurrentConsult = cons

   With gridview1
      .Columns.count = 2
      .Rows.count = 3
      .Columns[0].Text = "Score"
      .Columns[1].Text = "% Bleeds per 100 patient years"

      '   .Columns[0, 1].Text = "Low Risk (1.1%)"
   End With
   With gridview1[0, 0]
      .text = "0-1"
      .Alignment = Align.Center
   End With
   With gridview1[1, 0]
      .text = "2"
      .Alignment = Align.Center
   End With
   With gridview1[2, 0]
      .text = ">=3"
      .Alignment = Align.Center
   End With
   gridview1[0, 1].text = "Low risk (1.1%)"
   gridview1[1, 1].text = "Intermediate Risk (1.9%)"
   gridview1[2, 1].text = "High Risk (4.9%)"

End

Public Sub EditARea_CheckBoxes_Click()

   Last.Parent.Parent.Tag = Last.value
   HasBled_Calculate()

End

Public Sub HasBled_Calculate()

   Dim hb As HBox
   Dim hCtrl As Control

   iHasBledScore = 0
   For Each hctrl In Vbox_EditARea.Children
      If hctrl Is Hbox Then
         hb = hctrl
         If hb.tag = True Then
            Inc iHasBledScore
         Endif
      Endif
   Next
   lblHasBledScore.text = "Hasbled Score= " & Str(iHasBledScore)

End
