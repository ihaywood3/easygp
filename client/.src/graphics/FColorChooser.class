' Gambas class file
' Copyright (C) 2008-2011 Dr. Richard Terry

' This program is free software: you can redistribute it and/or modify
' it under the terms of the GNU General Public License as published by
' the Free Software Foundation, either version 3 of the License, or
' (at your option) any later version.

' This program is distributed in the hope that it will be useful,
' but WITHOUT ANY WARRANTY; without even the implied warranty of
' MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
' GNU General Public License for more details.

' You should have received a copy of the GNU General Public License
' along with this program.  If not, see <http://www.gnu.org/licenses/>.

Static Public Color As String
Static Public Value As Integer

Static Private $sColor As String
Static Private $bAlpha As Boolean
Static Private $aSystem As String[] = ["Background", "Foreground", "SelectedBackground", "SelectedForeground", "LightBackground", "TextBackground", "TextForeground", "ButtonBackground", "ButtonForeground"]

Private Const DEFAULT_COLOR As String = "-"

Private $cColor As New Collection

Static Public Function Run(Optional sColor As String, Optional bAlpha As Boolean) As Boolean

  $sColor = sColor
  $bAlpha = bAlpha
  'IF NOT $sColor THEN $sColor = Color
  'IF NOT $sColor THEN $sColor = "&H" & Hex$(Value, 6)
  If FColorChooser.ShowModal() Then
    Color = $sColor
    If $sColor Then
      Try Value = Val($sColor)
      If Error Then 
        Value = Object.GetProperty(Classes["Color"], $sColor)
      Else 
        Color = "&H" & Hex$(Value, 8) & "&"
      Endif
    Else
      Value = -1
    Endif
    'DEBUG Value;; "'"; Color; "'"
  Else
    Return True
  Endif

End


Private Sub AddColor(sText As String, sTag As String, sColor As String)

  Dim hBox As HBox
  Dim hLabel As Label
  Dim hColor As Label
  Dim hPict As PictureBox

  hBox = New HBox(lstSystem)
  hBox.Padding = 4
  hBox.Spacing = 4
  hBox.Height = 24
  hBox.Tag = sTag

  If sColor = DEFAULT_COLOR Then
    hPict = New PictureBox(hBox)
    'hPict.Border = Border.Plain
    hPict.Picture = Picture["img/16/tile.png"]
    hPict.Resize(16, 16)
    hPict.Design = True
  Else
    hColor = New Label(hBox)
    hColor.Background = Object.GetProperty(Classes["Color"], sColor)
    hColor.Border = Border.Plain
    hColor.Resize(16, 16)
    hColor.Design = True
  Endif

  hLabel = New Label(hBox)
  hLabel.Expand = True
  hLabel.Text = sText
  hLabel.Design = True

  $cColor[sColor] = hBox

End

Public Sub btnOK_Click()

  If tabColor.Index = 1 Then
    $sColor = dlgColor.SelectedColor
  Else
    Try $sColor = lstSystem.Current.Tag
  Endif
  Me.Close(True)

End

Public Sub btnCancel_Click()

  Me.Close

End

Public Sub Form_Open()

  Dim sSystem As String

  Settings.Read(Me)

  lstSystem.Clear
  AddColor("Default", "", DEFAULT_COLOR)
  For Each sSystem In $aSystem
    AddColor(sSystem, sSystem, sSystem)
  Next

  If Not IsNull(Val($sColor)) Then
    dlgColor.SelectedColor = Val($sColor)
    tabColor.Index = 1
  Else
    If $sColor Then
      lstSystem.Select($cColor[$sColor])
    Else
      lstSystem.Select($cColor[DEFAULT_COLOR])
    Endif
    tabColor.Index = 0
  Endif
  
  dlgColor.ShowAlpha = $bAlpha

End

Public Sub dlgColor_Activate()

  btnOK.Value = True

End


Public Sub lstSystem_Activate()

  btnOK.Value = True

End


Public Sub Form_Close()

  Settings.Write(Me)  

End
