' Gambas class file

' Copyright (C) 2008-2014 Dr. Richard Terry. Dr. Ian Haywood

' This program is free software: you can redistribute it and/or modify
' it under the terms of the GNU General Public License as published by
' the Free Software Foundation, either version 3 of the License, or
' (at your option) any later version.

' This program is distributed in the hope that it will be useful,
' but WITHOUT ANY WARRANTY; without even the implied warranty of
' MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
' GNU General Public License for more details.

' You should have received a copy of the GNU General Public License
' along with this program.  If not, see <http://www.gnu.org/licenses/>.
' ---------------------------------------------------------------------
' A simple not too visually accurate pie graph generator FIXME
' ---------------------------------------------------------------------
Private titles As String[]
Private Values As New Float[]
Private total_value As Integer
Private colours As Variant[]

Public Sub Init(total_count As Integer, pie_data As Collection)
   
   Colours = New Variant[]
   colours.Add(Color.Blue)
   colours.Add(Color.Red)
   colours.Add(Color.Orange)
   colours.Add(Color.Yellow)
   colours.Add(Color.Green)
   colours.Add(Color.Gray)
   colours.Add(Color.Pink)
   colours.Add(Color.Cyan)
   colours.Add(Color.Magenta)
   colours.Add(Color.DarkBlue)
   colours.Add(Color.DarkGreen)
   Make_Pie(pie_data, total_count)
   DrawingArea1.Refresh()

End

Public Sub Make_Pie(pie_data As Collection, total_count As Integer)
   
   Dim data As Collection
   
   titles = New String[]
   Values.Clear()
   For Each data In pie_data
      If data!number Then 
         If data!number Then
            AddSlice(data!label & " (" & modUtil.As_Percent(data!number, total_count) & ")", data!number)
         Endif
      Endif
   Next
   
End

Public Sub AddSlice(title As String, value As Float)
   
   If titles.Length >= 11 Then Error.Raise("Not enough colours for the pie chart")
   total_value += value
   Values.Add(value)
   titles.Add(title)
   
End

Public Sub DrawingArea1_Draw()
   
   Dim i As Integer
   Dim x2 As Integer = CInt(DrawingArea1.Width * 0.5) ' half: i.e. the midpoint
   Dim y2 As Integer = CInt(DrawingArea1.Height * 0.5)
   Dim mid_radians As Float ' middle of pie: angle for point of text
   Dim text_x As Integer
   Dim text_y As Integer  
   Dim values_thus_far As Float = 0
   
   Draw.FillStyle = Fill.Solid
   
   For i = 0 To Values.Max
      Draw.FillColor = colours[i]
      Draw.Font.Size = 6
      Draw.Ellipse(x2 / 2, y2 / 2, x2, y2, values_thus_far / total_value * 2 * Pi, (values_thus_far + Values[i]) / total_value * 2 * Pi)
      mid_radians = (values_thus_far + (Values[i] / 2)) / total_value * 2 * Pi
      text_x = CInt(x2 + (Cos(mid_radians) * x2 * 0.6))
      text_y = CInt(y2 - (Sin(mid_radians) * y2 * 0.75))
      If text_x < x2 Then ' it's to the left of the midline
         text_x -= Draw.TextWidth(titles[i])
      Endif
      Draw.Text(titles[i], text_x, text_y)
      values_thus_far += Values[i]
   Next
   
End
