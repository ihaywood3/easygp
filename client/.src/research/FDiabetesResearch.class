' Gambas class file

Private bExit As Boolean
Private dacc_in_date As Picture = Picture["icons/12/green_dot.png"]
Private dacc_out_of_date As Picture = Picture["icons/12/red_dot.png"]
Private empty_2020png As Picture = Picture["icons/20/empty2020.png"]
Private bShowPatient As Boolean
Private patients As Collection
Private Patient As Collection
Private iTotalDataSetcount As Integer 
Private sSortOrder As String = "ASC"
Private sHba1cSortOrder As String = "ASC"
Private bSortbyHba1c As Boolean = False   
Private LessThan6 As Integer
Private Between6and7 As Integer
Private Between7and7point5 As Integer
Private Between7point5and8 As Integer
Private Over8 As Integer
Private Form_Graph_Hba1c As FGraph
Private Form_Graph_Lipids As FGraph
Private Form_Graph_eGFR As FGraph
Private Form_Graph_Weight As FGraph
Private Form_Graph_BP As FGraph
Private Hba1c_Results As Result
Private Lipid_Results As Result
Private eGFR_Results As Result
Private iGraphFontSize As Integer 'applies to all graphs.
Private DACCS_Overdue As Collection
Private form_pdf As FPdf
Private medications As Collection
Private medications_display_by As String
Private colours As Variant[]
Private total_value As Float = 0
Private values As New Float[]
Private titles As String[]
Private form_Progress_Notes As FProgressNotesViewer
Private iCurrentTab As Integer
Private cTab_Summary As Integer = 0
Private cTab_LastDACC As Integer = 1 
Private cTab_ProgressNotes As Integer = 2
Private cTab_CarePlanning As Integer = 3
Private cTab_Recalls As Integer = 4
Private cTab_StaffTasks As Integer = 5
Private CurrentConsult As CConsult  
Private Patients_Not_Diabetic As Collection
Private form_recalls As FRecalls
Private form_tasks As FStaffTasks
Private form_dacc As FDACC
Private egFRs As Collection
Private iDisplay_Subset As Integer
Private Const cDisplaySubset_Any As Integer = 0
Private Const cDisplaytype_Cholesterol_Not_At_Target As Integer = 1
Private Const cDisplaytype_Cholesterol_Overdue As Integer = 2
Private bTempory_Table_Exists As Boolean

Public Sub Init()
   
   lblHba1cRange.width = lblFindPatient.Width
   cvwPatients.Columns.count = 6
   cvwPatients.Columns[2].Text = "Age"
   cvwPatients.Columns[3].Text = "Last Date" 'is column 5
   cvwPatients.Columns[4].Text = "Hba1c"
   cvwValues.Columns.count = 2
   cvwValues.Columns[0].text = "Date"
   cvwValues.Columns[1].text = "Hba1c"
   cmbSortBy.Add("surname", 0)
   cmbSortBy.Add("age", 1)
   cmbSortBy.Add("hba1c")
   cmbHba1cRange.Add("all")
   cmbHba1cRange.Add("<6.0")
   cmbHba1cRange.Add("6.0-7.0")
   cmbHba1cRange.Add("7.0-7.5")
   cmbHba1cRange.Add("7.5-8.0")
   cmbHba1cRange.Add(">8.0") 
   Colours = New Variant[]
   colours.Add(Color.Blue)
   colours.Add(Color.Red)
   colours.Add(Color.Orange)
   colours.Add(Color.Yellow)
   colours.Add(Color.Green)
   colours.Add(Color.Gray)
   colours.Add(Color.Pink)
   colours.Add(Color.Cyan)
   colours.Add(Color.Magenta)
   colours.Add(Color.DarkBlue)
   colours.Add(Color.DarkGreen)
   
   medications_display_by = "brand"
   SEttings_Load()
   bExit = True   
   
   With Form_Graph_HBa1c = New FGraph(Hbox_GraphHba1c)
      .tbGraphRemove.Visible = False   
      .tbInboxResults_AddGraph.Visible = False  
      .chkGraph_MakeDefault.Visible = False  
      .HBox_Styletypes.Visible = False 
      .Vbox_Left.visible = False
      .VBox_Right.visible = False   
      .pbMeasurementGraph.Background = Color.White
      .slGraphFontSize.value = iGraphFontSize
   End With
   With Form_Graph_Weight = New FGraph(Hbox_GraphWeight)
      .tbGraphRemove.Visible = False   
      .tbInboxResults_AddGraph.Visible = False  
      .chkGraph_MakeDefault.Visible = False  
      .HBox_Styletypes.Visible = False 
      .Vbox_Left.visible = False
      .VBox_Right.visible = False   
      .pbMeasurementGraph.Background = Color.White
      .slGraphFontSize.value = iGraphFontSize
   End With
   With Form_Graph_Lipids = New FGraph(Hbox_GraphLipids)
      .tbGraphRemove.Visible = False   
      .tbInboxResults_AddGraph.Visible = False  
      .chkGraph_MakeDefault.Visible = False
      .HBox_Styletypes.visible = False
      .Vbox_Left.visible = False
      .VBox_Right.visible = False   
      .pbMeasurementGraph.Background = Color.White
      .slGraphFontSize.value = iGraphFontSize
   End With
   With Form_Graph_eGFR = New FGraph(Hbox_GraphEGFR)
      .tbGraphRemove.Visible = False   
      .tbInboxResults_AddGraph.Visible = False  
      .chkGraph_MakeDefault.Visible = False  
      .HBox_Styletypes.Visible = False 
      .Vbox_Left.visible = False
      .VBox_Right.visible = False   
      .pbMeasurementGraph.Background = Color.White
      .slGraphFontSize.value = iGraphFontSize
   End With
   With Form_Graph_BP = New FGraph(Hbox_GraphBP)
      .tbGraphRemove.Visible = False     
      .tbInboxResults_AddGraph.Visible = False  
      .chkGraph_MakeDefault.Visible = False  
      .HBox_Styletypes.Visible = False 
      .Vbox_Left.visible = False
      .VBox_Right.visible = False   
      .pbMeasurementGraph.Background = Color.White
      .slGraphFontSize.value = iGraphFontSize
   End With
   bExit = False 
   lblMeasure.text = "Last Microalbumin   "  
   modEditAreaHelpers.Resize_labels(VBox_LastMeasurements, lblMeasure)
   
End

Public Sub cvwPatients_Refresh()       'fixme remove flag                                     
   '----------------------------------------------------------
   'Displays a list of separate patients with their last hab1c
   '----------------------------------------------------------
   
   Dim key As Integer
   
   Gui_Clear()
   cvwPatients.Columns[0].width = 24
   For Each patient In patients  
      key = patient!pk 
      If patient!date_last_dacc = "01/01/1900" Then
         cvwPatients.Add(key, "", empty_2020png)
      Else
         If patient!dacc_overdue = True Then
            cvwPatients.Add(key, "", dacc_out_of_date)
         Else
            cvwPatients.Add(key, "", dacc_in_date)
         Endif
      Endif
      cvwPatients[key][1] = patient!surname & "," & patient!firstname
      cvwPatients[key][2] = patient!age_numeric
      Select Case iDisplay_Subset
         Case cDisplaytype_Cholesterol_Not_At_Target, cDisplaytype_Cholesterol_Overdue
            cvwPatients[key][3] = Format(patient!date_last_ldlcholesterol, "dd/mm/yyyy")
            cvwPatients[key][4] = patient!ldl_cholesterol  'the cholesterol
            cvwPatients.Columns[4].text = "LDL"
            cvwPatients[key][5] = patient!ldl_cholesterol & " (" & patient!ldlcholesterol_reference_range & ")"
            cvwPatients.Columns[5].text = "Range"
         Case Else
            cvwPatients[key][3] = Format(patient!observation_date, "dd/mm/yyyy")
            cvwPatients[key][4] = patient!value_numeric  'the hba1c
            cvwPatients.Columns[4].text = "HbA1c"
            cvwPatients.Columns[5].text = ""
      End Select
   Next
   
End

Public Sub tbRefresh_Click()
   
   Patients_Reload()
   
End

Public Sub Reload()
   '------------------------------------------------------------------------------------
   'Reloads the data for all unique patients with hba1c no ordering into temporary table
   'The data is as yet incomplete, and unordered
   'we will insert new information such as data of last DACC
   'Have checked the numbers, the ranges are ok
   '------------------------------------------------------------------------------------   
   
   Dim RDacc As Result
   Dim bDaccOverdue As Boolean
   Dim Latex As String
   Dim date_last_dacc As Date
   Dim date_last_egfr As Date
   Dim seGFR As String  'eg > 90
   Dim eGFR_Abnormal_count As Integer
   Dim beGFR_Abnormal As Boolean
   Dim eGFR_Reference_Range As String
   Dim bEgfr_Not_recorded As Boolean
   Dim microalbuminuria_count As Integer
   Dim num_had_dacc_but_overdue As Integer
   Dim num_without_dacc As Integer
   Dim num_had_dacc_in_date As Integer
   Dim num_hba1c_older_than_6months As Integer
   Dim num_egfr_not_recorded As Integer
   Dim num_eye_review_not_recorded As Integer
   Dim num_eye_review_Overdue As Integer
   Dim num_bp_not_recorded As Integer
   Dim num_weight_not_recorded As Integer
   Dim sql As String
   Dim eyereview As Result
   Dim eyedocuments As Collection 
   Dim eyereview_date As Date
   Dim beyereview_overdue As Boolean
   Dim beyereview_not_recorded As Boolean
   Dim LatestBPs As Collection
   Dim bBP_Overdue As Boolean
   Dim bBP_Not_Recorded As Boolean
   Dim Last_BP_Date As Date
   Dim num_bp_overdue As Integer
   Dim LatestWeights As Collection
   Dim bweight_overdue As Boolean
   Dim bweight_not_recorded As Boolean
   Dim last_weight_date As Date
   Dim num_weight_overdue As Integer
   Dim LatestLDLCholesterols As Collection
   Dim LDlCholesterol As Variant
   Dim bLDLcholesterol_overdue As Boolean
   Dim bLDLCholesterol_abnormal As Boolean
   Dim bLDLCholesterol_not_at_target As Boolean
   Dim LDLCholesterol_Reference_Range As String
   Dim last_ldlcholesterol_date As Date
   Dim num_ldlcholesterol_overdue As Integer
   Dim num_ldlcholesterol_never_measured As Integer
   Dim num_ldlcholesterol_not_at_target As Integer 
   Dim ldl_upper_range As Variant
   Dim data As Collection
   
   Inc Application.Busy
   Gui_Clear()
   LessThan6 = 0 
   Between6and7 = 0
   Between7and7point5 = 0  
   Between7point5and8 = 0
   Over8 = 0
   Patients_Not_Diabetic = modResearchDBI.Patients_Not_Diabetic_Get()
   modResearchDBI.Patients_With_Hba1c_Tempory_Table_Create()                  'create the table we will use
   bTempory_Table_Exists = True
   patients = modResearchDBI.Patients_Get_Hba1cs()                            'get unique & latest but unordered and incomplete data
   '----------------------------------------------------------------------------------------------------------
   'this is a bug-fix I've not to date found why some measurements were not auto-parsed when a consult is save
   'so this checks all the patients progress notes and saves back to the back-end FIXME find the bug
   '-----------------------------------------------------------------------------------------------------------
   ' For Each patient In patients
   '    modMeasurementsDBI.Run_Measurement_Fix(modConsultDBI.progressnotes_get(patient!fk_patient), patient!fk_patient)
   ' Next
   egFRs = modResearchDBI.Patients_Get_EgfR()                                 'get egFr's for all these patients.
   eyedocuments = modResearchDBI.Patients_Get_Most_Recent_Eye_Documents()
   LatestBPs = modResearchDBI.Patients_Get_Most_Recent_BP()
   LatestWeights = modResearchDBI.Patients_Get_Most_Recent_Weight()
   LatestLDLCholesterols = modResearchDBI.Patients_Get_LDLCholesterol()
   For Each patient In patients  
      If Not Patients_Not_Diabetic.Exist(patient!fk_patient) Then 
         If patient!value_numeric <= 5.9 Then Inc LessThan6
         If patient!value_numeric >= 6 And patient!value_numeric <= 6.9 Then Inc Between6and7
         If patient!value_numeric >= 7.0 And patient!value_numeric <= 7.4 Then Inc Between7and7point5
         If patient!value_numeric >= 7.5 And patient!value_numeric <= 7.9 Then Inc Between7point5and8
         If patient!value_numeric >= 8.0 Then Inc Over8
         
         If DateDiff(patient!observation_date, Now(), gb.Month) > 6 Then
            Inc num_hba1c_older_than_6months
         Endif
         '---------------------------------
         'get the last DACC for the patient
         '---------------------------------
         bDaccOverdue = False                                                    'default to overdue DACC
         Latex = ""                                                              'default to no Latex
         date_last_dacc = Date(1900, 01, 01)
         RDacc = modResearchDBI.Patient_GetDaysSinceDACC(patient!fk_patient)     'get last DACC days/LaTex
         If RDacc.count Then 
            If Not IsNull(RDacc!days_since_dacc_done) Then
               If RDacc!days_since_dacc_done > 365 Then
                  bDaccOverdue = True  
                  Inc num_had_dacc_but_overdue
               Else
                  Inc num_had_dacc_in_date
               Endif
               latex = RDacc!latex
               date_last_dacc = RDacc!date_completed
            Else
               Inc num_without_dacc 'these are partially completed DACC's
            Endif
         Else
            Inc num_without_dacc
         End If 
         '------------------------------------------------
         'get the last renal function egfr for the patient
         '------------------------------------------------
         bEgfr_Not_recorded = False 
         If Not IsNull(egFRs[patient!fk_patient]) Then
            seGFR = Str(egFRs[patient!fk_patient]!value_numeric) & egFRs[patient!fk_patient]!qualifier
            date_last_egfr = egFRs[patient!fk_patient]!observation_date
            beGFR_Abnormal = False  
            eGFR_Reference_Range = egFRs[patient!fk_patient]!reference_range
            If Not IsNull(egFRs[patient!fk_patient]!abnormal) Then
               If egFRs[patient!fk_patient]!abnormal <> "N" Then 
                  beGFR_Abnormal = True
                  Inc eGFR_Abnormal_count
               End If   
            Endif
         Else
            Inc num_egfr_not_recorded
            bEgfr_Not_recorded = True  
         Endif
         '--------------------------------------------------------------------------
         'get the last ldl cholesterol for the patient, also count how many too high
         '--------------------------------------------------------------------------
         bLDLcholesterol_overdue = True                                      'assume overdue
         bLDLCholesterol_abnormal = False 
         last_ldlcholesterol_date = Date(1900, 01, 01)                                           'default to having no cholesterol
         If Not IsNull(LatestLDLCholesterols[patient!fk_patient]) Then                           'has had cholesterol done at some point
            LDlCholesterol = LatestLDLCholesterols[patient!fk_patient]!value_numeric             ' & LatestLDLCholesterols[patient!fk_patient]!reference_range
            last_ldlcholesterol_date = LatestLDLCholesterols[patient!fk_patient]!observation_date
            If Not (DateDiff(Now, DateAdd(last_ldlcholesterol_date, gb.Year, 1), gb.day) <= 0) Then 'was it over  a year ago?
               bLDLcholesterol_overdue = False                                                     'cholesterol date < 1yr, not overdue
            Else
               bLDLcholesterol_overdue = True                                                      'cholesterol > 1 year = ovedue
               Inc num_ldlcholesterol_overdue
            End If 
            If patient!fk_patient = 90 Then
               Print
            Endif
            'References ranges are a problem, some labs give eg n1-n2 others only the upper eg <n2
            If Not IsNull(LatestLDLCholesterols[patient!fk_patient]!reference_range) Then          'do we have a reference range
               LDLCholesterol_Reference_Range = LatestLDLCholesterols[patient!fk_patient]!reference_range
               If InStr(LDLCholesterol_Reference_Range, "-") Then 
                  ldl_upper_range = Val(Split(LatestLDLCholesterols[patient!fk_patient]!reference_range, "-")[1])
               Else 
                  If Left(LDLCholesterol_Reference_Range, 1) = "<" Then
                     ldl_upper_range = Val(Right(LDLCholesterol_Reference_Range, Len(LDLCholesterol_Reference_Range) - 1))
                  End If
               End If   
            Endif
            
            If Not IsNull(LatestLDLCholesterols[patient!fk_patient]!abnormal) Then                  'if  cholesterol flagged as abnormal
               If LatestLDLCholesterols[patient!fk_patient]!abnormal <> "N" Then                    'some labs put a normal flag in this field
                  If LatestLDLCholesterols[patient!fk_patient]!value_numeric > ldl_upper_range Then   'if over the top
                     bLDLCholesterol_abnormal = True                                                'flag abnormal
                     bLDLCholesterol_not_at_target = True
                     Inc num_ldlcholesterol_not_at_target                                           'incremement the not at target count
                  Else
                     bLDLCholesterol_not_at_target = False
                     bLDLCholesterol_abnormal = False  
                  End If 
               End If   
            Else
               bLDLCholesterol_not_at_target = False  
            Endif
         Else                                                                                        'never had ldl cholesterol measured.
            Inc num_ldlcholesterol_overdue                                                        
            Inc num_ldlcholesterol_never_measured
         Endif
         '---------------------------------------
         'get the last eye review for the patient
         '---------------------------------------
         If Not IsNull(eyedocuments[patient!fk_patient]) Then
            eyereview_date = eyedocuments[patient!fk_patient]!date_created
            beyereview_not_recorded = False  
            If Not (DateDiff(Now, DateAdd(eyedocuments[patient!fk_patient]!date_created, gb.Year, 2), gb.day) <= 0) Then
               beyereview_overdue = False
            Else
               Inc num_eye_review_Overdue
               beyereview_overdue = True
            End If 
         Else
            eyereview_date = Date(1900, 01, 01)
            beyereview_not_recorded = True
            Inc num_eye_review_not_recorded
         Endif
         '-------------------------------------------
         'get the last BP measurement for the patient
         '-------------------------------------------
         If Not IsNull(LatestBPs[patient!fk_patient]) Then
            Last_BP_Date = LatestBPs[patient!fk_patient]!consult_date
            bBP_Not_Recorded = False 
            If Not (DateDiff(Now, DateAdd(LatestBPs[patient!fk_patient]!consult_date, gb.Month, 6), gb.day) <= 0) Then
               bBP_Overdue = False
            Else
               Inc num_bp_overdue
               bBP_Overdue = True
            End If 
         Else
            Inc num_bp_not_recorded
            bBP_Not_Recorded = True
            Last_BP_Date = Date(1900, 01, 01)
         Endif
         '-----------------------------------------------
         'get the last weight measurement for the patient
         '-----------------------------------------------
         If Not IsNull(LatestWeights[patient!fk_patient]) Then             'yes, there is a weight, check if in date
            last_weight_date = LatestWeights[patient!fk_patient]!consult_date
            bweight_not_recorded = False
            If Not (DateDiff(Now, DateAdd(LatestWeights[patient!fk_patient]!consult_date, gb.Month, 6), gb.day) <= 0) Then
               bweight_overdue = False
            Else
               Inc num_weight_overdue
               bweight_overdue = True
            End If 
         Else
            Inc num_weight_not_recorded
            bweight_not_recorded = True
            last_weight_date = Date(1900, 01, 01)
         Endif
         data = New Collection 
         data!fk_patient = patient!fk_patient
         data!fk_person = patient!fk_person
         data!fk_image = patient!fk_image
         data!pk_observations = patient!pk_observations
         data!firstname = patient!firstname
         data!surname = patient!surname
         data!wholename = patient!wholename
         data!birthdate = patient!birthdate
         data!age_numeric = patient!age_numeric
         data!street1 = patient!street1
         data!street2 = patient!street2
         data!suburb = patient!town
         data!postcode = patient!postcode
         data!observation_date = patient!observation_date
         data!value_numeric = patient!value_numeric
         data!date_last_dacc = patient!date_last_dacc
         data!dacc_overdue = patient!dacc_overdue
         data!latex = patient!latex
         data!date_last_egfr = date_last_egfr
         data!last_egfr = seGFR
         data!egfr_abnormal = beGFR_Abnormal
         data!egfr_reference_range = eGFR_Reference_Range
         data!egfr_not_recorded = bEgfr_Not_recorded
         data!date_last_eye_review = eyereview_date
         data!eye_review_overdue = beyereview_overdue
         data!eye_review_not_recorded = beyereview_not_recorded
         data!date_last_bp = Last_BP_Date
         data!bp_overdue = bBP_OverDue
         data!bp_not_recorded = bweight_not_recorded
         data!date_last_weight = last_weight_date
         data!weight_overdue = bweight_overdue
         data!weight_not_recorded = bweight_not_recorded
         data!date_last_ldlcholesterol = last_ldlcholesterol_date
         data!ldl_cholesterol = LDlCholesterol
         data!ldlcholesterol_overdue = bLDLcholesterol_overdue
         data!ldlcholesterol_not_at_target = bLDLCholesterol_not_at_target
         data!ldlcholesterol_reference_range = LDLCholesterol_Reference_Range
         modDBConnect.insert("patients_latest_hba1c", data)
      End If   
   Next
   modDBConnect.CommitTrans()
   
   patients = modDBConnect.exec_query_collection("Select * from patients_latest_hba1c ORDER BY surname asc")
   iTotalDataSetcount = patients.Count
   cvwPatients.Columns[1].text = Str(iTotalDataSetcount) & " patients were found" 
   lblNeverHadDacc.text = Str(num_without_dacc) & " patients (" & As_Percent(num_without_dacc) & ") have no DACC recorded "
   lblEyeReviewOverdue.text = Str(num_eye_review_Overdue) & " patients (" & As_Percent(num_eye_review_Overdue) & ") are overdue an eye examination"
   lblEyesNotRecorded.text = Str(num_eye_review_not_recorded) & " patients (" & As_Percent(num_eye_review_not_recorded) & ") have no record of an eye examination"
   lblDACCOverdue.text = Str(num_had_dacc_but_overdue) & " patients (" & As_Percent(num_had_dacc_but_overdue) & ") have had a previous DACC but are overdue"
   lblDACCCurrent.text = Str(num_had_dacc_in_date) & " patients (" & Str(num_had_dacc_in_date) & "%) have completed a DACC In the Last 12 months"
   lblHBa1cOutOfDate.text = Str(num_hba1c_older_than_6months) & " patients (" & As_Percent(num_hba1c_older_than_6months) & ") last hba1c is older than 6 months"
   lblRenalFunctionNotRecorded.text = Str(num_egfr_not_recorded) & " patients (" & As_Percent(num_egfr_not_recorded) & ") have no renal function recorded"
   lblImpairedRenalFunction.text = Str(eGFR_Abnormal_count) & " patients (" & As_Percent(eGFR_Abnormal_count) & " have impaired renal function"
   lblBPOverdue.text = Str(num_bp_overdue) & " patients (" & As_Percent(num_bp_overdue) & ") have not had BP measured in last 6 months"
   lblBPNotRecorded.text = Str(num_bp_not_recorded) & " patients (" & As_Percent(num_bp_not_recorded) & ") have not had BP recorded at all"
   lblWeightOverdue.text = Str(num_weight_overdue) & " patients (" & As_Percent(num_weight_overdue) & ") have not had weight measured in last 6 months"
   lblWeightNotRecorded.text = Str(num_weight_not_recorded) & " patients (" & As_Percent(num_weight_not_recorded) & ") have no record of a weight measurement"
   lblCholesterolNeverMeasured.text = Str(num_ldlcholesterol_never_measured) & " patients (" & As_Percent(num_ldlcholesterol_never_measured) & ") have no ldl cholesterol result" 
   lblCholesterolOverdue.text = Str(num_ldlcholesterol_overdue) & " patients (" & As_Percent(num_ldlcholesterol_overdue) & ") are overdue a cholesterol test" 
   lblCholesterolNotAtTarget.text = Str(num_ldlcholesterol_not_at_target) & " patients (" & As_Percent(num_ldlcholesterol_not_at_target) & ") are above target ldl cholesterol levels"
   Make_Pie()
   lblPatientsListHeading.Text = "Listing of all Patients" 
   lblPieGraphHeading.text = "Statistics For Your Diabetic Patients (n=" & Str(iTotalDataSetcount) & ")"
   Dec Application.Busy
   
End

Public Sub mnuDiabeticPatients_Click()
   '-----------------------------------------------------------------
   'Popup the menu over the patient's list, allow cleaning up of data
   '-----------------------------------------------------------------   
   
   Dim x As Integer
   Dim icount As Integer
   Dim sMsg As String
   
   sMsg = "You have selected to alter the status of %number%  %status%.\n\nThey will no longer appear in "
   "the patients list or the clinical record, but will not actually be removed from the database."
   "\n\nAre you sure you wish to proceed?"
   
   Select Case Last.tag
      Case "change font"
         modUtil.Columnview_SetFont(cvwPatients, "Research_" & Me.name)
      Case "left practice"
         '------------------------------------------------
         'create appropriate message for 1 or more patient
         '------------------------------------------------
         cvwPatients.MoveFirst
         For x = 0 To cvwPatients.count - 1
            If cvwPatients.Item.Selected = True Then
               Inc icount 
            End If    
            cvwPatients.MoveNext
         Next
         If icount = 1 Then 
            Message.Title = "Marking a Patient as Left The Practice"
            sMsg = Replace(sMsg, "%number%", "a patient")
            sMsg = Replace(sMsg, "%status%", "who has left the practice")  
         Else If icount > 1 Then
            Message.Title = "Marking Patients as Left The Practice"
            sMsg = Replace(sMsg, "%number%", "a number of patients")
            sMsg = Replace(sMsg, "%status%", "who have left the practice")
         Endif
         If Message.Question(sMsg, "Yes", "No") = 2 Then Return
         '---------------------
         'User wants to proceed
         '---------------------
         modDBConnect.exec_query("drop table patients_latest_hba1c") 
         cvwPatients.MoveFirst
         For x = 0 To cvwPatients.count - 1
            If cvwPatients.Item.Selected = True Then
               patient = patients[cvwPatients.Item.Key]
               modDBConnect.update("clerical.data_patients", Null, ["pk": patient!fk_patient, "active_status": "i"])
               modDBConnect.CommitTrans()
            Endif
            cvwPatients.MoveNext 
         Next    
         Reload()
         cvwPatients_Refresh()
         If cvwPatients.count Then
            cvwPatients.MoveFirst
            cvwPatients.Item.Selected = True
            cvwPatients.SetFocus()
         Else
            'bumma none are left 
            Gui_Clear()
         Endif
      Case "exclude giving reason"
            '------------------------------------------------------------------------------
            'allowable reasons could be e.g 'dying of cancer' or 'demented in nursing home'
            '------------------------------------------------------------------------------
      Case "mark not diabetic"
         modDBConnect.exec_query("drop table patients_latest_hba1c")   
         modResearchDBI.Patient_Not_Diabetic_Mark(patient!fk_patient)
         modDBConnect.CommitTrans()
         Reload()
         cvwPatients_Refresh()
         If cvwPatients.count Then
            cvwPatients.MoveFirst
            cvwPatients.Item.Selected = True
            cvwPatients.SetFocus()
         Else
            'bumma none are left 
            Gui_Clear()
         Endif
      Case "mark deceased"
         '------------------------------------------------
         'create appropriate message for 1 or more patient
         '------------------------------------------------
         cvwPatients.MoveFirst
         For x = 0 To cvwPatients.count - 1
            If cvwPatients.Item.Selected = True Then
               Inc icount 
            End If    
            cvwPatients.MoveNext
         Next
         If icount = 1 Then 
            Message.Title = "Marking a Patient as Deceased"
            sMsg = Replace(sMsg, "%number%", "a patient")
            sMsg = Replace(sMsg, "%status%", "who has died")  
         Else If icount > 1 Then
            Message.Title = "Marking Patients as Deceased"
            sMsg = Replace(sMsg, "%number%", "a number of patients")
            sMsg = Replace(sMsg, "%status%", "who have died")
         Endif
         If Message.Question(sMsg, "Yes", "No") = 2 Then Return
         '---------------------
         'User wants to proceed
         '---------------------
         modDBConnect.exec_query("drop table patients_latest_hba1c")  'will have to be refreshed
         cvwPatients.MoveFirst
         For x = 0 To cvwPatients.count - 1
            If cvwPatients.Item.Selected = True Then
               patient = patients[cvwPatients.Item.Key]
               
               modDBConnect.update("contacts.data_persons", Null, ["pk": patient!fk_person, "deceased": True]) 'mark person deceased
               modDBConnect.CommitTrans()
            Endif
            cvwPatients.MoveNext
         Next    
         Reload()
         cvwPatients_Refresh()
         If cvwPatients.count Then
            cvwPatients.MoveFirst
            cvwPatients.Item.Selected = True
            cvwPatients.SetFocus()
         Else
            'bumma none are left 
            Gui_Clear()
         Endif
   End Select 
   
End

Public Sub cvwPatients_Menu()
   
   If Last.count Then mnuDiabeticPatients.popup()
   
End

Public Sub cvwPatients_Select()
   
   cvwPatients.MoveCurrent()
   patient = patients[cvwPatients.Item.Key]
   Patient_Display()
   
End

Public Sub Patient_Display_Summary()
   
   Consult_CheckStarted()
   pbPatientPicture.Picture = Null
   lblPatientName.text = modStrings.CapitaliseWord(patient!wholename)
   tlPatientDetails.text = ""
   tlPatientDetails.text &= "Age:" & patient!age_display & "<BR>"
   tlPatientDetails.text &= "DOB:" & patient!birthdate & "<BR>"
   tlPatientDetails.text &= Patient!street1 & "<BR>"
   If Not IsNull(patient!street2) Then
      tlPatientDetails.text &= Patient!street2 & "<BR>"
   Endif
   tlPatientDetails.text &= Patient!town & " " & patient!postcode & "<BR><BR>"
   Try pbPatientPicture.Picture = modContactsDBI.Person_Get_Photo(patient!fk_image)
   medications = modPrescribingDBI.Prescriptions_Written(patient!fk_patient)
   modPrescribingDBI.Prescriptions_Fill_Columnview(cvwMedications, medications, medications_display_by, True) 'active meds
   cvwMedications.Height = (cvwMedications.count + 1) * 18
   '------------------------------------------------------------------
   'Display information about the DACC (diabetes annual cycle of care)
   '------------------------------------------------------------------
   If Year(patient!date_last_dacc) = 1900 Then
      lbllastDacc.text = "NONE RECORDED"
   Else
      Try lbllastDacc.text = Format(patient!date_last_dacc, "dd/mm/yyyy")
      If patient!dacc_overdue = True Then
         lbllastDacc.text &= "  [OVERDUE]"
      Endif
   End If   
   Try lblLastHba1c.text = Format(patient!observation_date, "dd/mm/yyyy")
   Try lblLasteGFR.text = Format(patient!date_last_egfr, "dd/mm/yyyy") & " " & patient!egfr_reference_range
   '---------------------------------------------------------------------------
   'The eye check can never be null but if never done is arbitrarily 01/01/1900
   '---------------------------------------------------------------------------
   If patient!date_last_eye_review <> "01/01/1900" Then 
      lblLastEyecheck.text = Format(patient!date_last_eye_review, "dd/mm/yyyy")
      If patient!eye_review_overdue = True Then
         lblLastEyecheck.text &= " [OVERDUE]"
      Endif
   Else
      lblLastEyecheck.text = "No letter found"
   End If   
   '---------------------------------------------------------------------------
   'The last BP date can never be null but if never done is arbitrarily 01/01/1900
   '---------------------------------------------------------------------------
   If patient!date_last_bp <> "01/01/1900" Then 
      lbllastBP.text = Format(patient!date_last_bp, "dd/mm/yyyy")
      If patient!bp_overdue = True Then
         lbllastBP.text &= " [OVERDUE]"
      Endif
   Else
      lbllastBP.text = "No BP found'"
   End If  
   '----------------------------------------------------------------------------------
   'The last weight date can never be null but if never done is arbitrarily 01/01/1900
   '----------------------------------------------------------------------------------
   If patient!date_last_weight <> "01/01/1900" Then 
      lblLastWeight.text = Format(patient!date_last_weight, "dd/mm/yyyy")
      If patient!weight_overdue = True Then
         lblLastWeight.text &= " [OVERDUE]"
      Endif
   Else
      lblLastWeight.text = "No Weight found'"
   End If  
   '------------------------
   'Finally graph everything
   '------------------------
   Hba1c_ShowGraph
   Lipids_ShowGraph
   EGFR_ShowGraph
   Weight_ShowGraph
   BP_ShowGraph '
   
End

Public Sub Patient_Display_Notes()
   
   Consult_CheckStarted()
   If IsNull(form_Progress_Notes) Then 
      form_Progress_Notes = New FProgressNotesViewer(VBox_ProgressNotes)
   End If   
   form_Progress_Notes.Init(currentconsult, modProgressNotes.AllProgressNotes_Construct_Html(currentconsult)) 
   
End

Public Sub Patient_Display_DACC()
   
   ' If IsNull(form_pdf) Then
   '    With form_pdf = New FPdf(VBox_DACC)
   '       .visible = True  
   '       .btZoomIn.Visible = True
   '       .btZoomOut.Visible = True  
   '       .BtPrint.Visible = True 
   '    End With
   ' Endif
   If IsNull(form_dacc) Then
      With form_dacc = New FDacc(VBox_DACC)
         .Init(currentconsult)
      End With
   Endif
   '  Dacc_View_Last()
   
End

Public Sub Patient_Recalls()
   
   Consult_CheckStarted()
   If IsNull(form_recalls) Then
      form_recalls = New FRecalls(Vbox_Recalls)
      form_recalls.Init(CurrentConsult)
   Endif
   
End

Public Sub Patient_StaffTasks()
   
   Consult_CheckStarted()
   If IsNull(form_tasks) Then
      form_tasks = New FStaffTasks(Vbox_Staff_Tasks)
      form_tasks.Init()
   Endif
   
End

Public Sub Patient_Display()
   '-----------------------------------------------------
   'display a summary of the patients diabetes parameters
   '-----------------------------------------------------
   'If bShowPatient = False Then Return    'if user is multi-selecting from the list
   
   lblPatientName.text = patient!title & " " & patient!wholename
   currentconsult = Null
   Select Case iCurrentTab
      Case cTab_Summary
         Patient_Display_Summary
      Case cTab_ProgressNotes
         Patient_Display_Notes
      Case cTab_LastDACC
         Patient_Recalls()
   End Select
   Return 
   
End

Public Sub Weight_ShowGraph()
   
   Dim R As Result
   
   With Form_Graph_Weight
      .Set_fk_Patient(Patient!fk_patient)
      .Graph(Null, "", "weight")
   End With 
   
End

Public Sub Lipids_ShowGraph()
   
   Dim R As Result
   
   With Form_Graph_Lipids
      .Set_fk_Patient(Patient!fk_patient)
      .Graph(Null, const.Loinc_TotalCholesterol, "HDL")
   End With 
   
End

Public Sub EGFR_ShowGraph()
   
   With Form_Graph_EGFR
      .Set_fk_Patient(Patient!fk_patient)
      .Graph(Null, const.Loinc_EGFR, "eGFR")
   End With 
   
End

Public Sub BP_ShowGraph()
   
   With Form_Graph_BP
      .Set_fk_Patient(Patient!fk_patient)
      .Graph(Null, "", "blood pressure")
   End With 
   
End

Public Sub Hba1c_ShowGraph()
   
   Dim R As Result
   ' 
   ' sql = "Select * from documents.vwObservations WHERE loinc =$$"
   ' sql &= const.Loinc_HBA1c & "$$ AND fk_patient =" & patient!fk_patient & " order by observation_date Desc "
   ' Hba1c_Results = modDBConnect.exec_query(sql) 
   ' cvwValues.Clear()
   ' For Each Hba1c_Results
   '    cvwValues.Add(Hba1c_Results!pk, 0)
   '    cvwValues[Hba1c_Results!pk][0] = Format(Hba1c_Results!observation_date, "dd/mm/yyyy")
   '    cvwValues[Hba1c_Results!pk][1] = Hba1c_Results!value_numeric
   ' Next
   ' lblDate.width = cvwValues.Columns[0].Width
   ' lblDate.text = "Date"
   ' lblValues.text = "HbA1c"
   ' lblValues.width = cvwValues.columns[1].width
   With Form_Graph_HBA1c
      .Set_fk_Patient(Patient!fk_patient)
      .Graph(Null, const.Loinc_HBA1c, "Hba1c")
   End With 
   
End

Public Sub rbSortAscendingDescending_Click()
   
   sSortOrder = Last.tag   
   Patients_Reload()   
   
End

Public Sub cmbsortBy_Click()
   
   Patients_Reload()   
   
End

Public Sub Patients_Reload()
   
   Dim fieldname As String
   Dim sql As String
   Dim iNoPatient_In_Range As Integer
   
   iDisplay_Subset = cDisplaySubset_Any
   Select Case cmbSortBy.text
      Case "hba1c"
         fieldname = "value_numeric"
      Case "age"
         fieldname = "age_numeric"
      Case "surname"
         fieldname = "surname"
   End Select
   sql = "Select * from patients_latest_hba1c "
   Select Case cmbHba1cRange.Text
      Case "<6.0"
         iNoPatient_In_Range = LessThan6
         sql &= "WHERE value_numeric <= 5.9 "
      Case "6.0-7.0"
         iNoPatient_In_Range = Between6and7
         sql &= " WHERE value_numeric between 6 and 6.9 "
      Case "7.0-7.5"
         iNoPatient_In_Range = Between7and7point5 
         sql &= " WHERE value_numeric between 7 and 7.4 "
      Case "7.5-8.0"  
         iNoPatient_In_Range = Between7point5and8 
         sql &= " WHERE value_numeric between 7.5 and 7.9 "
      Case ">8.0" 
         sql &= " WHERE value_numeric >= 8"
   End Select
   If Trim(txtSearch.text) <> "" Then
      If InStr(sql, "value_numeric") Then
         sql &= " AND "
      Else
         sql &= " WHERE "
      End If   
      sql &= "surname ILIKE $$%" & Trim(txtSearch.text) & "%$$"
   Endif
   Patients = modDBConnect.exec_query_collection(sql & " order by " & fieldname & " " & sSortOrder)
   cvwPatients_Refresh()   
   If cvwPatients.count And Trim(txtSearch.text) = "" Or cvwPatients.count = 1 Then
      cvwPatients.MoveFirst
      cvwPatients.Item.Selected = True
      cvwPatients.SetFocus()
      cvwPatients.Columns[0].text = Str(patients.count)
      If cmbHba1cRange.text <> "all" Then
         cvwPatients.Columns[0].text &= " (" & As_Percent(iNoPatient_In_Range) & ")"
      End If    
   Endif
   
End

Public Sub cmbHba1cRange_Click()
   
   Patients_Reload()   
   
End

Private Sub Gui_Clear()
   
   cvwPatients.Clear 
   cvwValues.Clear
   Form_Graph_Hba1c.Measurement_Graph_Picture_Clear
   Form_Graph_Lipids.Measurement_Graph_Picture_Clear
   Form_Graph_EGfr.Measurement_Graph_Picture_Clear
   Form_Graph_BP.Measurement_Graph_Picture_Clear
   Form_Graph_Weight.Measurement_Graph_Picture_Clear
   
End

Public Sub txtSearch_KeyRelease()
   '----------------------------------------------------
   'Except in hospital won't have many patients
   'so not using timer - if you want one I'll put one in
   '----------------------------------------------------
   
   If Not IsNull(patients) Then Patients_Reload()
   
End

Public Sub txtSearch_GotFocus()
   
   txtSearch.text = ""  
   
End

Public Sub txtSearch_KeyPress()
   
   If key.code = key.down Then
      If cvwPatients.count Then
         cvwPatients.MoveFirst
         cvwPatients.Item.Selected = True
         cvwPatients.SetFocus()
      End If
   Endif
   
End

Private Sub Settings_Load()
   
   Hsplit1.Layout = Settings["Research_" & Me.name & "/Hsplit1.layout"] 
   Hsplit_PatientData.Layout = Settings["Research_" & Me.name & "/Hsplit_PatientData.layout"] 
   iGraphFontSize = Settings["Research_" & Me.name & "/graph_font.size", 3]
   Try cvwPatients.Font = Font[Settings["Research_" & Me.name & "/cvwPatients.font"]]
   
End

Public Sub Settings_Save()
   
   Settings["Research_" & Me.name & "/Hsplit1.layout"] = Hsplit1.Layout
   Settings["Research_" & Me.name & "/graph_font.size"] = iGraphFontSize
   Settings["Research_" & Me.name & "/Hsplit_PatientData.layout"] = Hsplit_PatientData.Layout
   
End

Public Sub SpinBox1_Change()
   
   If bExit Then Return  
   SpinBox1.Refresh()
   SpinBox1.Enabled = False
   Inc Application.Busy
   Form_Graph_Hba1c.slGraphFontSize.value = Last.value
   Form_Graph_Lipids.slGraphFontSize.value = Last.value
   Form_Graph_eGFR.slGraphFontSize.value = Last.value
   Dec Application.Busy
   SpinBox1.Enabled = True 
   
End

Public Sub rbMedications_Click()
   
   medications_display_by = Last.tag 
   modPrescribingDBI.Prescriptions_Fill_Columnview(cvwMedications, medications, medications_display_by, True) 'active meds
   
End

Public Function As_Percent(num As Integer) As String
   
   Return Str(Round((num / iTotalDataSetcount) * 100)) & "%"
   
End

Public Sub Make_Pie()
   '----------------------------------------------- 
   'Display the pie chart + labels with percentages
   '-----------------------------------------------
   
   Dim tp As Integer = patients.Count
   
   Titles = New String[]
   values.Clear()
   AddSlice("<6 (" & As_Percent(LessThan6) & ")", LessThan6)
   AddSlice("6-7 (" & As_Percent(Between6and7) & ")", Between6and7)
   AddSlice("7-7.5 (" & As_Percent(Between7and7point5) & ")", Between7and7point5)
   AddSlice("7.5-8 (" & As_Percent(Between7point5and8) & ")", Between7point5and8)
   AddSlice(">8 (" & As_Percent(Over8) & ")", Over8)
   
End

Public Sub AddSlice(title As String, value As Float)
   
   If titles.Length >= 11 Then Error.Raise("Not enough colours for the pie chart")
   total_value += value
   values.Add(value)
   titles.Add(title)
   
End

Public Sub DrawingArea1_Draw()
   
   Dim i As Integer
   Dim x2 As Integer = CInt(DrawingArea1.Width * 0.5) ' half: i.e. the midpoint
   Dim y2 As Integer = CInt(DrawingArea1.Height * 0.5)
   Dim mid_radians As Float ' middle of pie: angle for point of text
   Dim text_x As Integer
   Dim text_y As Integer  
   Dim values_thus_far As Float = 0
   
   Draw.FillStyle = Fill.Solid
   
   For i = 0 To values.Max
      Draw.FillColor = colours[i]
      Draw.Ellipse(x2 / 2, y2 / 2, x2, y2, values_thus_far / total_value * 2 * Pi, (values_thus_far + values[i]) / total_value * 2 * Pi)
      mid_radians = (values_thus_far + (values[i] / 2)) / total_value * 2 * Pi
      text_x = CInt(x2 + (Cos(mid_radians) * x2 * 0.6))
      text_y = CInt(y2 - (Sin(mid_radians) * y2 * 0.6))
      If text_x < x2 Then ' it's to the left of the midline
         text_x -= Draw.TextWidth(titles[i])
      Endif
      Draw.Text(titles[i], text_x, text_y)
      values_thus_far += values[i]
   Next
   
End

Public Sub Dacc_View_Last()
   '--------------------------------------------------------
   'If it exists, then view the latex as pdf of the last DCC
   '-------------------------------------------------------- 
   
   If Not IsNull(patient!latex) Then
      With form_pdf
         .Load_PDF(modPrinting.Latex_To_PDF(patient!latex))
      End With
   Else
      TabStrip1.index = cTab_Summary
      Message.Info("Missing latex definition, sorry........")   
   Endif  
   
End

Public Sub Consult_CheckStarted()
   
   If IsNull(currentconsult) Then  
      currentconsult = New CConsult(Patient)           'Each patient has a new consult 
      'there is no commit until the consult type is set
      'currentconsult.Set(const.ConsultType_NotesPatientNotPresent, "diabetes audit") 
   End If
   
End

Public Sub TabStrip1_Click()
   
   iCurrentTab = Last.index
   Select Case Last.Index
      Case cTab_Summary
         Patient_Display_Summary()
      Case cTab_LastDACC
         Patient_Display_DACC
      Case cTab_ProgressNotes
         Patient_Display_Notes()      
      Case cTab_CarePlanning
      Case cTab_StaffTasks 
         Patient_StaffTasks  
      Case cTab_Recalls
         Patient_Recalls 
   End Select
   
End

Public Sub tbStats_Click()
   '---------------------------------------------------------------------
   'Filter diabetic cycle of care patients as "due", "overdue", "in date"
   '---------------------------------------------------------------------
   
   iDisplay_Subset = cDisplaySubset_Any
   
   Select Case Last.tag 
      Case "no egfr"
         patients = modResearchDBI.Patients_egfr_not_recorded()
      Case "cholesterol never measured"
         patients = modResearchDBI.Patients_No_Cholesterol_Recorded()
      Case "cholesterol overdue"
         patients = modResearchDBI.Patients_OverDue_LDLCholesterol()
         iDisplay_Subset = cDisplaytype_Cholesterol_Overdue
      Case "cholesterol not at target"
         patients = modResearchDBI.Patients_LDLCholesterol_NotAtTarget()
         iDisplay_Subset = cDisplaytype_Cholesterol_Not_At_Target
      Case "no weight recorded"
         patients = modResearchDBI.Patients_weight_not_recorded()
      Case "weight overdue"
         patients = modResearchDBI.Patients_OverDue_Weight()
      Case "no bp recorded"
         patients = modResearchDBI.Patients_bp_not_recorded()
      Case "bp overdue"
         patients = modResearchDBI.Patients_OverDue_BP()
      Case "dacc overdue", "dacc in date", "no dacc"
         patients = modResearchDBI.Diabetic_Cycle_Of_Care_Status(Last.tag)  
         lblPatientsListHeading.Text = "Patients DACC Status:" & Last.tag  
      Case "overdue hba1c"
         patients = modResearchDBI.Patients_Last_HBA1C_Over_6Months()
         lblPatientsListHeading.Text = "Patients not had HbA1C in last 6 months"
      Case "renal impairment"
         lblPatientsListHeading.Text = "Patients with Renal Impairment"
      Case "eye review not recorded"
         patients = modResearchDBI.Patients_Eye_Review_Not_Recorded()
      Case "eye review"
         patients = modResearchDBI.Patients_OverDue_eye_Examination()
         lblPatientsListHeading.Text = "Patients Overdue an Eye Examination"
   End Select
   
   cvwPatients.Columns[1].text = Str(patients.count) & "/" & Str(iTotalDataSetcount) & " " & Last.tag
   cvwPatients_Refresh() 
   If cvwPatients.count Then
      cvwPatients.MoveFirst
      cvwPatients.Item.Selected = True
   Endif
   
End

Public Sub Form_KeyPress()
   
   Dim ctrlDown As Boolean
   
   ' If CtrlDown = key.Control Then
   '   bShowPatient = False 
   '   Print "the ctrl key is down"
   ' Else
   '    bShowPatient = True  
   ' Endif
   
End

Public Sub cvwMedications_Leave()
   
   Last.ScrollBar = Scroll.None
   
End

Public Sub cvwMedications_Enter()
   
   If Last.count Then Last.ScrollBar = Scroll.Horizontal
   
End

Public Sub Analyse()  
   '-------------------------------------------------------------------------------
   'Do the basic data analysis
   'As this can take some time, it is not automatically started when the form loads
   'otherwise the gui just seems to freeze
   '-------------------------------------------------------------------------------
   
   If bTempory_Table_Exists Then
      Try modDBConnect.exec_query("drop table patients_latest_hba1c") 
      modDBConnect.CommitTrans()   'fixme could just truncate it
   End If   
   Reload()                                        'create temporary table, load all data work out stats
   cvwPatients_Refresh()                           'fill the columnview to display the data
   If cvwPatients.count Then
      cvwPatients.MoveFirst
      cvwPatients.Item.Selected = True             'trigger cvwPatients_Select()
      cvwPatients.SetFocus()                       'which shows the first patient's data summary   
   Endif  
   DrawingArea1.Refresh()
   
End

Public Sub Generate_Report()
   
   modUtil.NotImplemented("Generating the report")
   
End

Public Sub Save_Snapshot()
   
   modUtil.NotImplemented("Generating the report")
   
End

Public Sub tbEditArea_Click()
   
   Select Case Last.tag
      Case "analyse"
         Analyse()
      Case "generate report"
         Generate_Report()
      Case "save snapshot"
         Save_Snapshot()
   End Select
   
End
' sql = "create temporary table patients_latest_hba1c ("
'  sql &= "fk_patient integer, fk_person integer, fk_image integer, pk_observations integer,"
'  sql &= "\"surname\" text,\"firstname\" text,\"wholename\" text,\"birthdate\" date, age_display integer,"
'  sql &= "\"street1\" text,\"street2\" text,\"suburb\" text,\"postcode\" text,"
'  sql &= "\"observation_date\" date,value_numeric numeric,"
'  sql &= "date_last_dacc date, dacc_overdue boolean, \"latex\" text,"
'  sql &= "date_last_egfr date, egfr_abnormal boolean,\"egfr_reference_range\" text,"
'  sql &= "date_last_eye_review date, eye_review_overdue boolean,"
'  sql &= "date_last_bp date, bp_overdue boolean,"
'  sql &= "date_last_weight date, weight_overdue boolean,"
'  sql &= "date_last_ldlcholesterol date, ldlcholesterol_overdue boolean,ldlcholesterol_not_at_target boolean,"
'  sql &= "ldl_cholesterol numeric,\"ldlcholesterol_reference_range\" text,"
'  sql &= "pk serial)"
