' Gambas class file

' Copyright (C) 2008-2016 Dr. Richard Terry, Dr Ian Haywood
' This program is free software: you can redistribute it and/or modify
' it under the terms of the GNU General Public License as published by
' the Free Software Foundation, either version 3 of the License, or
' (at your option) any later version.

' This program is distributed in the hope that it will be useful,
' but WITHOUT ANY WARRANTY; without even the implied warranty of
' MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
' GNU General Public License for more details.

' You should have received a copy of the GNU General Public License
' along with this program.  If not, see <http://www.gnu.org/licenses/>.
'-------------------------------------------------------------------
' PURPOSE   To allow user to research database using queries
'-------------------------------------------------------------------
Private query As CRow
Private queries As Collection
Private loaded As String = "sql"

Private picture_file As String

Public Sub rbText_Click()
   
   If IsNull(query) Then Return
   
   query[loaded] = taText.Text
   If Last.Tag = "SQL" Then
      loaded = "sql"
      taText.Text = query!sql
   Endif
   If Last.tag = "Gnuplot" Then
      loaded = "gnuplot"
      taText.Text = query!gnuplot
   Endif
   
End

Public Sub Form_Open()
   
   Settings_Load()
   
   queries = modUtil.Copy_Collection_Keyed_Sequentially(modResearchDBI.Load_Queries())
   rbSQL.Value = True
   Refresh() 
   query = New CRow
   query!sql = Null
   query!gnuplot = Null
   
End

Private Sub Refresh()
   
   Dim i As CRow
   
   lbQueries.Clear()
   For Each i In queries
      lbQueries.Add(i!title)
   Next
   
End

Public Sub bNew_Click()
   
   query = New CRow
   query!gnuplot = Null
   query!sql = Null
   taText.Clear()
   taText.SetFocus
   txtTitle.Clear()
   
End

Public Sub tbRunQuery_Click()
   
   If IsNull(query) Then Return
   If Trim(taText.Text) = "" Then Return
   ' If Trim(txtTitle.text) = "" Then 
   '    Message.Info("Please enter a title for this query")
   '    txtTitle.SetFocus
   '    Return
   ' End If     
   query[loaded] = taText.Text
   query!title = txtTitle.Text
   ' query.Save("research.queries", "pk")
   ' modDBConnect.CommitTrans()
   Run_Query()
   
End

Private Sub Run_Query()
   
   Dim pic As Picture
   Dim res As Result
   Dim gnuplot As String
   Dim i As Integer
   Dim j As Integer
   
   If Not IsNull(picture_file) Then Try Kill picture_file
   res = modDBConnect.exec_query(query!sql)
   Print res
   If IsNull(query!gnuplot) Then
      gnuplot = ""
   Else
      gnuplot = query!gnuplot
      gnuplot = "box"
   Endif
   If gnuplot = "" Or Lower$(gnuplot) = "grid" Or Lower$(gnuplot) = "data" Then
      PictureBox1.Visible = False
      picture_file = Null
      tbSaveImage.Text = "Save Data"
      Grid.Visible = True
      Grid.Columns.Count = res.Fields.Count
      Grid.Rows.Count = res.Count
      For i = 0 To res.Fields.Count - 1
         Grid.Columns[i].Title = res.Fields[i].Name
      Next
      i = 0
      For Each res
         For j = 0 To res.Fields.Count - 1
            Grid[i, j].Text = Str$(res[j])
         Next
         Inc i
      Next
   Else
      Grid.visible = False
      PictureBox1.Visible = True
      tbSaveImage.Text = "Save Image"
      If Lower$(gnuplot) = "guess" Then
         ' try some default gnuplots
         If res.Fields.Count = 2 Then
            If res.Fields[0].Type = db.String Then
               If res.Count < 10 Then
                  ' pie chart time!
                  gnuplot = modPlot.PieChart(res)
               Else 
                  ' bar chart
                  gnuplot = modPlot.Gnuplot_BoxGraph(res)
               Endif
            Else
               gnuplot = modPlot.ScatterPlot(res)
            Endif
         Else
            If res.Fields[0].Type = db.String Then
               gnuplot = modPlot.Gnuplot_BoxGraph(res)
            Endif
         Endif
      Endif
      If Lower$(gnuplot) = "scatter" Then gnuplot = modPlot.ScatterPlot(res)
      If Lower$(gnuplot) = "pie" Then gnuplot = modPlot.PieChart(res)
      If Lower$(gnuplot) = "box" Then gnuplot = modPlot.Gnuplot_Boxgraph(res)
      If Lower(gnuplot) = "line" Then gnuplot = modPlot.Gnuplot_LineGraph(res)
      Debug gnuplot
      picture_file = modPlot.Gnuplot_Run(PictureBox1.Width, PictureBox1.Height, gnuplot, res)
      pic = Picture.Load(picture_file)
      PictureBox1.Background = Color.White
      PictureBox1.Picture = pic
   Endif
   
Catch
   Message.Error(Error.Text)
   
End

Public Sub lbQueries_Click()
   
   Dim i As CRow
   
   For Each i In queries
      If i!title = lbQueries.Text Then
         query = i
      Endif
   Next
   loaded = "sql"
   taText.Text = query!sql
   rbSQL.Value = True
   txtTitle.Text = query!title
   
End

Public Sub Settings_Load()
   
   HSplit1.Layout = Settings["FQueries/HSplit1.Layout", modUtilGUI.HSplit([1, 4])]
   '  VSplit1.Layout = Settings["FQueries/VSplit1.Layout", modUtilGUI.VSplit([1, 6])]
   
End

Public Sub HSplit1_Resize()
   
   Settings["FQueries/HSplit1.Layout"] = HSplit1.Layout
   
End

Public Sub VSplit1_Resize()
   
   '  Settings["FQueries/VSplit1.Layout"] = VSplit1.Layout
   
End

Public Sub tbSaveImage_Click()
   
   Dim res As Result
   Dim f As File
   Dim s As String
   Dim i As Integer
   
   If IsNull(picture_file) Then
      If Not IsNull(query!sql) Then
         Inc Application.Busy
         res = modDBConnect.exec_query(query!sql)
         Dec Application.Busy
         Dialog.Filter = ["*.tsv", "Tab Separated Values"]
         Dialog.SaveFile()
         f = Open Dialog.Path For Write Create
         For i = 0 To res.Fields.Count - 1
            Print #f, res.Fields[i].Name;
            If i < res.Fields.Count - 1 Then Print #f, "\t";
         Next
         Print #f
         For Each res
            For i = 0 To res.Fields.Count - 1
               s = Str$(res[i])
               s = Replace$(s, "\t", " ")
               s = Replace$(s, "\n", " ")
               s = Replace$(s, "\r", "")
               Print #f, s;
               If i < res.Fields.Count - 1 Then Print #f, "\t";
            Next
            Print #f
         Next
         Close #f      
      Endif
   Else
      Dialog.Filter = ["*.png", "PNG Files"]
      Dialog.SaveFile()
      If Not IsNull(Dialog.Path) Then Try Move picture_file To Dialog.Path
   Endif
   
End

Public Sub Grid_Click()
   
End

Public Sub taText_LostFocus()
   
   Last.BackGround = Color.White
   
End

Public Sub taText_GotFocus()
   
   Last.BackGround = Color.rgb(95, 255, 175)
   
End

Public Sub txtTitle_LostFocus()
   
   Last.BackGround = Color.White
   
End

Public Sub txtTitle_GotFocus()
   
   Last.BackGround = Color.rgb(95, 255, 175)
   
End
