' Gambas class file

' Copyright (C) 2008-2016 Dr. Richard Terry, Dr Ian Haywood
' This program is free software: you can redistribute it and/or modify
' it under the terms of the GNU General Public License as published by
' the Free Software Foundation, either version 3 of the License, or
' (at your option) any later version.

' This program is distributed in the hope that it will be useful,
' but WITHOUT ANY WARRANTY; without even the implied warranty of
' MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
' GNU General Public License for more details.

' You should have received a copy of the GNU General Public License
' along with this program.  If not, see <http://www.gnu.org/licenses/>.
'-------------------------------------------------------------------
' PURPOSE   To allow user to research database using queries
'-------------------------------------------------------------------
Private query As CRow
Private queries As Collection

Private picture_file As String

Public Sub Form_Open()
   
   Settings_Load()
   
  Refresh() 
  query = New CRow
  query!sql = Null
  
   
End

Private Sub Refresh()
   
  Dim i As CRow
  
  queries = modUtil.Copy_Collection_Keyed_Sequentially(modResearchDBI.Load_Queries())
  lbQueries.Clear()
  For Each i In queries
     lbQueries.Add(i!title)
  Next
   
   lbQueries.Clear()
   For Each i In queries
      lbQueries.Add(i!title)
   Next
   
End

Public Sub bNew_Click()
   
   query = New CRow
   query!sql = Null
   taText.Clear()
   taText.SetFocus
   tbTitle.Clear()
   
End

Private Sub Parse_Sql(sql As String, ByRef actual_sql As String, ByRef gnuplot As String, ByRef embedded_title As String)

  Dim lines As String[]
  Dim l As String

   lines = Split(sql, "\n")
   actual_sql = ""
   gnuplot = ""
   For Each l In lines
      If l Begins "title:" Then
         embedded_title = Trim$(Right$(l, -6))
      Else
         If l Begins "gnuplot:" Then
            gnuplot &= Right$(l, -8)
         Else
            actual_sql &= l 
         Endif
      Endif
   Next

End


Public Sub bSave_Click()

   Dim title_changed As Boolean = False
   Dim actual_sql As String
   Dim embedded_title As String
   Dim embedded_gnuplot As String
   
   If IsNull(query) Then Return
   query!sql = taText.Text
   Parse_Sql(query!sql, ByRef actual_sql, ByRef embedded_gnuplot, ByRef embedded_title)
   If Not IsNull(embedded_title) And tbTitle.Text = "" Then
      tbTitle.Text = embedded_title
   Endif
   If query!title <> tbTitle.Text Then
      title_changed = True
      query!title = tbTitle.Text
   Endif
   query.Save("research.queries", "pk")
   modDBConnect.CommitTrans()
   If title_changed Then 
      Refresh()
   Endif
   Run_Query(actual_sql, embedded_gnuplot)
   
End

Private Sub Run_Query(sql As String, gnuplot As String)
   
   Dim pic As Picture
   Dim res As Result
   Dim gnuplot2 As String
   Dim i As Integer
   Dim j As Integer
   
   If Not IsNull(picture_file) Then Try Kill picture_file
   res = modDBConnect.exec_query(sql)
   gnuplot2 = Lower$(Trim$(gnuplot))
   If IsNull(gnuplot) Or If gnuplot = "" Or If gnuplot2 = "grid" Or If gnuplot2 = "data" Then
      PictureBox1.Visible = False
      picture_file = Null
      tbSaveImage.Text = "Save Data"
      Grid.Visible = True
      Grid.Columns.Count = res.Fields.Count
      Grid.Rows.Count = res.Count
      For i = 0 To res.Fields.Count - 1
         Grid.Columns[i].Title = res.Fields[i].Name
      Next
      i = 0
      For Each res
         For j = 0 To res.Fields.Count - 1
            Grid[i, j].Text = Str$(res[j])
         Next
         Inc i
      Next
   Else
     Grid.visible = False
     PictureBox1.Visible = True
     tbSaveImage.Text = "Save Image"
     If gnuplot2 = "guess" Then
       ' try some default gnuplots
       If res.Fields.Count = 2 Then
         If res.Fields[0].Type = db.String Then
           If res.Count < 10 Then
             ' pie chart time!
             gnuplot = modPlot.PieChart(res)
           Else 
             ' bar chart
             gnuplot = modPlot.Gnuplot_BoxGraph(res)
           Endif
         Else
            If res.Fields[0].Type = db.String Then
               gnuplot = modPlot.Gnuplot_BoxGraph(res)
            Endif
         Endif
       Else
         If res.Fields[0].Type = db.String Then
            gnuplot = modPlot.Gnuplot_BoxGraph(res)
         Endif
       Endif
     Endif
     If gnuplot2 Begins "scatter" Then gnuplot = modPlot.ScatterPlot(res)
     If gnuplot2 Begins "pie" Then gnuplot = modPlot.PieChart(res)
     If gnuplot2 Begins "box" Or If gnuplot2 Begins "bar" Then gnuplot = modPlot.Gnuplot_Boxgraph(res)
     Debug gnuplot
     picture_file = modPlot.Gnuplot_Run(PictureBox1.Width, PictureBox1.Height, gnuplot, res)
     pic = Picture.Load(picture_file)
     PictureBox1.Background = Color.White
     PictureBox1.Picture = pic
   Endif
   
Catch
   Message.Error(Error.Text)
   
End

Public Sub lbQueries_Click()
   
   Dim i As CRow
   
   For Each i In queries
      If i!title = lbQueries.Text Then
         query = i
      Endif
   Next
   taText.Text = query!sql
   tbTitle.Text = query!title
   
End

Public Sub Settings_Load()
   
   HSplit1.Layout = Settings["FQueries/HSplit1.Layout", modUtilGUI.HSplit([1, 4])]
   '  VSplit1.Layout = Settings["FQueries/VSplit1.Layout", modUtilGUI.VSplit([1, 6])]
   
End

Public Sub HSplit1_Resize()
   
   Settings["FQueries/HSplit1.Layout"] = HSplit1.Layout
   
End

Public Sub VSplit1_Resize()
   
   '  Settings["FQueries/VSplit1.Layout"] = VSplit1.Layout
   
End

Public Sub tbSaveImage_Click()
   
   Dim res As Result
   Dim f As File
   Dim s As String
   Dim i As Integer
   
   If IsNull(picture_file) Then
      If Not IsNull(query!sql) Then
         Inc Application.Busy
         res = modDBConnect.exec_query(query!sql)
         Dec Application.Busy
         Dialog.Filter = ["*.tsv", "Tab Separated Values"]
         Dialog.SaveFile()
         f = Open Dialog.Path For Write Create
         For i = 0 To res.Fields.Count - 1
            Print #f, res.Fields[i].Name;
            If i < res.Fields.Count - 1 Then Print #f, "\t";
         Next
         Print #f
         For Each res
            For i = 0 To res.Fields.Count - 1
               s = Str$(res[i])
               s = Replace$(s, "\t", " ")
               s = Replace$(s, "\n", " ")
               s = Replace$(s, "\r", "")
               Print #f, s;
               If i < res.Fields.Count - 1 Then Print #f, "\t";
            Next
            Print #f
         Next
         Close #f      
      Endif
   Else
      Dialog.Filter = ["*.png", "PNG Files"]
      Dialog.SaveFile()
      If Not IsNull(Dialog.Path) Then Try Move picture_file To Dialog.Path
   Endif
   
End

Public Sub Grid_Click()
   
End

Public Sub taText_LostFocus()
   
   Last.BackGround = Color.White
   
End

Public Sub taText_GotFocus()
   
   Last.BackGround = Color.rgb(95, 255, 175)
   
End

Public Sub tbTitle_LostFocus()
   
   Last.BackGround = Color.White
   
End

Public Sub tbTitle_GotFocus()
   
   Last.BackGround = Color.rgb(95, 255, 175)
   
End
