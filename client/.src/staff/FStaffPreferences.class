' Gambas class file

' Copyright (C) 2008-2012 Dr. Richard Terry
' This program is free software: you can redistribute it and/or modify
' it under the terms of the GNU General Public License as published by
' the Free Software Foundation, either version 3 of the License, or
' (at your option) any later version.

' This program is distributed in the hope that it will be useful,
' but WITHOUT ANY WARRANTY; without even the implied warranty of
' MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
' GNU General Public License for more details.
' You should have received a copy of the GNU General Public License
' along with this program.  If not, see <http://www.gnu.org/licenses/>.
'----------------------------------------------------------------------
'STAFF PREFERENCES
'
'PURPOSE       Allow staff members to determine how they want the program to behave
'              Tabs are loaded according to role (see admin.lu_roles)
'              For example a doctor who orders requests gets to configure how this behaves
'              but the request config won't be seen by secretaries
'
'                
'BUGS           Probably plenty please email me rterry@pacific.net.au
'             
'TODO          
'             

'-----------------------------------------------------------------------------------------------' 
Private Form_RequestPreferences As FStaffPreferencesRequests
Private Form_PrescribingPreferences As FStaffPreferencesPrescribing
Private Form_FontsColors As FStaffPreferencesFontsColors
Private Form_GeneralPreferences As FStaffPreferencesClinical
Private Form_Help As FHtmlViewer
Private Form_Workcenter As FStaffPreferencesWorkCentre
Private Form_LibraryPreferences As FStaffPreferencesLibrary
Private help_dir As String
Private cIconPanel_Section_Clinical As Integer
Private cIconPanel_Section_Fonts As Integer 
Private cIconPanel_Section_Library As Integer 
Private cIconPanel_Section_Prescribing As Integer 
Private cIconPanel_Section_Requests As Integer 
Private cIconPanel_Section_Workcenter As Integer 
Private iCurrent_Section As Integer
Private Obj As Object 
Private TabIcons As New Collection
Private bIniting As Boolean
Private bStopArrange As Boolean

Static Private form_staffpreferences As FStaffPreferences

Static Public Sub set_heading(hc As Integer)
   
   form_staffpreferences.heading_set(hc)
   
End

Public Sub heading_set(hc As Integer)
   
   hbox_heading.Background = hc 
   lblSectionHeading.Background = hc
   
End


Public Sub Init()
   '------------------------------------
   'Build the iconpanel according to role
   '------------------------------------
   
   Dim role As String
   Dim tabnames As New String[]
   Dim x As Integer
   
   biniting = True
   help_dir = Application.Path &/ "help/"
   form_staffpreferences = Me
   
   With Form_Help = New FHtmlViewer(Vbox_Help)
      .tbWebBrowserZoomIn.Visible = True
      .tbWebBrowserZoomOut.Visible = True  
      Try .WebView1.Zoom = Settings["Staff Preferences/Help_Viewer_Zoom"]
   End With
   
   tabicons.add("tabicons/24/desktop_2424.png", "Clinical")
   tabicons.add("tabicons/22/font22x22.png", "Fonts & Colors")
   tabicons.add("tabicons/32/centigrade_pills3232.png", "Prescribe")
   tabicons.add("tabicons/22/emblem-office.png", "Library")
   tabicons.add("tabicons/16/bloodtube16x16.png", "Requests")
   tabicons.add("tabicons/22/scanner.png", "Workcentre")
   
   '------------------------
   'What is the staff role?
   '-----------------------
   role = modDBConnect.currentUser!role
   Select Case role 
      Case "doctor", "doctor - assistant", "sysadmin"
         tabnames.Add("Clinical")
         tabnames.Add("Fonts & Colors")
         tabnames.Add("Prescribe")
         tabnames.Add("Library")
         tabnames.Add("Requests")
         tabnames.Add("Workcentre")
         
         cIconPanel_Section_Clinical = 0
         cIconPanel_Section_Fonts = 1
         cIconPanel_Section_Prescribing = 2
         cIconPanel_Section_Library = 3
         cIconPanel_Section_Requests = 4
         cIconPanel_Section_Workcenter = 5
      Case "secretary", "practice manager"  
         tabnames.Add("Fonts & Colors")
         cIconPanel_Section_Fonts = 0
   End Select
   '------------------------------------------------------
   'Ok, have the particular tabs for each role, build them
   '-------------------------------------------------------
   IconPanel2.count = tabnames.Count 
   For x = 0 To tabnames.Count - 1
      With iconpanel2
         .index = x
         .text = tabnames[x]
         Try .picture = Picture.Load(tabicons[tabnames[x]])
         IconPanel_Add_Section(.text)
      End With 
   Next
   Try Settings_Load()
   IconPanel2.index = 0
   IconPanel2_Click()
   bIniting = False  
End

Public Sub IconPanel2_Click()
   '------------------------------------------------------
   'User has clicked on one of the icons in the icon panel
   'Try and save any un-saved data on the previous tab
   '------------------------------------------------------

    If Not biniting Then 
       If Object.Call(obj, "Save") Then 'save anything changed on the previous panel
          Stop Event
          Return   
       End If     
    End If  
 
   Select Case IconPanel2.index
      Case cIconPanel_Section_Fonts
         obj = Form_FontsColors
         Show_Help("user-preferences-fonts-and-colors.html")
      Case cIconPanel_Section_Clinical
         Show_Help("staff-preferences---clinical.html")
         obj = Form_GeneralPreferences
      Case cIconPanel_Section_Prescribing
         Show_Help("staff-preferences---prescribing.html")
         obj = Form_GeneralPreferences
      Case cIconPanel_Section_Workcenter
         Show_Help("staff-preferences---workcenter.html")
         obj = Form_Workcenter
      Case cIconPanel_Section_Library
         Show_Help("staff-preferences---library.html")
         obj = Form_LibraryPreferences
      Case cIconPanel_Section_Requests
         Show_Help("user-request-provider-defaults.html")
         obj = Form_RequestPreferences
   End Select
   
End

Public Sub IconPanel_Add_Section(sSection As String) 
   '-------------------------------
   'Adds a section to the iconpanel
   '-------------------------------   

   Select Case sSection
      Case "Fonts & Colors"
         With Form_FontsColors = New FStaffPreferencesFontsColors(IconPanel2) 'VBox_FontsColors)
            .Init() 
            .Background = Me.Background
         End With       
      Case "Clinical"
         With Form_GeneralPreferences = New FStaffPreferencesClinical(IconPanel2)
            .Init()
            .Background = Me.Background
         End With
      Case "Prescribe"
         With Form_PrescribingPreferences = New FStaffPreferencesPrescribing(IconPanel2)
            .Init() 
            .Background = Me.Background
         End With 
      Case "Workcentre"
         With Form_Workcenter = New FStaffPreferencesWorkCentre(IconPanel2)
            .Init()
            .Background = Me.Background
         End With
      Case "Library"
         With Form_LibraryPreferences = New FStaffPreferencesLibrary(IconPanel2)
            .Init()
            .Background = Me.Background
         End With
      Case "Requests"
         With Form_RequestPreferences = New FStaffPreferencesRequests(IconPanel2)
            .Set_Staff_member(modDBConnect.currentUser!fk_staff)
            .Init()
            .Background = Me.Background
            .RequestsDefaults_Refresh()
         End With
   End Select
   
End

Public Sub Show_Help(sHtmlFile As String)
   
   Form_Help.tag = ""
   modEditAreaHelpers.Help_Show(sHtmlFile, Form_Help)
   
End Sub

Private Sub Settings_Load()
   
   Try Form_Help.WebView1.Zoom = Settings["Staff Preferences/Help_Viewer_Zoom"]
   Try HSplit1.Layout = Settings["Staff Preferences/HSplit1.Layout"] 
   
End

Public Sub Settings_save()
   
   Form_RequestPreferences.Settings_save()
   Form_FontsColors.Settings_save()
   Form_GeneralPreferences.Settings_Save()
   Form_PrescribingPreferences.Settings_Save()
   Settings["Staff Preferences/Help_Viewer_Zoom"] = Form_Help.WebView1.Zoom
   Settings["Staff Preferences/HSplit1.Layout"] = HSplit1.Layout 
 '  Try Object.Call(obj, "Save")  'save any unsaved data

End




