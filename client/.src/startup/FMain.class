' Gambas class file

' Copyright (C) 2008-2011, 2010 Dr. Richard Terry

' This program is free software: you can redistribute it and/or modify
' it under the terms of the GNU General Public License as published by
' the Free Software Foundation, either version 3 of the License, or
' (at your option) any later version.

' This program is distributed in the hope that it will be useful,
' but WITHOUT ANY WARRANTY; without even the implied warranty of
' MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
' GNU General Public License for more details.

' You should have received a copy of the GNU General Public License
' along with this program.  If not, see <http://www.gnu.org/licenses/>.

' ------------------------------------------------------------------------------
' FMain is called by modStartup, if logon successful, and 'runs the whole show'
'------------------------------------------------------------------------------
Private SBClinical As CRoundButton
Public CurrentForm As Object
Public sidebarfont As Font
Private Btn_Section_Tasks As CCoolButton
Private btnSection_Clinical As CCoolButton
Private btnSection_Inbox As CCoolButton
Private btnSection_WorkCentre As CCoolButton
Private btnSection_DrugRef As CCoolButton
Private btnSection_Library As CCoolButton
Private btnSection_Clerical As CCoolButton
Private btnSection_Appointments As CCoolButton
Private btnSection_Research As CCoolButton
Private BtnSection_Admin As CCoolButton
Private BtnSection_Patients As CCoolButton
Private btnSection_Contacts As CCoolButton
Private btnSection_Help As CCoolButton
Private btnSection_Workstation As CCoolButton
Private btnSection_Scanning As CCoolButton
Private btnSection_Preferences As CCoolButton
Private obs As Observer
Public pnlDayBook As FDayBook               'nerve centre to organise your day
Public pnlClinical As FClinical              'The main clinical panel
Public pnlInbox As FInbox                    'The document inbox
Public pnlDrugReference As FDrugReference    'Drug drug bible - currently not implemented
Public pnlLibrary As FLibrary                'The Library for staff documents or patient handouts
Public pnlClerical As FClerical              'Clerical tasks such as scanning, managing recalls
Public pnlResearch As FResearch              'All research stuff
Public pnlAdmin As FAdmin                    'Admin = tasks such as importing/exporting data
Public pnlContacts As FContacts              'Contacts database for organisations and people who are not patients or staff
Public pnlPatients As FContactsPatients      'Patient details, currently only demographic.
Public pnlStaff As FStaff                    'Staff module - including basic staff preferences           
Public pnlHelp As FHelp                      'The main program help browser, sort of like a chm file
Public pnlPopUpHelp As FHtmlViewer           'overlays the program with a html viewer - used by any form except FClinical
Public pnlSetupWorkStation As FRoomSetup             'Setup just for this Workstation
Public pnlStaffPreferences As FStaffPreferences
Public pnlScanning As FScanning
Public pnlWorkCentre As FWorkCentre
Public pnlAppointments As FMakeAppointments
Public pnlDayList As FDayList

Private Form_Surveillance As FSurveillance

Private Const POS_BUTTON As Integer = 0
Private HEIGHT_BUTTON As Integer = Desktop.Scale * 4
Private bLogonshowing As Boolean = False

Static Private form_main As FMain

Private Const admin_user_error As String = "The admin user is not allowed to access this module."
" Please create an ordinary user in the Staff tab and log in as them" 

Public Sub Form_Open()
   
   Init() 
   
End

Public Function Get_SideBar_Font() As String
   
   Return form_main.sidebarfont.ToString()
   
End

Static Public Sub Set_SideBar_Fonts(theFont As Font)
   
   form_main.mnuSideBar_FontSet(theFont)
   
End

Public Sub Set_PopUp_Help(sPath As String)
   
   Try form_main.pnlPopUpHelp.WebBrowser1.path = sPath
   If Error Then
      form_main.pnlPopUpHelp = New FHtmlViewer(VBoxMain)
      form_main.pnlPopUpHelp.WebView1.Url = sPath
   End If
   
End

Public Sub Show_PopUp_Help(flag As Boolean)
   
   If flag Then 
      Try CurrentForm.Hide()
      Try form_main.pnlPopUpHelp.Show()
      If Error Then
         form_main.pnlHelp1 = New PnlHelp1(VBoxMain)
         pnlHelp1.Show()
      End If
   Else
      form_main.pnlPopUpHelp.Close()
      CurrentForm.Show()
   End If
   
End

Public Sub Init()
   
   form_main = Me                               'Allow access from other modules.
   If IsNull(modDBConnect.currentUser) Then
      Me.Title = "EasyGP (on " & modDBConnect.DbHost & ":" & modDBConnect.DbName & ") " 
   Else
      Me.Title = UCase(modDBConnect.currentUser!title & " " & modDBConnect.currentUser!wholename) & "  - EasyGP (on " & modDBConnect.DbHost & ":" & modDBConnect.DbName & ") " 
   Endif
   Try Settings_Load()
   Create_SideBar_Buttons()                     'create sidebar buttons
   Expose_SideBar_Buttons()                     'according to role
   FMain.Background = VboxSideBar.Background
   const.init()                                 'init some constant string arrays, global preferences
   If IsNull(modDBConnect.currentUser) Then     'If user who logged on <> valid user
      btnSection_Admin_Click()                  'throw them into contacts
   End If
   StaffTasks_For_Staff_Member_ReLoad()
   Appointments_Display()                       'display appointments for current user
   Wait
   
End

Public Sub Appointments_Display()
   '---------------------------------------------------------------
   'Display the appointments - this form has a timer on it and will
   'update the appointments regularly
   '---------------------------------------------------------------
    With pnlDayList = New FDayList(Vbox_DayList)
      .Gridview1.Font.Size = 6
      .Init(modDBConnect.currentUser, Now, modDBConnect.currentUser!fk_clinic, modAppointmentsDBI.Appointment_icons_get())
      .Gridview1.Columns[0].width = 35
      .Gridview1.Columns[1].width = 0 
      .Background = Color.RGB(65, 82, 132)
      .lblDate.Foreground = Color.white
      .lblDate.text = Format(Now, gb.MediumDate)
      .Padding = 0
   End With
   
   
End

Public Sub run_image_fix()
   
   'only for me.
   If Message.Question("Run Image Fix?", "Yes", "No") = 2 Then Return
   modConsultDBI.Run_Image_Fix()
   
End

Public Sub Create_SideBar_Buttons()
   '-------------------------------------------------------
   'Creates the buttons on the side bar, sets the fonts etc
   '-------------------------------------------------------
   
   Dim sidebarfontString As String
   Dim W As Integer
   Dim R As Result
   Dim image As New Image
   Dim pic As Picture
   
   sidebarfontString = sidebarfont.ToString()
   W = VboxSideBar.width
   btnSection_Clinical = New CCoolButton(VBox6, 0, POS_BUTTON, W, HEIGHT_BUTTON, ("Clinical Desktop"), "", sidebarfontString) As "btnSection_Clinical"
   btnSection_Clinical.Foreground = Color.SelectedForeground
   btnSection_Clinical.Visible = False
   
   btnSection_WorkCentre = New CCoolButton(VBox6, 0, POS_BUTTON + HEIGHT_BUTTON + VBox6.height, W, HEIGHT_BUTTON, ("Work Centre"), "", sidebarfontstring) As "btnSection_WorkCentre"
   btnSection_WorkCentre.Foreground = Color.SelectedForeground
   btnSection_WorkCentre.visible = False
   
   btnSection_Clerical = New CCoolButton(VBox6, 0, POS_BUTTON + HEIGHT_BUTTON * 7, W, HEIGHT_BUTTON, ("Clerical"), "", sidebarfontstring) As "btnSection_Clerical"
   btnSection_Clerical.Foreground = Color.SelectedForeground
   btnSection_Clerical.Visible = False 
   
   btnSection_Appointments = New CCoolButton(VBox6, 0, POS_BUTTON + HEIGHT_BUTTON * 7, W, HEIGHT_BUTTON, ("Appointments"), "", sidebarfontstring) As "btnSection_Appointments"
   btnSection_Appointments.Foreground = Color.SelectedForeground
   btnSection_Appointments.Visible = False 
   
   btnSection_Scanning = New CCoolButton(VBox6, 0, POS_BUTTON + HEIGHT_BUTTON * 7, W, HEIGHT_BUTTON, ("Scanning"), "", sidebarfontstring) As "btnSection_Scanning"
   btnSection_Scanning.Foreground = Color.SelectedForeground
   btnSection_Scanning.Visible = False 
   
   ' btnSection_Research = New CCoolButton(Vbox4, 0, POS_BUTTON + HEIGHT_BUTTON * 3, W, HEIGHT_BUTTON, ("Research"), "icons/20/calipers2020.png", sidebarfontstring) As "btnSection_Research"
   ' btnSection_Research.Foreground = Color.SelectedForeground
   ' btnSection_Research.visible = False 
   
   btnSection_Admin = New CCoolButton(VBox6, 0, POS_BUTTON + HEIGHT_BUTTON * 3, W, HEIGHT_BUTTON, ("Administrator"), "", sidebarfontstring) As "btnSection_Admin" 
   btnSection_Admin.Foreground = Color.SelectedForeground
   btnSection_Admin.Visible = False
   
   btnSection_Patients = New CCoolButton(VBox6, 0, POS_BUTTON + HEIGHT_BUTTON * 3, W, HEIGHT_BUTTON, ("Patients"), "", sidebarfontstring) As "btnSection_Patients" 
   btnSection_Patients.Foreground = Color.SelectedForeground
   btnSection_Patients.Visible = False 
   
   btnSection_Contacts = New CCoolButton(VBox6, 0, POS_BUTTON + HEIGHT_BUTTON * 4, W, HEIGHT_BUTTON, ("Contacts"), "", sidebarfontstring) As "btnSection_Contacts"
   btnSection_Contacts.Foreground = Color.SelectedForeground
   btnSection_Contacts.Visible = False 
   
   Btn_Section_Tasks = New CCoolButton(VBox6, 0, POS_BUTTON, W, HEIGHT_BUTTON, ("My Tasks"), "", sidebarfontString, "") As "Btn_Section_Tasks"
   Btn_Section_Tasks.Foreground = Color.SelectedForeground
   Btn_Section_Tasks.Visible = False  
   
   btnSection_Workstation = New CCoolButton(VBox6, 0, POS_BUTTON, W, HEIGHT_BUTTON, ("Setup Room"), "", sidebarfontString) As "btnSection_Workstation"
   BtnSection_Workstation.Foreground = Color.SelectedForeground
   BtnSection_Workstation.Visible = False  
   
   btnSection_Preferences = New CCoolButton(VBox6, 0, POS_BUTTON, W, HEIGHT_BUTTON, ("Preferences"), "", sidebarfontString) As "btnSection_Preferences"
   btnSection_Preferences.Foreground = Color.SelectedForeground
   btnSection_Preferences.Visible = False  
   
   btnSection_DrugRef = New CCoolButton(VBox6, 0, POS_BUTTON + HEIGHT_BUTTON * 2, W, HEIGHT_BUTTON, ("Drugs"), "", sidebarfontstring) As "btnSection_DrugRef"
   btnSection_DrugRef.Foreground = Color.SelectedForeground
   btnSection_DrugRef.Visible = False  
   
   btnSection_Library = New CCoolButton(VBox6, 0, POS_BUTTON + HEIGHT_BUTTON * 5, W, HEIGHT_BUTTON, ("Library"), "", sidebarfontstring) As "btnSection_Library"
   btnSection_Library.Foreground = Color.SelectedForeground
   btnSection_Library.Visible = False  
   
   btnSection_Help = New CCoolButton(VBox6, 0, POS_BUTTON + HEIGHT_BUTTON * 7, W, HEIGHT_BUTTON, ("Help"), "", sidebarfontstring) As "btnSection_Help"
   btnSection_Help.Foreground = Color.SelectedForeground
   btnSection_Help.visible = False 
   '   new_buttons  'don't remove I'm experimenting with new buttons, these are hidden anyway
   
End

' Public Sub new_buttons()
'    
'    SBClinical = New CRoundButton(VBox6, 0, 0, VBox6.Width, 32, "Clinical")
'    SBClinical = New CRoundButton(VBox6, 0, 45, VBox6.Width, 32, "Work Centre")  
'    SBClinical = New CRoundButton(VBox6, 0, 90, VBox6.Width, 32, "Clerical")  
'    SBClinical = New CRoundButton(VBox6, 0, 135, VBox6.Width, 32, "Administrator")  
'    SBClinical = New CRoundButton(VBox6, 0, 180, VBox6.Width, 32, "Setup Room")  
'    SBClinical = New CRoundButton(VBox6, 0, 225, VBox6.Width, 32, "Preferences")  
'    SBClinical = New CRoundButton(VBox6, 0, 270, VBox6.Width, 32, "Drugs")  
'    SBClinical = New CRoundButton(VBox6, 0, 315, VBox6.Width, 32, "Library")  
'    SBClinical = New CRoundButton(VBox6, 0, 350, VBox6.Width, 32, "Help")  
'    
'    
' End

Public Sub Expose_SideBar_Buttons()
   '---------------------------------------------------------------------
   'Setup easyGp according to role.
   'This is just for proof of concept, now need to debate with ian/horst
   'about making roles, access permissions more sophisticated
   '--------------------------------------------------------------------
   
   Dim r As String
   
   If IsNull(modDBConnect.currentUser) Then
      r = "sysadmin"
   Else
      r = modDBConnect.currentUser!role
   Endif
   Select Case r
      Case "doctor", "practice manager", "doctor - assistant", "sysadmin"
         btnSection_Clinical.Visible = True
         '  btnSection_Inbox.Visible = True
         btnSection_WorkCentre.visible = True 
         btnSection_Appointments.Visible = True  
         btnSection_DrugRef.Visible = True
         btnSection_Library.Visible = True
         btnSection_Clerical.Visible = True
         btnSection_Patients.visible = True  
         btnSection_Contacts.Visible = True  
         BtnSection_Workstation.Visible = True  
         btnSection_Preferences.Visible = True  
         btnSection_Help.Visible = True
         btnSection_Admin.Visible = True 'fixme remove me so only admin can see
         btnSection_Clinical_Click()
         
      Case "secretary"
         btnSection_Library.Visible = True
         btnSection_Clerical.Visible = True
         btnSection_Appointments.Visible = True  
         Btn_Section_Tasks.Visible = True
         btnSection_Help.Visible = True
         btnSection_Preferences.Visible = True  
         Vbox_DayList.Visible = False  
   End Select
   
End

Public Sub Btn_Section_Tasks_Click()
   '----------------------------------------------------------------------------------------
   'Inbox:  everything which comes into/is generated by the clinic which could be considered 
   'a document ends up here - we use this term loosely, to mean eg a scanned bit of paper,
   'an email, a patients result, a staff memo
   '----------------------------------------------------------------------------------------
   
   If IsNull(modDBConnect.currentUser) Then
      Message.Error(admin_user_error)
      Return
   Endif
   If Not Document_Dirs_Check_Exist() Then Return 
   
   Try CurrentForm.Hide()
   Try pnlDayBook.Show()
   If Error Then
      pnlDayBook = New FDayBook(VBoxMain)
      pnlDayBook.Show()
   End If
   CurrentForm = pnlDayBook
   
End

Public Sub btnSection_Clinical_Click()
   '---------------------------------------------------------------------------
   'Access the clinical section, ie where patient data is entered and displayed
   'Clinical needs some things to be present before we can use it
   '1) A user with valid access rights
   '2) A default coding system
   '---------------------------------------------------------------------------
   
   Dim sMsg As String    
   
   If IsNull(modDBConnect.currentUser) Then
      Message.Error(admin_user_error)
      Return
   Endif
   If Not modCodingDBI.Coding_System_Check_Default_Exists() Then
      sMsg = "You must set a default coding system before using the clinical section.\n\n"
      "This must be set by someone with administrator privileges and can be \n"
      "done by selecting the Admin tab, and then the import data/coding system data tabs"
      Message.Info(sMsg)
      Return
   End If
   Try CurrentForm.Hide()
   If IsNull(pnlClinical) Then pnlClinical = New FClinical(VBoxMain)
   pnlClinical.Show()
   CurrentForm = pnlClinical
   
End

Public Sub btnSection_Preferences_Click()
   '-----------------------------------------------
   'Shown the chm style help viewer for EasyGP help
   '-----------------------------------------------
   
   Try form_main.pnlStaffPreferences.Close()
   Try CurrentForm.Hide()
   Try pnlStaffPreferences.Show()
   If Error Then
      pnlStaffPreferences = New FStaffPreferences(VBoxMain)
      pnlStaffPreferences.lblSectionHeading.Background = VboxSideBar.Background
      pnlStaffPreferences.Init()
      pnlStaffPreferences.Show()
   End If
   CurrentForm = pnlStaffPreferences
   
End

Public Sub btnSection_WorkCentre_Click()
   '----------------------------------------------------------------------------------------
   'Inbox:  everything which comes into/is generated by the clinic which could be considered 
   'a document ends up here - we use this term loosely, to mean eg a scanned bit of paper,
   'an email, a patients result, a staff memo
   '----------------------------------------------------------------------------------------
   
   If IsNull(modDBConnect.currentUser) Then
      Message.Error(admin_user_error)
      Return
   Endif
   Try CurrentForm.Hide()
   Try pnlWorkCentre.Show()
   If Error Then
      With pnlWorkCentre = New FWorkCentre(VBoxMain)
         .HBox_Heading.Background = VboxSideBar.Background
         .lblSectionHeading.Background = VboxSideBar.Background
         .Show() 
      End With 
   End If
   CurrentForm = pnlWorkCentre
   
End

Public Sub btnSection_DrugRef_Click()
   '--------------------------------------------------------------------------------------
   'The 'drug bible' - currently not implemented, though I've written the code for MIMS it
   'can't be put into open source. The gui exists though
   '--------------------------------------------------------------------------------------
   
   Try CurrentForm.Hide()
   Try pnlDrugReference.Show()
   If Error Then
      With pnlDrugReference = New FDrugReference(VBoxMain)
         .HBox_Heading.Background = VboxSideBar.Background
         .lblSectionHeading.Background = VboxSideBar.Background
         .Show 
      End With
   End If
   CurrentForm = pnlDrugReference
   
End

Public Sub btnSection_Library_Click()
   '----------------------------------------------------------------------------
   'Repository of all things of knowlege - documents can be html/images/pdfs
   'the libary mirrors the library tree  on the HDD as designated in preferences
   'FIXME - IMPLEMENT LIBRARY DIRECTORY IN PREFERENCES
   '----------------------------------------------------------------------------
   
   Try CurrentForm.Hide()
   Try pnlLibrary.Show()
   If Error Then
      With pnlLibrary = New FLibrary(VBoxMain)
         .HBox_Heading.Background = VboxSideBar.Background
         .lblSectionHeading.Background = VboxSideBar.Background
         .Show
      End With
   End If
   CurrentForm = pnlLibrary
   
End

Public Sub btnSection_Clerical_Click()
   '------------------------------------------------------------------------------
   'Selects the clerical tasks section - e.g to manage recall, monitor staff tasks
   'allocate scanned documents
   '------------------------------------------------------------------------------
   
   If IsNull(modDBConnect.currentUser) Then
      Message.Error(admin_user_error)
      Return
   Endif
   If Not Document_Dirs_Check_Exist() Then Return 
   
   Try CurrentForm.Hide()
   Try pnlClerical.Show()
   If Error Then
      With pnlClerical = New FClerical(VBoxMain)
         .HBox_Heading.Background = VboxSideBar.Background
         .lblSectionHeading.Background = VboxSideBar.Background
         .Show() 
      End With
   End If
   CurrentForm = pnlClerical
   
End

Public Sub btnSection_Research_Click()
   '-------------------------------------
   'Do all research here when implemented
   '-------------------------------------
   
   Try CurrentForm.Hide()
   Try pnlResearch.Show()
   If Error Then
      pnlResearch = New FResearch(VBoxMain)
      pnlResearch.Show()
   End If
   CurrentForm = pnlResearch
   
End

Public Sub BtnSection_Workstation_Click()
   '-------------------------------------
   'Do all research here when implemented
   '-------------------------------------
   
   Try CurrentForm.Hide()
   Try pnlSetupWorkStation.Show()
   If Error Then
      With pnlSetupWorkStation = New FRoomSetup(VBoxMain)
         .HBox_Heading.Background = VboxSideBar.Background
         .lblSectionHeading.Background = VboxSideBar.Background
         .Show()
      End With
   End If
   CurrentForm = pnlSetupWorkStation
   
End

Public Sub btnSection_Patients_Click()
   '--------------------------------------------------------------------------
   'Contacts manager for patients
   'NB: this is entirely untested, will be buggy as I don't use it as I import
   'from a legacy database, so needs debugging FIXME- DEBUG PATIENTS
   '--------------------------------------------------------------------------
   
   Try CurrentForm.Hide()
   Try pnlPatients.Show()
   If Error Then
      With pnlPatients = New FContactsPatients(VBoxMain)
         .HBox_Heading.Visible = True 
         .lblSectionHeading.Background = VboxSideBar.Background
         .Show() 
      End With
   End If
   CurrentForm = pnlPatients
   
End

Public Sub BtnSection_Scanning_Click()
   '-------------------------------------
   'Do all research here when implemented
   '-------------------------------------
   
   Try CurrentForm.Hide()
   Try pnlScanning.Show()
   If Error Then
      pnlScanning = New FScanning(VBoxMain)
      pnlScanning.Show()
   End If
   CurrentForm = pnlScanning
   
End

Public Sub btnSection_Admin_Click()
   '-------------------------------------------------------------------------------------------------
   'All admin tasks here - eg setting global preferences, setting hl7 import, library directories etc
   'importing/updating postcodes, exporting or importing clinical records
   '-------------------------------------------------------------------------------------------------
   
   Try CurrentForm.Hide()
   Try pnlAdmin.Show()
   If Error Then
      With pnlAdmin = New FAdmin(VBoxMain)
         .Init()
         .Show()
         .lblSectionHeading.Background = VboxSideBar.Background
         .HBox_Heading.Background = VboxSideBar.Background
         
      End With
   End If
   CurrentForm = pnlAdmin
   
End

Public Sub btnSection_Appointments_Click()
   '-------------------------------------------------------------------------------------------------
   'All admin tasks here - eg setting global preferences, setting hl7 import, library directories etc
   'importing/updating postcodes, exporting or importing clinical records
   '-------------------------------------------------------------------------------------------------
   
   Try CurrentForm.Hide()
   Try pnlAppointments.Show()
   If Error Then
      With pnlAppointments = New FMakeAppointments(VBoxMain)
         .Init()
         .Show()
      End With
   End If
   CurrentForm = pnlAppointments
   
End

Public Sub btnSection_Contacts_Click()
   '----------------------------------------------------------------------------
   'Contacts manager for all organisations/clinics/branches/persons not patients
   '----------------------------------------------------------------------------
   
   Try form_main.pnlPopUpHelp.Close()
   Try CurrentForm.Hide()
   Try pnlContacts.Show()
   If Error Then
      With pnlContacts = New FContacts(vBoxMain)
         .HBox_Heading.Visible = True 
         .lblSectionHeading.Background = VboxSideBar.Background
         .Show() 
      End With
   End If
   CurrentForm = pnlContacts
   
End

' Public Sub btnSection_Staff_Click()
'    '--------------------------------------------------------------
'    'Staff management centre for your organisation and its branches
'    '--------------------------------------------------------------  
'    
'    Try CurrentForm.Hide()
'    Try pnlStaff.Show()
'    If Error Then
'       pnlStaff = New FStaff(VBoxMain)
'       pnlStaff.Show()
'    End If
'    CurrentForm = pnlStaff
'    
' End
' 
Public Sub btnSection_Help_Click()
   '-----------------------------------------------
   'Shown the chm style help viewer for EasyGP help
   '-----------------------------------------------
   
   Try form_main.pnlPopUpHelp.Close()
   Try CurrentForm.Hide()
   Try pnlHelp.Show()
   If Error Then
      With pnlHelp = New FHelp(VBoxMain)
         .lblSectionHeading.Background = VboxSideBar.Background 
         .HBox_Heading.Background = VboxSideBar.Background 
         .Show()
      End With
   End If
   CurrentForm = pnlHelp
   
End

Public Sub Settings_Save()
   '---------------------------------------------------------------------
   'Saves settings of the EasyGP main window, and all splits on all forms
   'Note: these may be 'over-ridden on a per patient basis in FClinical
   '---------------------------------------------------------------------
   
   Settings["Main/Top"] = Me.Top
   Settings["Main/Left"] = Me.Left
   Settings["Main/Height"] = Me.Height 
   Settings["Main/Width"] = Me.Width
   Settings["User Preferences" & modDBConnect.userName() & "/HsplitMain.Layout"] = HSplitMain.Layout
   ' Settings["Main/application.tooltip.font"] = Application.tooltip.Font.ToString()
   Try pnlClinical.Settings_Save()
   Try pnlInbox.Settings_Save()
   Try pnlDrugReference.Settings_Save()
   Try pnlClerical.Settings_Save()
   Try pnlResearch.Form_settings("save")
   Try pnlAdmin.Settings_Save()
   Try pnlContacts.frmOrganisations.Settings_Save()
   Try pnlContacts.frmPersons.Settings_Save()
   Try pnlContacts.frmPatients.Settings_Save()
   Try pnlStaff.Form_settings("save")
   Try pnlStaffPreferences.Settings_Save()
   Try pnlHelp.Settings_Save()
   Try pnlDayBook.Settings_Save()
   Try pnlSetupWorkStation.Settings_Save()

End

Private Sub Settings_Load()
   '------------------------------------------------------------------------------------
   'Each user can have different application and side bar fonts and different main split
   'Try and load these, or if not use defaults
   '------------------------------------------------------------------------------------   

   Dim V As Variant

   Me.Left = Settings["Main/Left", 0] 
   Me.Height = Settings["Main/Height", 771] 
   Me.Width = Settings["Main/Width", 1267] 
   sidebarfont = New Font
   Try Sidebarfont = Font[modAdminDBI.Config_Get_Staff_Member("user_sidebar_font", FMain.Get_Sidebar_Font(), Null, modDBConnect.currentUser!fk_clinic, modDBConnect.currentUser!fk_staff)]
   Try Application.font = Font[modAdminDBI.Config_Get_Staff_Member("user_application_font", Application.font.ToString(), Null, modDBConnect.currentUser!fk_clinic, modDBConnect.currentUser!fk_staff)] 
   Try HSplitMain.Layout = Settings["User Preferences" & modDBConnect.currentUser!logon_name & "/HsplitMain.Layout", [1, 5]] 
   Try form_main.Adjust_Colors(modAdminDBI.Config_Get_Staff_Member("user_background_color", 5737869, Null, modDBConnect.currentUser!fk_clinic, modDBConnect.currentUser!fk_staff))

End

Public Sub Form_Close()
   
   Settings_Save() 'save this form and all child forms settings 
   Quit
   
End

Public Sub mnuSideBar_FontSet(theFont As Font)
   '----------------------------------------------------
   'Expects a font object, sets all buttons to that font
   '----------------------------------------------------
   
   Dim hCtrl As Control
   Dim pnl As Panel
   Dim lbl As Label
   
   For Each hCtrl In VBox6.children 
      If hctrl Is Panel Then
         pnl = hctrl 
         For Each hctrl In pnl.Children
            If hctrl Is Label Then
               lbl = hctrl 
               lbl.font = theFont
               Break 
            Endif
         Next
      Endif
   Next
   
End 

Static Public Sub Adjust_Colors(bgc As Integer)

   form_main.HSplitMain.Background = bgc
   form_main.VboxSideBar.Background = bgc
   form_main.Vbox1.Background = bgc
   form_main.VBox3.Background = bgc
   
End 

Public Function Document_Dirs_Check_Exist() As Boolean
   '----------------------------------------------------------------------
   'Checks the user has set directories for documents.
   'FIXME code at the moment dosn't check this dir is mounted, writable to
   'once ian puts docs back in Db this maynot be used
   '----------------------------------------------------------------------
   
   Dim sMsg As String 
   
   If Not Exist(modAdminDBI.Config_Get("document_scanning_directory", "/var/easygp/scanning/")) Then 
      sMsg = "You do not seem to have set the default directories where you place scanned documents.\n\n"
      sMsg &= "Please go to the Admin section and select the General Preferences tab, and enter the appropriate directories, before continuing."
      Message.Info(sMsg)
      Return False   
   End If
   Return True
   
End

Public Sub StaffTasks_For_Staff_Member_ReLoad()
   '--------------------------------------------------------------------
   'Reloads and fills columnview of tasks just for a single staff member
   'Each task = 1 component of the overall task which could have several
   'staff members involved
   '--------------------------------------------------------------------
   
   Dim component As Collection
   Dim TasksComponents As Collection 
   Dim routine As Integer
   Dim moderate As Integer
   Dim urgent As Integer
   Dim sSTring As String 
   
   If IsNull(modDBConnect.currentUser) Then Return 
   Inc Application.Busy
   
   TasksComponents = modDayBookDBI.Staff_Tasks_Get(modDBConnect.currentUser!fk_staff, 0, True, 0, const.cTaskCompletionNo, const.cTaskRestriction_User, "ASC")              'get uncompleted tasks
   
   ' If TasksComponents.count = 0 Then
   '    Try Btn_Section_Tasks.$hImageNotify.Visible = False
   '    If Error Then
   '       Log.ErrorMsg(Error.Text & " " & Error.Backtrace.Join()) 
   '    Endif
   ' End If   
   Dec Application.Busy
   
End

Public Sub Timer1_Timer()

   

End
