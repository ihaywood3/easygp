' Gambas class file

' Copyright (C) 2008-2011 Dr. Richard Terry

' This program is free software: you can redistribute it and/or modify
' it under the terms of the GNU General Public License as published by
' the Free Software Foundation, either version 3 of the License, or
' (at your option) any later version.

' This program is distributed in the hope that it will be useful,
' but WITHOUT ANY WARRANTY; without even the implied warranty of
' MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
' GNU General Public License for more details.
'----------------------------------------------------------------------
' PURPOSE         A simple manual pregnancy calculator
' HOW THIS WORKS  Note that inserting into notes by tbInsert works by
'                 it's event over-ridden from FClinical
'----------------------------------------------------------------------
Public Sub EditArea_Clear()
   
   modEditAreaHelpers.EditArea_Clear(Vbox_EditArea) 
   
End

Public Function Dates_Summary_GetRichText() As String

   Dim sDatesSummary As String = "<P>" & modHTML.Html_Template_Pregnancy_Calculations()
   sDatesSummary = Replace$(sDatesSummary, "%lmp", txtLMP.text)
   sDatesSummary = Replace$(sDatesSummary, "%gestation", Trim(txtGestation.text))
   sDatesSummary = Replace$(sDatesSummary, "%date_confinement", txtEDC.text)
   sDatesSummary = Replace$(sDatesSummary, "%nuchal_fold_scan", txtnuchalFold.text)
   sDatesSummary = Replace$(sDatesSummary, "%morphology_scan", txtMorphologyScanFrom.text & " - " & txtMorphologyScanTo.text)
   sDatesSummary = Replace$(sDatesSummary, "%anti-d", txtAntiD.text & " at 28 weeks")
   sDatesSummary = Replace$(sDatesSummary, "%glucose_tolerance_test", txtGTT.text & " at 28 weeks")
   Return sDatesSummary & "</P>"
End

Public Sub EditArea_TxtBox_KeyPress()
   
   Dim bkeyvalid As Boolean
   '----------------------------------------------------
   'Each line may have a different set of key exclusions
   'so only keep valid keypresses
   '----------------------------------------------------
   bkeyvalid = EditArea_ExcludeKeys(key.code, Last.tag)
   
   If bkeyvalid = False Then
      Stop Event
      Return
   End If
   Select Case key.Code
      Case key.return, key.Tab
         Select Case Last.tag
            Case "lmp"
               tbInsert.SetFocus()
         End Select
   End Select
   
End

Public Function EditArea_ExcludeKeys(keycode As Integer, tag As String) As Boolean
   '----------------------------------------------------------
   'PURPOSE       Restrict key presses for validation purposes
   'HOW IT WORKS  see routines names
   'FIXME         Ian would do this much simpler I'm sure
   '-----------------------------------------------------------
   
   Dim bKeyValid As Integer
   
   Select Case Last.Tag
      Case "revised weeks", "revised days"
         bKeyValid = modUtil.AllowKeys(const.AllowKeys_NumbersOnly, keycode)
      Case "lmp", "scan date"
         bkeyvalid = modUtil.AllowKeys(const.AllowKeys_DateFormat, keycode) 
   End Select
   Return bKeyValid
   
End

Public Sub EditArea_TxtBox_LostFocus()
   
   Select Case Last.tag
      Case "lmp", "scan date"
         If Last.text <> "" Then
            If Not IsDate(Last.text) Then
               Last.text = ""
               Return
            End If
         End If 
         
   End Select
   
End

Public Sub DateChooser1_Activate()
   
   Last.tag.text = Format(Last.value, "dd/mm/yyyy")
   HBox_Calander.Visible = False  
   DateChooser1.tag.SetFocus()
   
End

Public Sub DateChooser1_LostFocus()
   
   HBox_Calander.Visible = False  
   
End

Public Sub EditArea_TxtBox_Change()
   
   Select Case Last.tag
      Case "lmp"
         If IsDate(Last.text) Then
            txtEDC.text = Format(DateAdd(Val(Last.text), gb.day, 280), "dd/mm/yyyy")
            txtNuchalFold.text = Format(DateAdd(Val(Last.text), gb.week, 12), "dd/mm/yyyy")
            txtMorphologyScanFrom.text = Format(DateAdd(Val(Last.text), gb.week, 18), "dd/mm/yyyy")
            txtMorphologyScanTo.text = Format(DateAdd(Val(Last.text), gb.week, 20), "dd/mm/yyyy")
            txtGestation.text = (DateDiff(Val(txtLMP.text), Now, gb.day) Div 7) & " wks "
            txtGestation.text &= (DateDiff(Val(txtLMP.text), Now, gb.day) Mod 7) & " days"
            txtGTT.text = Format(DateAdd(Val(Last.text), gb.week, 28), "dd/mm/yyyy")
            txtAntiD.text = txtGTT.Text
         Endif
   End Select
   
End

Public Sub tbDatePicker_ReviewDate_Click()
   
   Dim hCtrl As Control
   Dim hTextbox As Textbox
   
   Select Case Last.tag
      Case "pick date"
         If HBox_Calander.Visible Then 
            HBox_Calander.Visible = False
            txtLMP.SetFocus()
         Else
            For Each hctrl In Last.parent.children
               If hctrl Is TextBox Then
                  hTextbox = hctrl
                  DateChooser1.tag = hTextbox
                  Break
               End If
            Next
            With HBox_Calander
               .top = Last.Parent.Parent.top + Last.Parent.Height + panel1.padding + Vbox_EditArea.padding
               .left = Last.Parent.left + panel1.padding + Vbox_EditArea.padding
               .width = 170
               .height = 170
               .Raise()
            End With     
            HBox_Calander.Visible = True 
            DateChooser1.SetFocus()
         End If
      Case "insert"
         
   End Select 
   
End



Public Sub tbEditArea_Click()

   Select Case Last.tag
     Case "clear"
       EditArea_Clear()  
     Case "insert", "insert revised"
         'this is over-ridden by Fclinical as  
     Case "print"
      modUtil.NotImplemented("Printing info for patient")
   End Select

End
